label aonl_day2:
    $ aonl_chapter(2)
    $ persistent.aonl_day2 = True
    $ day_time()
    $ persistent.sprite_time = "day"
    scene bg int_house_of_un_day with fade
    play music music_list["my_daily_life"] fadein 3
    window show
    "Порой случается так, что множество событий, произошедших накануне, изматывают настолько, что когда ты засыпаешь, ты совсем не видишь снов.{w} Так, очередная ночь в твоей жизни проходит бесследно, не оставляя никаких воспоминаний о другом мире, который есть только в твоём воображении."
    "Проснулась я с неохотой.{w} Наверное, то, что из моей памяти вылетели три недели, сказалось на восприятии организмом режима дня, ведь, насколько я помню, пионеры просыпаются рано и тут же приступают к своим обязанностям."
    "То ли дело я: с полузакрытыми глазами, даже не помня, как оделась, волочусь к умывальникам приводить своё бренное тело в чувство."
    "Дома Мику уже не было, вот что значит — настоящий пионер. После её вчерашней говорливости можно было сделать точный вывод о том, что эта девочка полна жизненных сил, с которыми в сердце она рвётся ни свет ни заря по делам."
    "Забавная она, вроде бы и болтушка, и не слишком серьёзная, но заботилась обо мне, как о сестре. Надеюсь, она на меня не обиделась. Будет время — поговорю с ней. А сейчас…{w} Сейчас я должна заставить себя не плюхнуться снова в кровать и дойти до умывальников."
    "Порывшись в тумбочке, я прихватила с собой зубной порошок, мыло и полотенце. На том и решив, я направилась на улицу."
    window hide
    
    scene bg ext_houses_day with dissolve
    play ambience ambience_camp_center_day fadein 2
    window show
    "Признаться честно, я уже и забыла о том, что такое утренняя природа. За городом это улавливалось всегда острее, ведь здесь самый настоящий свежий, чистый воздух."
    "Вдобавок к нежным запахам природы, по пути меня сопровождало пение птиц, а солнце ласкало лучами лицо, будто призывая: «Проснись, Лена, проснись! Утро в самом разгаре, а ты отключаешься на ходу!»"
    "Но, похоже, в моей ситуации помочь проснуться мне могла только холодная вода. Хоть я и не была достаточно закалённой, эту процедуру я считала необходимостью."
    window hide
    
    scene bg ext_washstand_day with dissolve
    window show
    "И так, наслаждаясь звуками природы и пытаясь побороть сонливость, я и добралась до умывальников."
    window hide
    
    scene bg ext_washstand2_day with dissolve
    play sound sfx_open_water_sink
    pause 1
    play sound_loop sfx_water_sink_stream
    window show
    "А теперь предстояло самое сложное.{w} Едва я открыла краны и опустила руки под струю воды, всё моё тело прошила дрожь."
    "Ничего удивительного: резкий спад температуры в теле."
    "Справившись с желанием крикнуть на весь лагерь от ощущения ледяной воды, я несколько раз омыла лицо руками и мылом. И наконец, долгожданная бодрость вернулась ко мне."
    window hide
    stop sound_loop
    play sound sfx_close_water_sink
    pause 1
    
    scene bg ext_houses_day with dissolve
    window show
    "Возвращалась назад я уже быстрой походкой, спать не хотелось. Меня просто накрыло чувство радости и удовлетворения жизнью. Давненько я такого не испытывала."
    "Сказывался одинокий образ жизни и замкнутость. Но на то у меня были свои причины, вспоминать которые сейчас было бы неразумно: это испортило бы моё настроение в секунды."
    show dv sad aonl_sport at cleft
    show us normal sport at cright
    with dissolve
    "Пока я шла домой, я встретила бредущих Алису и Ульяну."
    th "Нет, все же это не я одна такая, они обе тоже были не в форме, казалось, что Ульяна вот-вот вцепится под руку Алисы и задремлет на ходу, хотя и Алиса была готова сделать то же самое."
    th "Представляю, что произойдёт, если они обе отключатся одновременно, надеюсь, не пострадают."
    window hide
    stop music fadeout 3
    
    play ambience ambience_int_cabin_day fadeout 1 fadein 2
    scene bg int_house_of_un_day with dissolve
    play sound sfx_dinner_horn_processed
    pause 1
    window show
    "Вернувшись в домик, я едва хотела задуматься о том, что же дальше делать, как прозвучал звук горна."
    th "Ладно, так и быть, надо так надо, быть может заодно свыкнусь со здешним распорядком."
    window hide
    
    play ambience ambience_camp_center_day fadeout 1 fadein 2
    scene bg ext_houses_day with dissolve
    window show
    "Куда точно надо было идти, я не совсем понимала, поэтому пошла за пионерами. Среди разрозненных кучек пионеров меня окликнула Славя:"
    show sl smile pioneer at center with dissolve
    sl "Доброе утро, Лена. Увидела, как ты растерянно бредёшь за остальными и решила составить тебе компанию."
    "Девочка улыбнулась мне."
    un "И тебе доброе, Славя. А что сейчас будет такое?"
    show sl normal pioneer at center with dspr
    sl "Сейчас общий сбор на площади. Вожатые расскажут своим группам, какой сегодня распорядок дел и что кому нужно будет сделать."
    th "Думаю, это было отличительной чертой Слави: когда она что-то объясняла, её лицо и манера речи напоминали профессора в институте, который с умным видом объяснял предмет студентам. Для ещё большего сходства девочке не хватало только очков, чтобы их поправлять."
    un "Поняла. Как думаешь, мне найдётся что-нибудь? Не хочу быть белой вороной среди всех занятых."
    "Поинтересовалась я."
    sl "Конечно, найдётся, а как же. К тому же, для тебя сегодня будет особое задание. Ты молодец, что рассказала мне о своих увлечениях."
    "Славя искренне улыбнулась, посмотрев на меня."
    th "Нечто особое…{w} Не люблю я эти сюрпризы: чувство неловкости, когда ты оказываешься в эпицентре неожиданностей, всегда было для меня тяжёлым. Чем меньше внимания ко мне, тем лучше."
    "Дальше мы шли молча. Я все пыталась успокоить себя, что ничего плохого не случится и переживать не из-за чего. "
    th "Страшно было представить, что если я не справлюсь с доверенной мне задачей, меня засмеют на весь лагерь…{w} Нет, прочь эти мысли, прочь, всё будет хорошо, только спокойствие."
    window hide
    
    scene bg ext_square_day with dissolve
    play music music_list["get_to_know_me_better"] fadein 3
    window show
    "Построение прошло так же незаметно для меня, как и дорога к площади, пока я занималась внутренним самокопанием."
    show mt normal pioneer at center with dissolve
    mt "Итак, товарищи пионеры! Сегодня в нашем лагере будет проводиться спортивное мероприятие."
    mt "Пионеры для футбола, кросса с препятствиями и настольного тенниса уже распределены. Не найден лишь второй человек для игры в бадминтон. Кто из вас умеет играть, поднимите руку вверх!"
    "Бойко говорила Ольга Дмитриевна."
    th "Бадминтон? Вот он, значит, сюрприз.{w} И хоть я часто играла в него в школе, сейчас я была не уверена, что это хорошая идея, всё же играть на глазах у всех, а вдруг я проиграю?"
    "Сомнения, казалось, затянулись в долгую паузу, остановившую время, будто пока я размышляла, всё вокруг замерло, ожидая моего ответа."
    "И я решилась — взяв волю в кулак, я подняла руку вверх."
    un "Я умею играть."
    "Скромно сказала я."
    show mt smile pioneer at center with dspr
    "Заметившая это вожатая улыбнулась и сказала:"
    mt "Молодец, Лена. Славя говорила мне, что ты умеешь играть, и мы очень надеялись, что ты не откажешься.{w} Ты будешь играть против Алисы. Игра начнётся после футбольного матча."
    "Я молча кивнула в знак согласия. А Ольга Дмитриевна продолжила свою речь:"
    show mt normal pioneer at center with dspr
    mt "А сейчас — все бегом на завтрак, перед играми стоит как следует подкрепиться."
    hide mt with dissolve
    "Ровный строй пионеров мгновенно рассыпался на идущих кто куда детей. Решив, что идти куда-либо, кроме столовой, нет смысла, я направилась прямиком к ней."
    window hide
    stop music fadeout 3
    
    play ambience ambience_dining_hall_full fadeout 1 fadein 2
    scene bg int_dining_hall_people_day with dissolve
    window show
    "В столовой было уже достаточно многолюдно. И я едва-едва заметила свободный столик посреди пионеров."
    "Взяв свою порцию, я уселась на свободное место. Но мне не удалось побыть одной и морально подготовиться к предстоящей игре."
    "Только я собиралась перекусить, как меня окликнул голос:"
    dv "Приятного аппетита. Здесь свободно?"
    show dv smile pioneer at center with dissolve
    "Голос принадлежал Алисе."
    th "Вот же ирония: та, против которой я буду играть, сидит сейчас рядом со мной. Надеюсь, она не поиздеваться пришла."
    un "Приятного. Свободно, присаживайся."
    show dv normal pioneer at center with dspr
    dv "Тебе страшно?"
    "Спокойно спросила она."
    un "Я не знаю, смешанные чувства, я просто не люблю внимания к себе."
    dv "Не дрейфь, никто тебя не обидит, и я тоже, какой бы счёт ни был, кого это волнует? Очередное «задание партии», от которого отвязаться бы поскорее."
    un "Тебе что, совсем не интересна эта игра?"
    dv "Смеёшься? Я играть-то в него начала только потому, что родители настаивали, чтобы я была в форме, считай заставили, собственно как и сейчас вожатая."
    dv "Так что мне бы просто отыграть и всё, а иначе — мне пригрозили, что за мои «шалости» последуют уже серьёзные наказания. Приходится вот отдуваться."
    un "А ты что, любишь вредничать?"
    dv "Ну ты скажешь тоже, вредничать. Имидж у меня такой — Гроза всего Лагеря, вот я и стараюсь на благо этого имиджа: мне он не мешает и с ним мне спокойнее."
    "Что-то в её последней фразе показалось мне очень знакомым: человек, который ищет себе подходящую маску, скрывшись за которой, он себя комфортнее чувствует."
    th "Странно слышать такое от Алисы, но, быть может, мне просто стоит узнать её получше?"
    "Алиса между тем продолжила:"
    dv "И ещё, извини, что вчера я нагрубила, была не в духе немного, да и ты себя необычно вела, не похоже на тебя."
    un "А мы с тобой что, хорошо знакомы разве?"
    th "И вправду интересный вопрос, ведь я ничего не помню."
    dv "Ну, как знать, как знать…"
    "Уклончиво ответила Алиса."
    dv "Ладно, мне пора идти готовиться, удачи тебе на игре."
    un "И тебе тоже."
    hide dv with dissolve
    "Алиса подмигнула и направилась к выходу."
    "В скором времени и я закончила завтракать и последовала её примеру."
    window hide
    pause 1

    play music music_list["orchid"] fadein 3
    window show
    "Но не успела я сделать и пары шагов, как меня одолели сомнения."
    th "Алиса так уверенно говорила, что ей не важно, кто победит, но если я своей победой обижу её? Вдруг она только притворяется, скрывая за уверенностью нежелание проиграть?"
    th "А нужна ли мне эта игра, ведь я могу проиграть и меня поднимут на смех, или Алиса в случае моей победы разозлится на меня?"
    th "Что же мне делать?"
    window hide
    
    menu:
        #TODO: добавить переменные в релизе. Временная мера против тех, кто любит подглядывать в код. Я понимаю, что после релиза всё равно найдутся любопытные варвары, но в демке я не буду указывать явные выборы для получения хорошей концовки. Не портите себе удовольствие от игры, зная правильные выборы заранее.
        "Пойти на игру":
            $ aonl_d2_play = True
            jump aonl_day2_play
        "Убежать, пока не поздно":
            $ aonl_d2_island = True
            jump aonl_day2_island

label aonl_day2_play:
    play music music_list["afterword"] fadeout 1 fadein 3
    window show
    th "Нет уж, хватит убегать, этим я только подведу остальных, ведь все настроились посмотреть игру. Гораздо быстрее меня поднимут на смех, если я убегу. Я должна быть увереннее в своих силах."
    th "А я думаю, что могу доверять Алисе. При разговоре чувствую в ней родственную душу, хоть она и пытается вести себя высокомерно. Не думаю, что она врала мне по поводу своего отношения к игре."
    th "Если я убегу, я могу испортить с ней отношения, нет, я должна идти."
    "Твёрдо решив не увиливать, я направилась к выходу из столовой. Поскольку вот-вот уже должна была начаться игра, я решила не тратить время на хождение по лагерю, сразу из столовой направилась к спортплощадке."
    window hide
    
    play music music_list["went_fishing_caught_a_girl"] fadeout 1 fadein 3
    play ambience ambience_soccer_play_background fadeout 1 fadein 2
    scene bg ext_playground_day with dissolve
    window show
    "На скамейках вокруг площадки уже собирались понемногу пионеры. Я заметила Мику, сидящую в одиночестве, и решила сесть с ней."
    show mi normal pioneer at center with dissolve
    un "Привет, Мику."
    "Поприветствовала я соседку по домику."
    mi "Ой, Леночка, приветик. Как ты, всё у тебя в порядке? Уже пришла в норму? А я вот столько дел на сегодня приметила, что с самого раннего утра на ногах, представляешь? Так устала, что сил нет говорить."
    th "Вот уж в чём она преувеличивала, так это про силы для разговора. Говорила она в своей обычной манере."
    un "Да, все хорошо, спасибо, что беспокоишься. Приятно знать, что здесь есть кому позаботиться обо мне."
    "Поблагодарила я её."
    show mi smile pioneer at center with dspr
    mi "Конечно-конечно! А как же!"
    "Заулыбалась Мику самой искренней улыбкой."
    window hide
    hide mi with dissolve
    
    scene cg d3_soccer with dissolve
    window show
    "Очень скоро начался и матч. Играла команда Ульяны против ребят из других групп."
    "Ульяна носилась по полю как метеор, обходя защиту противника, будто все они были просто статичными фигурами."
    "На деле же за ней никто не мог успеть, девочка явно была создана для того, чтобы убегать. Так что то, что она в итоге разгромила команду противника, при содействии своих сокомандников, не было неожиданностью."
    window hide
    
    $ set_mode_nvl()
    scene bg ext_playground_day with dissolve
    window show
    "Теперь наступало время моей игры против Алисы. Как я ни пыталась справиться с волнением, оно никуда не делось."
    "Наверное, если бы не чувство ответственности, которое было обострено у меня с детства, я бы попросту сбежала. Но сейчас я не могла позволить себе подобного."
    "И поэтому, когда настало время играть, я собралась с духом и сделала первую подачу."
    "Игра выдалась тяжёлой: Алиса была отличной соперницей. То и дело кто-то из нас мог пропустить волан, но ни разу не случалось такого, чтобы кто-то слишком уходил в отрыв по счёту."
    "Играли мы в три сета, после каждого мы брали перерыв, поскольку играть залпом было равно изматыванию вплоть до потери сознания. После двух матчей счёт, как это часто бывает, был равным, решающим оставался последний сет."
    "Было похоже, что Алиса вошла в азарт, не было того наплевательского отношения, которое она показывала, когда мы говорили в столовой. Теперь она была настроена серьёзно, а это значит, что и я должна постараться не ударить в грязь лицом."
    nvl clear
    "Игра была на пределе наших навыков, каждый пропущенный волан мог стать решающим, ведь в такой близкой игре шансы потерять мораль и волю к победе и из-за этого проиграть были очень велики."
    "Спустя 15 минут счёт был 29-30 в мою пользу, забиваю сейчас я — это победа, если нет — у Алисы будет шанс отыграться и забрать победу себе."
    "И вот она: решающая подача. Вдохнув поглубже, я делаю замах — летящий волан вырисовывает чёткую траекторию полёта, которую я очерчиваю взглядом, а после — Алиса неожиданно не отбивает атаку и пропускает волан."
    "Победа за мной. Пожав плечами и улыбнувшись, Алиса молча покинула поле. Не знаю, о чем она думала в тот момент, надеюсь, не злилась на меня."
    window hide
    
    $ set_mode_adv()
    window show
    "В мою же сторону раздались аплодисменты, все кричали:"
    all "Молодец, Лена!"
    all "Отличная игра!"
    all "Лена лучшая!"
    "Мне было неуютно, когда на меня обрушился шквал поздравлений. Да и не считала я, что сделала что-то такое, за что меня нужно так поздравлять."
    "Поздравления нужны только в том случае, если ты делаешь что-то для кого-то другого и тебе это удаётся. А сейчас я, возможно, обидела Алису, и эта буря радости и ликования выглядела как нечто эгоистичное."
    "От таких мыслей мне стало как-то неприятно находиться здесь, и я собралась уйти куда ноги приведут."
    window hide
    
    play music music_list["silhouette_in_sunset"] fadeout 2 fadein 3
    stop ambience fadeout 2
    scene bg ext_square_sunset with fade2
    $ sunset_time()
    $ persistent.sprite_time = "sunset"
    window show
    "Спортивные мероприятия длились почти весь день, а к вечеру у всего лагеря появилось свободное время."
    "Все пионеры разбежались кто куда: кто-то — в какие-то кружки, а кто-то просто убежал в свой домик и занимался своими делами."
    "Но даже издалека была слышна музыка со стороны музыкального клуба. Либо сама Мику, либо она с кем-то играет…"
    window hide

    scene bg aonl_ext_library_sunset with dissolve
    window show
    "Спортивные мероприятия закончились, но они так меня утомили, всё же мне надо немного побыть одной. К тому же, я давно собиралась взять что-нибудь почитать."
    "Искать библиотеку пришлось недолго."
    window hide
    
    $ persistent.sprite_time = "day"
    scene bg int_library_day with dissolve
    play ambience ambience_library_day fadein 2
    window show
    "Внутри библиотеку из окон освещал яркий солнечный свет и был особый запах — запах знаний и книжного дома."
    "От этого запаха у меня пробудились довольно приятные воспоминания, ведь я часто бывала в школьной библиотеке и проводила там довольно много времени. К тому же, многие из книг школьной программы были только там."
    "Я часто приходила в неё просто так, чтобы побыть одной, так как достаточно распространённое выражение «тишина в библиотеке» более чем соответствовало правде."
    "Там можно было побыть вдали от суеты. Ведь книги никогда не были любимым занятием большинства…"
    "Эта библиотека почти ничем не отличалась от школьной: те же полки, тот же читальный зал и места для непродолжительной работы с литературой."
    "Единственное, чем отличалась эта библиотека от многих других — это большим количеством флагов, плакатов и символов социализма."
    "Я начала осматривать полки в поисках какой-нибудь книги, подходящей мне по жанру, но мне попадались лишь идеологические произведения известных революционеров."
    "Ещё здесь сохранилось много старых газет «Пионерская правда», журналов «Перец» и «Крокодил». Надо будет как-нибудь полистать их."
    "Но где же библиотекарь? Я так ничего тут не найду.{w} Но меня отвлёк чей-то шум, вернее сопение."
    window hide
    
    scene cg d2_micu_lib with dissolve
    window show
    "Это и была библиотекарь. Та самая девочка в очках, которая стояла с нами в строю и показалась мне очень умной."
    "Что ж, неудивительно, что она здесь работает.{w} Она мило спала, никого и ничего не замечая вокруг. Даже как-то не хотелось было её будить."
    window hide
        
    menu:
        "Не будить":
            window show
            "Думаю, она скоро проснётся, а я пока постараюсь ещё раз найти себе что-нибудь по вкусу."
            window hide
            
            scene bg int_library_day with dissolve
            window show
            "Полок было немало, и искать нужно внимательно, иначе ничего, кроме идеологической литературы, на глаза не попадётся."
            "Обойдя уже не первую полку, но так и не найдя ничего подходящего, я присела, чтобы немного отдохнуть. Библиотекарь всё ещё не проснулась, а мне становилось скучно."
            "Моё внимание привлекла довольно небольшая книга в сером переплёте. Я взяла её и прочитала название. «Унесённые ветром» Маргарет Митчелл."
            "Это же то, что нужно! Всегда любила почитать романы. Как хорошо, что я её нашла среди всей этой идеологической литературы."
            "Взяв книгу, я поспешила покинуть библиотеку, но книга, как назло, выскользнула из рук и упала на пол, подняв с него пыль.{w} Библиотекарь тут же подскочила и посмотрела на меня."
            show mz bukal glasses pioneer at center with dissolve
            aonl_mz "Хочешь взять книгу?"
            un "Да, хотела бы."
            aonl_mz "Чего не разбудила-то? Давай её мне."
            "Сказала она сонным голосом."
            "Открыв книгу на форзаце и написав там что-то, она отдала её мне."
            show mz normal glasses pioneer at center with dspr
            aonl_mz "Как только прочитаешь, верни."
            un "Хорошо."
            th "Наконец-то, я нашла то, что можно почитать!"
            "Я вышла из библиотеки с большим чувством облегчения."
            window hide
            stop music fadeout 3
            stop ambience fadeout 2
            
        "Разбудить":
            $ aonl_d2_mz_wake = True
            window show
            "Надо её разбудить, вдруг она мне поможет найти что-нибудь интересное, она ведь тут всё знает."
            un "Кхе-кхе."
            window hide
            
            scene bg int_library_day with dissolve
            show mz bukal glasses pioneer at center with dissolve
            window show
            aonl_mz "Что такое?"
            "Внезапно проснулась она. Я даже удивилась немного: она как будто и не спала."
            un "Мне бы…{w} книгу найти подходящую, поможешь мне?"
            show mz normal glasses pioneer at center with dspr
            aonl_mz "А какие жанры тебе по вкусу?"
            "Спросила она, улыбнувшись. Видимо, ей давно не задавали этот вопрос."
            "Мне нравились разные жанры, но больше всего мне приглянулись романы."
            un "Романы."
            aonl_mz "Дай подумать…"
            "Библиотекарь сначала немного подумала, а потом спросила:"
            aonl_mz "Ты читала «Унесённые ветром»?"
            un "Нет, не читала."
            aonl_mz "Значит, тебе точно должно понравиться, я сейчас найду её, подожди немного."
            "Она, обойдя несколько полок, нашла небольшую книгу в сером переплёте."
            aonl_mz "Сейчас, я тебя отмечу, подожди немного."
            un "Хорошо."
            "Открыв форзац книги и что-то написав там, она отдала книгу мне."
            un "Спасибо тебе, что помогла."
            show mz laugh glasses pioneer at center with dspr
            aonl_mz "Пустяки. Уверена, книга тебе понравится. Чего только стоит момент, когда Скарлетт и Батлер…"
            un "Не нужно рассказывать, я сама прочитаю."
            "Перебила я её."
            show mz normal glasses pioneer at center with dspr
            aonl_mz "Хорошо, как скажешь. А как тебя зовут? Просто сюда вообще редко кто заходит, чтобы взять что-нибудь почитать."
            un "Лена."
            aonl_mz "Женя. Рада знакомству."
            un "И тебе спасибо."
            mz "Это моя работа."
            "Взяв книгу и попрощавшись с Женей, я, полная радости от нового знакомства и от того, что нашла хорошую книгу, вышла из библиотеки."
            window hide
            stop music fadeout 3
            stop ambience fadeout 2
    
    scene bg ext_square_sunset with fade2
    play ambience ambience_camp_center_evening fadein 2
    window show
    "Ну и денёк выдался. Я выиграла у Алисы в бадминтон."
    "У меня давно не было таких насыщенных дней и я думаю, этот день в лагере мне запомнится надолго. И ведь на футболе и в моей игре в бадминтон «Зарница» далеко не закончилась…"
    "Дальше был бег, в котором первой, обогнав всех в первые же секунды, прибежала Славя, и ей уступила лишь Ульяна."
    "Остальные состязания мне плохо запомнились или вовсе забылись, так как я, видимо, вместо того, чтобы следить за игрой и болеть за наш отряд, была где-то не здесь."
    window hide

    scene bg ext_square_sunset with fade2
    window show
    "Мои мысли прервал шум ветра и листьев деревьев на пустой вечерней площади. Уже вечерело, а я всё ещё здесь сижу."
    "И я ведь не теряла времени даром, сбегала в местную библиотеку и взяла книгу почитать."
    if aonl_d2_mz_wake:
        "Та девочка-библиотекарь по имени Женя оказалась очень доброй и дала мне книгу «Унесённые ветром», сказав, что я не пожалею, если прочту её."
    else:
        "Найти что-нибудь интересное среди огромного количества «идеологической» литературы было непросто."
    "Однако, она так и лежала не открытой с вечера у меня на коленях."
    "Я уже собиралась уходить, как услышала голос Алисы:"

    play music music_list["afterword"] fadein 3
    show dv grin pioneer at center with dissolve
    dv "Да уж, подруга, удивила ты всех сегодня."
    "Хитро улыбаясь, начала разговор Алиса."
    un "П-подруга?"
    "Говорила она это так, будто мы и правда были давними подругами."
    show dv normal pioneer at center with dspr
    dv "Ах да, ты ведь не помнишь. Хотя, если хочешь, могу рассказать тебе."
    un "Хорошо, о чём ты хотела рассказать?"
    th "Не думаю, что будет правильно сопротивляться тому, чтобы как можно скорее вернуть себе память."
    dv "Мы с тобой, Ленка, подруги с детства, я даже могу сказать — лучшие подруги. Однажды, когда ты была маленькой, мы познакомились с тобой, когда…"
    show dv shocked pioneer at center with dspr
    extend " Впрочем, не так важно; с тех пор мы дружим."
    "Мне точно не показалось: Алиса явно занервничала, как это обычно бывает с человеком, который взболтнул лишнего не подумав."
    un "Когда что? Почему ты запнулась?"
    "Я строго посмотрела на неё. В этот момент она напоминала провинившегося ребёнка."
    dv "Я…{w} Я…{w} Я не думаю, что тебе стоит сейчас это знать…{w} Прости, я сказала лишнее. Вот дура…"
    un "Нет уж, договаривай, если это связано со мной, то я должна знать!"
    "Настаивать я не слишком любила, но сейчас это было важно."
    dv "Но ведь…"
    un "Говори прямо."
    stop music fadeout 3
    show dv sad pioneer at center with dspr
    dv "Ладно…{w} Если ты так хочешь знать. Мы с тобой познакомились в тот день, когда ты потеряла своих родителей…"
    "Меня как молнией пронзило."
    un "Ч-что ты с-сейчас с-сказала?"
    dv "Прости, Лен, но это правда, мне очень жаль…"
    window hide
    stop ambience fadeout 2
    
    scene bg black with fade
    window show
    "В глазах потемнело, едва я услышала её слова.{w} Меня трясло, а после в памяти начали всплывать забытые воспоминания."
    window hide
    pause 1
    jump aonl_day2_flashback
    
label aonl_day2_flashback:
    $ prolog_time()
    scene cg aonl_un_parents
    with fade
    $ persistent.aonl_m_shitsui = True
    play music aonl_mus_shitsui fadein 3
    window show
    "Зимнее утро.{w} В тот день была сильная метель. Мои папа и мама готовились к месячной поездке в другой город по работе."
    "Спешка, снующие туда-сюда взрослые, второпях раскладывающие вещи по чемоданам, а я сижу на стуле, наблюдаю за этим всем."
    "Было весело, когда папа мог случайно опростоволоситься и в спешке ронял что-либо, а после бормотал, как колдун из сказок, какие-то заклинания себе под нос."
    "Спустя час они собрались. Бабушка долго с ними прощалась, а после и я. Выводить меня на улицу не стали: тогда был жуткий мороз, боялись, что я простужусь. Мы обняли друг друга, и на прощание мама сказала мне:"
    aonl_ma "Ленусь, береги себя, мамочка скоро приедет, всего месяц — и мы будем снова с тобой. Не скучай, будь умницей и слушайся бабушку."
    "Видно было, что мама плачет, ей было трудно расставаться со мной."
    aonl_pa "Обещай, что будешь хорошей девочкой. Мы тебя любим, малышка, не скучай."
    "Вслед за мамой попрощался папа."
    "Мы стояли в обнимку все втроём несколько минут. А после они направились к выходу, закрыв за собой двери."
    "Я наблюдала в окно за тем, как они садятся в машину, как та уезжает, постепенно исчезая за утренней мглой."
    "Ещё в тот момент меня охватило странное чувство страха, но тогда я приняла его просто за горечь расставания. Я не могла знать, что это были последние минуты, когда я видела их."
    window hide
    scene bg black with fade2
    
    $ set_mode_nvl()
    scene anim prolog_1 with fade2
    window show
    "Я помню, как незадолго до возвращения родителей бабушке позвонили. Она уронила трубку телефона, закрыв лицо ладонями."
    "Тогда я ещё не понимала, что случилось: ребёнок ведь, наивный малыш, не знающий, что людям в силу их хрупкости свойственно умирать."
    "На мой вопрос о том, что произошло, бабушка сказала, что всё в порядке, родители задерживаются, потому что мама заболела."
    "Я поверила в это, не задумываясь о том, почему бабушка так сильно переживает. Я была всего-навсего лишь маленьким ребёнком."
    "А потом, спустя неделю, я мельком услышала, как бабушка говорила с кем-то по телефону насчёт похорон… Тогда я была растеряна, в глубине души я отказывалась верить, что это произошло с мамой и папой, но я начала понимать, что ещё тогда именно это бабушка и услышала: их больше нет."
    "Они погибли в ДТП, когда возвращались назад; скользкая дорога и пьяный водитель на встречной оборвали их жизни."
    nvl clear
    "В тот день я сбежала из дома на улицу и плакала. От холода слёзы замерзали на лице, но мне было всё равно. Я потеряла самых близких людей в моей жизни."
    "Именно тогда во мне что-то переломилось, я стала совсем другой: равнодушной, ищущей уединения от окружающих девочкой, которая избегала общения со сверстниками и взрослыми."
    "Я старалась чаще быть одна, чтобы никто не видел моих слёз. Пускай думают, что я холодна и равнодушна. Маска — единственный выход, который я видела в том, чтобы скрывать свои чувства от других."
    nvl clear
    "В тот день я рано повзрослела. Бабушка замечала, что я стала говорить и вести себя иначе. Думаю, она понимала, что всему причиной, и не хотела лишний раз трогать незажившую рану в душе."
    "Но тогда же я нашла единственного человека, кто стал мне близким — Алису. Когда я сидела на скамейке и плакала, из всех прошедших мимо людей она была первой, кто подошёл и спросил, что случилось."
    "Я помню, как рассказала ей о своих родителях и о том, что произошло. В ответ она лишь промолчала, а после — достала платок и вытерла мои слёзы. Она подбадривала меня и в какой-то момент сказала:"
    dv "Теперь я буду за тобой присматривать; помни, что куда бы ты ни ушла, я найду тебя, ведь для того и нужны лучшие подруги, правда?.."
    window hide
    stop music fadeout 3
    scene bg black with fade2
    $ set_mode_adv()
    if aonl_d2_island:
        $ day_time()
        $ persistent.sprite_time = "day"
        jump aonl_day2_island2
    else:
        $ sunset_time()
        $ persistent.sprite_time = "sunset"
        jump aonl_day2_evening

label aonl_day2_evening:
    scene bg ext_square_sunset with dissolve
    play ambience ambience_camp_center_evening fadein 2
    show dv normal pioneer close at center with dissolve
    window show
    "Слёзы ручьями потекли из глаз; сидевшая рядом Алиса, обняв меня, пыталась утешить."
    dv "Понимаю, что ты чувствуешь сейчас. Да, я не потеряла родителей, но могу представить, каково это."
    dv "И я всё ещё рядом, как и тогда, когда пообещала, что буду твоей подругой, которая тебя поддержит."
    "Странное это чувство: когда я, потеряв память, увидела её, я совсем не так восприняла Алису, а оказывается, мы были такими близкими подругами. И сейчас, когда мне больно на душе, она рядом."
    un "Спасибо тебе, Алис, за поддержку. Спасибо, что не стала лгать мне и сказала правду о моём прошлом."
    show dv smile pioneer close at center with dspr
    "Алиса улыбнулась."
    dv "Для этого ведь и нужны лучшие подруги, не так ли?"
    un "Наверное."
    dv "Пойдём, доведу тебя до твоего жилища. Не могу же я оставить тебя в такую минуту."
    th "Не стану отказывать ей, ведь она заботится обо мне, а те, кому дорога я, дороги и мне самой."
    un "Пойдём."
    window hide
    
    scene bg ext_house_of_un_day with dissolve
    window show
    "Мы шли не слишком быстро; Алиса молчала, поглядывая на меня. Наверняка беспокоилась, не упаду ли я в обморок. Вскоре мы были уже на месте."
    show dv normal pioneer at center with dissolve
    dv "Лен, ты это, береги себя, не натвори глупостей каких, ложись и отдохни."
    un "Спасибо тебе большое."
    dv "Да о чём ты, подруга, я ничего не сделала такого."
    un "Но всё же я благодарна тебе."
    show dv smile pioneer at center with dspr
    dv "Всё, не смущай, а то расплачусь сама."
    "Алиса хихикнула и, кивнув, направилась к себе домой."
    window hide
    jump aonl_day2_un_house

label aonl_day2_island:
    window show
    th "Нет, я не хочу, ни за что! Если я проиграю, это будет большим позором. Пусть лучше я не приду на игру, чем потом буду терпеть насмешки со стороны."
    th "Я отказываюсь играть. Но если я приду и скажу об этом, меня наверняка заставят. Уж лучше спрятаться подальше от всех в этом лагере."
    th "Не хочу я ни внимания к себе, ни участия в мероприятиях, где много народу. Я привыкла быть одна, и тем лучше, если так оно и будет дальше. Не стоит менять свою жизнь только ради какой-то игры."
    th "В конце концов, кто знает, что могло бы пойти не так, останься я на игру. А так я могу быть точно уверена, что никто не сделает хуже мне и я никому не причиню вреда."
    window hide
    stop music fadeout 3
    
    play ambience ambience_boat_station_day fadeout 1 fadein 2
    scene bg ext_boathouse_day with dissolve
    window show
    "За этими мыслями я и не заметила, как вышла на пристань."
    "Возле неё на воде мирно покачивались привязанные лодки, а вдали виднелся клочок одиночества — остров."
    th "Отличное место, чтобы скрыться от людских взглядов и порицаний. И будь что будет, а сейчас я лучше останусь наедине с собой."
    "Я отвязала лодку от пристани и принялась грести вёслами к острову. Плыть было трудно, но это не шло ни в какое сравнение с тем, что меня могли засмеять. Уж лучше устать физически, чем пережить моральную боль."
    window hide
    
    play ambience ambience_lake_shore_day fadeout 1 fadein 2
    scene bg ext_island_day with dissolve
    window show
    "Так я и оказалась на острове."
    th "Приятное место. Как раз то, что нужно человеку вроде меня. Тишина, шум воды, и нет никого поблизости, кто бы мог потревожить."
    "Наступившее душевное умиротворение начало склонять меня в дремоту."
    "Я не стала сопротивляться этому, ведь глупо редкие моменты спокойствия менять на взбалмошный темп жизни, где постоянно что-то, да происходит."
    th "Так что, что бы там ни происходило в лагере, какое это имеет значение…"
    window hide
    show blink
    $ renpy.pause(1, hard=True)
    window show
    "Я закрыла глаза, отдавшись наслаждению звуками природы."
    window hide
    pause 3
    
    window show
    "Не знаю, как долго я дремала, но меня разбудили приближавшиеся звуки разгребаемой вёслами воды."
    window hide
    hide blink
    show unblink
    $ renpy.pause(1, hard=True)
    window show
    dv "Ле-е-на-а-а-а, ау-у-у-у, ты здесь?"
    "По голосу я узнала Алису. И что она тут забыла, неужели меня искала?"
    un "Я тут, Алиса."
    show dv normal pioneer at center with dissolve
    play music music_list["afterword"] fadein 3
    dv "Вот я тебя и нашла. Ты чего это убежала с игры-то? Что случилось, мы тебя искали по всему лагерю, а ты, оказывается, здесь."
    dv "Хорошо, что один мальчик видел, как ты уплывала, так бы мы тебя точно не нашли."
    "Алиса говорила быстро, то и дело жадно глотая воздух."
    un "Искали?{w} Меня?{w} Вы беспокоились обо мне?"
    "Для меня было необычно видеть Алису такой обеспокоенной."
    dv "Уж не знаю как другим, но мне точно не было всё равно. В конце концов, как я могу наплевать на то, что происходит с моей лучшей подругой детства?"
    "Сказать что я была в шоке — ничего не сказать. Неужели мы с Алисой так хорошо были знакомы? Никогда бы не подумала."
    un "Мы п-п-подруги?"
    "Волнение не дало мне чётко сказать то, что хотела."
    dv "Конечно, уже очень давно, с того самого дня, как…"
    show dv guilty pioneer at center with dspr
    "Алиса запнулась на полуслове. Судя по выражению её лица, она сказала что-то, что не следовало, и меня это заинтересовало:"
    un "Как что? Договаривай!"
    "Строго посмотрела я на подругу."
    dv "Но…{w} Но…{w} Это же…"
    un "Говори прямо!"
    "Алиса сглотнула подобравшийся к горлу комок и сказала:"
    stop music fadeout 3
    show dv sad pioneer at center with dspr
    dv "Мы с тобой подруги с того самого дня, как умерли твои родители, прости, Лена, я не хотела говорить этого."
    window hide
    stop ambience fadeout 2
    scene bg black with dissolve
    window show
    "Меня будто молнией ударило, а после — от волнения потемнело в глазах, а в голове стали проясняться забытые воспоминания."
    window hide
    pause 1
    jump aonl_day2_flashback
    
label aonl_day2_island2:
    scene bg ext_island_day with fade
    play ambience ambience_lake_shore_day fadein 2
    window show
    "Слёзы ручьями потекли из глаз; сидевшая рядом Алиса, обняв меня, пыталась утешить."
    show dv normal pioneer close at center with dissolve
    dv "Понимаю, что ты чувствуешь сейчас. Да, я не потеряла родителей, но могу представить, каково это."
    dv "И я всё ещё рядом, как и тогда, когда пообещала, что буду твоей подругой, которая тебя поддержит."
    "Странное это чувство: когда я, потеряв память, увидела её, я совсем не так восприняла Алису, а оказывается, мы были такими близкими подругами. И сейчас, когда мне больно на душе, она рядом."
    un "Спасибо тебе, Алис, за поддержку. Спасибо, что не стала лгать мне и сказала правду о моём прошлом."
    show dv smile pioneer close at center with dspr
    "Алиса улыбнулась."
    dv "Для этого ведь и нужны лучшие подруги, не так ли?"
    un "Наверное."
    "Мы ещё какое-то время сидели молча и смотрели на воду."
    dv "Уже темнеет, нас уже там заждались, наверно, давай возвращаться."
    "Предложила Алиса."
    un "Ладно, ты права, пойдём."
    window hide
    
    $ sunset_time()
    $ persistent.sprite_time = "sunset"
    play ambience sfx_rowboat_loop fadeout 1 fadein 2
    scene bg black with dissolve
    window show
    "На том мы и договорились; Алиса гребла сама, рассудив, что мне нужно собраться с мыслями."
    "Пока мы плыли, я всё время молчала и думала о родителях."
    window hide
    
    play ambience ambience_boat_station_day fadeout 1 fadein 2
    scene bg aonl_ext_boathouse_sunset with dissolve
    window show
    "Так мы и доплыли назад. Вокруг было тихо: видимо, спортивные мероприятия закончились. Алиса привязала лодку и, взяв меня под руку, повела по направлению к домикам."
    window hide
    
    play ambience ambience_camp_center_evening fadeout 1 fadein 2
    scene bg ext_houses_sunset with dissolve
    show dv normal pioneer at center with dissolve
    window show
    dv "Я сама скажу вожатой, что нашла тебя и привела домой. А ты отдыхай, сейчас для тебя это лучшее, что можно придумать. Только глупостей не натвори, обещаешь?"
    un "Обещаю."
    "Печально ответила я."
    dv "Помни, что где бы ты ни была, я найду тебя и защищу: с тех пор, как мы познакомились, я в ответе за тебя, помнишь?"
    un "Да, спасибо тебе большое, Алиса."
    show dv shy pioneer at center with dspr
    dv "Ой, да не смущай меня."
    "Покраснела Алиса."
    window hide
    
    scene bg ext_house_of_un_day with dissolve
    show dv normal pioneer at center with dissolve
    window show
    dv "Ладно, вот и твой домик, береги себя и постарайся больше так не убегать."
    un "Х-хорошо."
    "Робко ответила я."
    dv "До завтра, подруга, спокойной ночи."
    un "И тебе тоже, спокойной."
    "Алиса подмигнула мне и направилась к домику вожатой."
    window hide
    jump aonl_day2_un_house

label aonl_day2_un_house:
    play ambience ambience_int_cabin_evening fadeout 1 fadein 2
    scene bg int_house_of_un_day with dissolve
    play music music_list["goodbye_home_shores"] fadein 3
    window show
    "Войдя в домик, я никого там не обнаружила, видимо, Мику всё так же была занята своими делами."
    "После произошедшего разговора и возвращения забытых воспоминаний я не могла найти себе место. В голове то и дело возникали образы родителей."
    "Жестоко: я забыла то, что невозможно забыть, и только сейчас начала понимать, что я потеряла."
    "Когда человек теряет кого-то очень ему близкого и дорогого, он проходит через все круги ада, справляясь с болью утраты и с нежеланием жить дальше."
    "Приходится искать в себе силы, чтобы общаться с окружающими, когда это необходимо, выдавливать из себя улыбку и фальшивые эмоции, ведь всё, что ты чувствуешь на душе — это пустота, и в ней нет места для искренних ярких эмоций."
    "Тогда справиться с желанием покинуть этот мир и быть снова вместе с родителями мне помогла Алиса, не будь её рядом, я бы с большой вероятностью придумала бы для себя способ воссоединения с семьёй."
    "А сейчас мне пришлось заново пережить потерю Мамы и Папы. Всё как в первый раз, только я уже не маленькая девочка, и только они всё такие же, как и тогда: улыбаются и обещают, что вернутся ко мне."
    "Но не стоит врать себе, утешая разболевшееся сердце несбыточными мечтами: их нет, и как бы я ни хотела, они не смогут быть рядом со мной."
    "Всё это время я просто смотрела в окно домика, и, видимо, с головой ушла в мысли, а когда снова пришла в себя, заметила на столе листок от альбома и карандаш."
    "Похоже, всё-таки я знаю как сделать так, чтобы родители всегда были рядом со мной. Я взяла в руки карандаш и начала рисовать."
    "Каждый штрих был отражением моей памяти, того, какими я видела своих родителей и какими они навсегда останутся."
    "Когда рисунок был закончен, я улыбнулась, глядя со стороны на то, как они прощаются со мной, как грустят о том, что придётся расстаться со мной, а я тепло обнимаю свою Маму."
    th "Вы всегда будете со мной, Мама и Папа, и пока я живу, этот рисунок будет отражением моих к вам чувств. Как бы мне хотелось быть уверенной, что там, где вы сейчас, вы гордитесь своей маленькой дочуркой и улыбаетесь, смотря за ней."
    window hide
    stop music fadeout 3
    stop ambience fadeout 2
    show blink
    $ renpy.pause(1, hard=True)
    window show
    "Прижав рисунок к груди, я и не заметила, как уснула."
    window hide
    
    $ prolog_time()
    scene bg black
    show prologue_dream
    with fade
    play music music_list["drown"] fadein 3
    window show
    "Снова это тёмное место. Когда я здесь, меня охватывает страх и кровь стынет в теле."
    "Я вдруг понимаю, что за моей спиной что-то происходит."
    window hide
    
    scene cg aonl_un_parents
    show prologue_dream
    with dissolve
    window show
    "Обернувшись, я увидела маленькую себя из детства, а рядом были родители, точь-в-точь как тогда. Это было похоже на экранизацию моих воспоминаний, а я была зрителем в кинотеатре."
    "Но я ведь знаю, что будет потом, может, это шанс всё исправить?"
    un "Стойте, прошу вас! Не ходите, мама, папа, пожалуйста!"
    "Кричала я в слезах."
    un "Не оставляйте меня одну, только не снова, я не хочу снова пережить это! Остановитесь!"
    "Они не слышали меня; всё шло как в киноленте, где зритель не может влиять на события, задуманные режиссёром."
    "Упав на колени, я рыдала, слёзы лились ручьём."
    th "Почему? Почему я снова вижу это, зачем надо мной так издеваются?!"
    aonl_ma "Ленусь, береги себя, мамочка скоро приедет, всего месяц — и мы будем снова с тобой. Не скучай, будь умницей и слушайся бабушку."
    "Видно было, что мама плачет, ей было трудно расставаться со мной."
    aonl_pa "Обещай, что будешь хорошей девочкой. Мы тебя любим, малышка, не скучай."
    "Вслед за мамой попрощался папа."
    "Эти до боли знакомые голоса и фразы, ставшие для меня предзнаменованием беды."
    un "Прошу, не уходите… Вы нужны мне…"
    "Шептала я, голос пропал от волнения."
    window hide
    
    scene bg black
    show prologue_dream
    with dissolve
    window show
    "Мама с папой уходили вперёд во тьму. Не выдержав, я побежала к ним, пройдя сквозь свой детский образ, но когда я добежала, они рассыпались на множество блестящих капель слёз, с каждой секундой исчезая…"
    "А после, под оглушающий грохот, будто кто-то ударил по листу железа, во тьме появилась надпись:"
    window hide
    $ aonl_save_future = "{size=100}ПРОШЛОЕ НЕ ИЗМЕНИТЬ,\nНО МОЖНО СПАСТИ БУДУЩЕЕ.{/size}"
    show aonl_credits aonl_save_future behind prologue_dream at truecenter with dissolve
    $ renpy.pause(5, hard=True)
    stop music fadeout 3
    scene bg black with fade
    pause 1
    jump aonl_day3
    
