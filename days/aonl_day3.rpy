label aonl_day3:
    $ aonl_chapter(3)
    $ persistent.aonl_day3 = True
    $ day_time()
    $ persistent.sprite_time = "day"
    scene bg int_house_of_un_day with fade
    play music music_list["sweet_darkness"] fadein 3
    play ambience ambience_int_cabin_day fadein 3
    window show
    "Начало нового дня выдалось не из приятных."
    "Как это обычно бывает, когда человек переживает сильный стресс в предыдущем дне, чувства, беспокоившие тогда, находят отклик в начале нового дня."
    "Едва проснувшись, я вспомнила вчерашний сон.{w} Всё это было похоже на злой умысел. Дважды за день я сперва наяву, а затем во сне пережила один и тот же момент."
    "И ещё это странное тёмное место. Всё выглядело так, будто меня предупреждают. Эти странные слова…"
    "Когда я увидела их в первый раз, как и любой здравомыслящий человек, я списала это на бред воображения во сне."
    "Но сейчас…{w} Мне кажется, что это всё имеет смысл и не стоит забывать все эти фразы, которые мне снятся. Пожалуй, стоит записывать их."
    stop music fadeout 3
    "Идея пришлась мне по нраву. Чтобы быть уверенной, что я могу сохранить это в тайне, я решила записывать их в свой личный дневник."
    window hide
    
    scene cg aonl_un_diary with dissolve
    window show
    un "Угум-с, так значит, что там было первым?"
    "Бормотала я себе под нос вполголоса. Это было моей чертой характера: когда я усердно пыталась вспомнить что-то, будучи в одиночестве, так как со стороны это выглядит как минимум странно, я могла нашёптывать мысли вслух, это помогало лучше сосредоточиться."
    un "Ага, кажется, «Выбирайся», записала. А какая же вторая фраза?  Чёрт, не могу вспомнить…"
    scene cg aonl_un_diary_evil with dissolve
    aonl_voice "Пиши…{w=1} пиши…"
    scene cg aonl_un_diary with dissolve
    un "Кто здесь?"
    "Я бегло огляделась — в комнате никого не было."
    un "Видимо, показалось. Наверное, утренний ветерок."
    scene cg aonl_un_diary_evil with dissolve
    aonl_voice "Пиши…{w=1} пиши…"
    scene cg aonl_un_diary with dissolve
    th "Второй раз.{w} Это уже не смешно."
    "По моему телу пробежала дрожь, а по голове как будто ударили чем-то тяжёлым. Встав и снова, на этот раз внимательно осмотрев комнату, я ничего не обнаружила."
    un "Да что же это? Мне не могло показаться дважды."
    "В любом случае, надо постараться как можно скорее вспомнить вторую фразу."
    "Но внезапно, бусами всплыли в голове и снова собрались в предложение. «Прошлое не изменить, но можно спасти будущее»."
    "Наскоро записав фразу и закончив личную летопись, я спрятала дневник в тумбочку. В школу я его обычно таскала с собой и писала, как прошёл мой день: свои мысли, чувства и важные дела, чтобы не забыть о них."
    "Но в лагере практически нет времени на записи в нём, да и стащить кто-нибудь может. Поэтому я прятала его от греха подальше.{w} Он слишком много значил для меня, будучи практически единственным, кому я могла доверить собственные переживания."
    "И пусть в домике я жила не одна, я верила, что Мику не будет рыться в моих вещах. К тому же, о существовании моего дневника больше никто не знает. И не узнает…"
    "Мой личный внутренний мир, в нём есть место только для меня. Однажды я уже обожглась на доверии, больше не допущу подобного."
    window hide
    scene bg int_house_of_un_day with fade2
    
    window show
    "Я уже собиралась пойти на утренние водно-умывальные процедуры, но, взглянув в сторону окна, закрытого занавесками, увидела чей-то силуэт, который вдруг растворился, как дым на ветру."
    th "Что ж, если ещё раз увижу, что кто-то подглядывает, ему не поздоровится. Нечего шастать по лагерю и за девочками шпионить!"
    play sound sfx_knock_door2
    "Пока я, аки римский полководец, произносила воинственные речи у себя в голове, в дверь домика постучали."
    un "Открыто!"
    "Гостем оказалась Славя. Не знаю, как ей это удаётся, но она выглядела бодро и свежо, будто и не просыпалась, как все, не так давно с утра пораньше."
    show sl smile2 pioneer at center with dissolve
    sl "Привет, Лена, как спалось?"
    un "Можно сказать, что не слишком плохо, а тебе как?"
    sl "Отлично, после вчерашних дел и забот я спала так, будто весь день вёдра с водой таскала."
    "Похоже, Славя не унывала ни на секунду. Всегда, когда я её видела, она была весёлая и добродушная."
    show sl normal pioneer at center with dspr
    sl "Лен, у тебя ведь никаких планов на ближайшие несколько часов не было?"
    un "Да как-то не успела подумать об этом, а что такое?"
    sl "Ольга Дмитриевна назначила меня и тебя вместе с несколькими ребятами из других отрядов на подготовку полянки в лесу для вечернего похода. Будем песни у костра петь, ну ты, думаю, и сама знаешь об этой традиции, не так ли?"
    "По правде говоря, я смутно припоминала, что там делают в этих пионерских походах, но решила не задавать лишних вопросов, всё увижу на месте."
    un "Да, знаю. И что нужно нам делать?"
    sl "Тебе нужно сходить умыться и привести себя в порядок, а я в это время соберу остальных ребят. Встречаемся у домика Ольги Дмитриевны."
    un "Договорились, ну пойдём тогда кто куда?"
    "Улыбнулась ей я, попутно прикидывая, какими делами одарит нас вожатая, особенно если вспомнить то, что последние два дня я не слишком-то и трудилась на благо Отечества."
    show sl smile pioneer at center with dspr
    sl "Пойдём! Родина ждёт детей своих!"
    "Улыбнувшись, чётко проговорила Славя."
    "Похоже, патриотизм у неё был на самом высоком уровне из всех пионеров."
    if aonl_d2_island:
        "Мы договорились о месте встречи, которое изменить нельзя, а если кто-то изменит, будут потом этого кого-то искать по всему лагерю, пока тот будет мирно отлёживаться на острове."
        th "Хорошее место, почаще бы там бывать, особенно в моменты, когда хочется бросить всё и сбежать от людей."
        "От собственных мыслей мне и самой стало смешно."
        th "Интересно, когда я стала такой позитивной? Похоже, природа меняет людей."
    else:
        "Мы договорились о месте встречи, которое изменить нельзя."
    window hide

    play music music_list["everyday_theme"] fadeout 1 fadein 3
    play ambience ambience_camp_center_day fadeout 1 fadein 2
    scene bg ext_houses_day with dissolve
    window show
    "Утро выдалось приятно прохладным. Вместе со свежестью утренней природы на душе было легко и спокойно, единение с природой дарило минуты счастья для такой неприметной и тихой девочки, как я."
    "Это позволяло забыть хотя бы на какое-то время о том, что в моей жизни, вне лагеря, поводов для радости кот наплакал."
    "Вот бы скрасить эти моменты мимолётной радости ещё и чувством, что я не одна и есть кто-то близкий — человек, с которым каждая минута станет драгоценным камнем, которому нет цены."
    "Как жаль, что мечты мечтами, а в реальности ты получаешь лишь мимолётное счастье, когда рассчитывать на что-то постоянное не приходится."
    show dv normal pioneer at cleft
    show us normal pioneer at cright
    with dissolve
    "Пока я шла к умывальникам, по дороге я встретила ковыляющих сомнамбул Алису и Ульяну."
    th "Да, им до энергичности Слави далеко, как Меркурию до Марса. Наверняка каждое утреннее пробуждение для них было той ещё пыткой."
    "В ответ на моё приветствие Алиса только что-то сонно пробубнила, а Ульянка, видимо, вообще не услышала ничего: дремала буквально на ходу."
    window hide
    
    scene bg ext_washstand_day with dissolve
    window show
    "Добравшись до умывальников, мне предстояла непростая задача: укротить холодную воду, не дав ей заставить меня закричать на весь лагерь от ледяного прикосновения воды к коже. Спасало меня то, что я была терпеливой с детства."
    "Потому, пережив ледяную ванну и ледовую битву зубного порошка с полостью рта, я, как стойкий солдат, с чувством выполненного долга и с выражением лица, отражающим фразу «не трогайте меня, я с поля боя», направилась обратно в домик."
    "Было бы не слишком разумно тащить умывальные принадлежности в лес. Закончив с делами, я наконец была готова к труду и обо… то есть походу."
    window hide
    
    play music music_list["reminiscences"] fadeout 1 fadein 3
    scene bg ext_house_of_mt_day with dissolve
    show sl normal pioneer at center with dissolve
    window show
    "Довольно быстро я добралась к домику вожатой. Славя и ещё несколько пионеров из младших отрядов уже ждали меня. Мне стало неловко от того, что я заставила всех ждать."
    un "Простите, что пришлось дожидаться меня."
    "Сказала я с привычной для себя ноткой неуверенности и робости."
    sl "Ничего страшного, Ольга Дмитриевна ещё не пришла, а нам надо её дождаться, чтобы получить указания работ."
    un "Ну, тогда ждём."
    "Улыбнулась я. Тем ребятам из младших отрядов, похоже, вообще было не интересно слушать наш разговор, поэтому они болтали о чём-то своём, не обращая на нас внимания."
    hide sl with dissolve
    "Прошло около трёх минут, и Ольга Дмитриевна подошла к нам, видимо, закончив раздавать поручения остальным."
    show mt normal pioneer at cleft
    show sl normal pioneer at cright
    with dissolve
    mt "Итак, пионеры! Ваша задача — подготовить поляну к вечернему походу: соберите хворост для костра, приберитесь, чтобы было чисто и опрятно; так же, как должен выглядеть настоящий пионер, должна выглядеть и поляна."
    mt "Инвентарь возьмёте в подсобке. Славя, ключи у тебя с собой?"
    sl "Да, Ольга Дмитриевна."
    mt "Вот и славно, как закончите, доложите мне."
    all "Сделаем!"
    "Хором ответили пионеры."
    window hide
    
    scene bg aonl_ext_shed_day with dissolve
    window show
    "Цель была дана, оставалось её выполнение. Довольно быстро мы пришли к подсобке."
    "Помучившись с замком, как Жорж Милославский в фильме «Иван Васильевич меняет профессию», Славе наконец удалось отпереть закрома."
    window hide
    stop ambience fadeout 1
    
    scene bg int_clubs_male2_night_nolight with dissolve
    window show
    "Из темноты доносились стук и шебуршание: Славя усердно пыталась что-то найти в темноте."
    sl "Лена, подойди сюда, нужна твоя помощь."
    "Окликнула меня труженица."
    sl "Вот, держи фонарик, посвети в дальний угол."
    "Послушно выполнив её просьбу, я направляла луч вперёд, заставляя темноту расступаться под напором света. Вскоре Славе удалось собрать все необходимые принадлежности и распределить их между всеми."
    "Она, на этот раз с лёгкостью закрыв замок, скомандовала нам идти вперёд. Разойдясь по парам, я пошла вместе со Славей."
    window hide
    
    play music music_list["afterword"] fadeout 1 fadein 3
    scene bg ext_path_day with dissolve
    play ambience ambience_forest_day fadein 2
    window show
    "Поляна находилась в глубине леса, и мы, стуча граблями и вёдрами, направились туда."
    show sl normal pioneer at center with dissolve
    sl "Ну как тебе в лагере, уже освоилась?"
    "Первой разговор решила начать Славя."
    un "Да, мне нравится здесь, уже приметила для себя любимые места, да и вспомнила некоторые моменты, можно сказать, прихожу в норму."
    th "Надеюсь, она не начнёт расспрашивать о том, что же я вспомнила: меньше всего хотелось заводить разговор об этом."
    sl "Это, безусловно, хороший признак.{w} Кстати, я так понимаю, вы с Алисой сдружились. Никогда бы не подумала, что такие, как вы, можете дружить."
    un "Противоположности притягиваются."
    th "К чему она клонит вообще? И что значит — такие, как мы? Надеюсь, это не что-то плохое."
    sl "В вашем случае я этого ожидала меньше всего. Знаешь, по статусу помощницы вожатой мне положено знать всё и про всех…"
    th "Прямо так и положено. Очень сомневаюсь, что это так. Наверное, просто привыкла следить за обстановкой в лагере и сама не замечает, как порой даёт про себя характеристики окружающим людям."
    un "Может, тебе самой нравится знать всё про каждого? Чрезмерное любопытство — не очень хорошая черта…"
    sl "Ну как тебе сказать: я в школе с третьего класса староста, и потому я привыкла быть ответственной, а для поддержания порядка лучше знать о коллективе как можно больше, чтобы в случае чего вовремя отреагировать."
    sl "Это совсем не любопытство, не говори так."
    th "Кажется, она немного обиделась. Ответы были череcчур резкими: всё-таки она была добра ко мне, пусть мне и не нравится то, как она думает об Алисе, не стану я с ней портить отношения."
    "Я решила перевести тему."
    un "Тяжело тебе, наверное, в твоей нелёгкой работе."
    sl "И не говори, бегаю, как ураган, туда-сюда, ответственность и разгильдяйство — вещи несовместимые. А я, как помощница пионервожатой, должна быть примером для остальных.{w} Вот бы все были такими, как я."
    "Про себя я усмехнулась."
    th "Да не дай бог таких, как ты, весь лагерь."
    "В голове сразу возникла картина: я одна стою в строю, полном одних Славь, и меня заваливают поручениями."
    sl "Лена, ау, ты в какие фантазии провалилась? Я тебя зову-зову, а ты просто молчишь, глядя в одну точку."
    "Похоже, моя разыгравшаяся фантазия выдернула меня из реального времени, отправив сознание в глубины воображения."
    un "Ой, прости, я тут мечтала про море, солнце, рыбок в воде…"
    "Не знаю, насколько глупо я выглядела, но это первое, что я нашлась ответить."
    show sl laugh pioneer at center with dspr
    "Славя засмеялась."
    th "Да, я точно выглядела глупо."
    sl "Ну ничего, все мы иногда мечтаем."
    un "Иногда так приятно в мечтах оказаться там, куда больше всего хочешь попасть наяву."
    "Сказала я серьёзно."
    show sl sad pioneer at center with dspr
    sl "Твоя правда. Жаль, что иногда мечты — это только мечты."
    "Славя заметно загрустила."
    "Странная она всё же. Как ни посмотришь, всё у неё уже есть, что нужно человеку, но даже такая, как она, о чём-то, да грустит."
    "Наверное, потому абсолютно счастлив тот, кто либо уже умер, либо ему плевать на всех и он живёт только ради собственного удовольствия."
    window hide

    play music music_list["she_is_kind"] fadeout 1 fadein 3
    scene bg ext_polyana_day with dissolve
    window show
    "Меж тем мы пришли. Не успела я это понять, как Славя начала разъяснять нашей группе, что нужно делать."
    show sl normal pioneer at center with dissolve
    sl "Вот мы и на месте. Так, ребята, вы тащите сухие ветки и всё, что хорошо будет гореть. Лена, мы с тобой приберём здесь."
    hide sl with dissolve
    "Мальчишки из младших отрядов разбрелись кто куда, в то время как Славя энергично принялась за уборку поляны, дабы привести её в надлежащий вид."
    "Что ни говори, а работу она любила. Мне не оставалось ничего, как последовать её примеру."
    "Не скажу, что работа была тяжёлой: надо отдать должное, а это место, как и весь остальной лес, как я успела увидеть по пути сюда, было ухожено и чисто."
    "Тем не менее, завтрак мы всё же пропустили. Впрочем, это даже хорошо: сейчас наработаюсь, а потом можно будет здорово подкрепиться обедом в столовой."
    "Работали мы усердно, и вскоре поляна была ухожена, а ребятня из младших отрядов натаскала веток. Из них Славя соорудила кострище, а я помогла ей, обложив его вокруг камнями."
    "Кое-как до обеда мы справились, пора было возвращаться."
    show sl normal pioneer at center with dissolve
    sl "Ну вот и всё, мы молодцы, работа выполнена, возвращаемся. Сперва сдадите инвентарь у подсобки. А после все идём доложить Ольге Дмитриевне."
    window hide
    
    scene bg ext_path_day with dissolve
    window show
    "Построившись опять так же, мы двинулись по пути назад."
    show sl normal pioneer at center with dissolve
    sl "Славно поработали, труд — дело благородное!"
    "Гордо сказала Славя."
    th "Опять она за своё, только бы она снова не начала произносить эти свои речи!"
    un "Да, хорошо, что всё обошлось без инцидентов."
    "Решила я хоть как-то поддержать разговор."
    show sl surprise pioneer at center with dspr
    sl "А? Что могло с нами случиться?"
    "На удивлённом лице Слави можно было прочесть испуг."
    un "Пустяки, не обращай внимания, я глупость сказала."
    "Вот только глупость ли? Не знаю почему, но мне стало не по себе, будто кто-то наблюдает за мной, точь-в-точь как сегодня утром."
    show sl normal pioneer at center with dspr
    sl "Не бойся, здесь в лагере тебе ничто не угрожает."
    "Славя снова говорила обычным тоном."
    un "Х-хорошо."
    "Я попыталась отогнать неприятное чувство."
    window hide
    stop ambience fadeout 1
    
    scene bg aonl_ext_shed_day with dissolve
    play ambience ambience_camp_center_day fadein 2
    window show
    "Тем временем мы уже подходили к подсобке. Вот мы уже стояли около дверей, наблюдая за попытками Слави взломать замок. Зрелище в какой-то степени забавляло."
    "Удивительно, что она держалась спокойно, хотя казалось, что вот-вот она бросит ключи на землю и начнёт орать на замок, что он никак не даёт открыть себя."
    "К сожалению или к счастью, но этого не понадобилось, поскольку Славя всё же открыла двери."
    show sl normal pioneer with dissolve
    sl "Так, давайте мне инвентарь, по очереди, Лена, сперва ты."
    hide sl with dissolve
    "Послушно я передала ей грабли и ведро, ребята сделали то же самое. Немного погромыхав, Славя показалась из темноты и скомандовала нам идти к вожатой."
    play music music_list["you_lost_me"] fadeout 1 fadein 3
    show sl normal pioneer with dissolve
    sl "Да, кстати, пока мы все не разошлись кто куда. Я ведь совсем забыла тебя предупредить. Старайся поменьше общаться с Алисой, она до добра не доведёт, скорее наоборот."
    "А вот тут Славя перешла черту, за которую заходить не следовало."
    un "Славя, ты, конечно, не обижайся, но с кем я общаюсь и как, тебя не касается. Ты не знаешь Алису столько, сколько знаю я, и мне очень неприятно, что ты за спинами других говоришь такие вещи. Я, пожалуй, пойду."
    hide sl with dissolve
    "Я покинула Славю и направилась к домику Ольги Дмитриевны."
    th "Не люблю я ссориться с людьми, но Славя явно переступает черту дозволенности, не стоит ей говорить такие вещи про мою единственную подругу."
    window hide

    scene bg ext_house_of_mt_day with dissolve
    window show
    "Преследуемая такими мыслями, а также желанием перекусить после работы, я пришла к Ольге Дмитриевне."
    show mt surprise pioneer at center with dissolve
    mt "Тихонова? А где все остальные?"
    "Она явно не ожидала увидеть меня одну."
    un "Они скоро подойдут, я просто решила поскорее прийти, мы всё сделали, Славя вам всё доложит, а теперь я могу пойти?"
    mt "Эм, ну… Иди, да…"
    "Судя по всему, она была в ступоре от моего поведения."
    th "Пусть уж лучше так, чем если Славя при мне что-то ей на меня скажет и произойдёт ЧП по меркам лагеря — драка."
    window hide
    stop music fadeout 3
    
    scene bg ext_washstand_day with dissolve
    window show
    "Я направилась к умывальникам умыться и привести себя в порядок."
    play sound sfx_dinner_jingle_normal
    "Едва я закончила водные процедуры, звук горна уже созывал пионеров на обед. Что ж, не вижу причин медлить с этим."
    "Наверное, это голод придал мне сил и я добралась до столовой настолько быстро, что опередила большую часть пионеров."
    window hide
    stop sound fadeout 1
    stop ambience fadeout 1
    
    scene bg int_dining_hall_people_day with dissolve
    play ambience ambience_dining_hall_full fadein 2
    window show
    "Вместо обычной шумной толпы за столами я застала всего несколько человек. Выбор, куда сесть, был как никогда разнообразен, и я уже хотела занять место в дальнем углу, но меня окликнул знакомый звонкий голос:"
    mi "Леночка, иди сюда, я тут, сзади, садись со мной, у меня свободно."
    "Это была Мику, как и всегда жизнерадостная и весёлая; улыбаясь, она махала мне. Я не была против её компании, всё же живём вместе и я, как никто другой, привыкла к её говорливости."
    show mi normal pioneer at center with dissolve
    play music music_list["so_good_to_be_careless"] fadein 3
    un "Привет, приятного аппетита, Микуль."
    th "Не стоит из-за инцидента со Славей грубить с ней, буду вести себя как обычно, пусть не беспокоится обо мне."
    mi "Привет-привет. Как ваш поход в лес, всё хорошо было?"
    "И снова здравствуйте. Мику начала речь со своей привычной скоростью."
    un "Всё в целом хорошо, ты же знаешь, как я люблю природу."
    "Как бы мне ни хотелось, а говорить с ней на равных я никогда не смогу."
    show mi smile pioneer at center with dspr
    mi "Вот как, здорово, что тебе понравилось. А я собралась репетировать одну песенку, но не знаю, кого пригласить послушать. А может, ты согласишься? Я уверена, тебе понравится! А мне не будет скучно одной, к тому же мы ведь подруги."
    window hide
    
    menu:
        "Согласиться не раздумывая":
            window show
            th "А что я теряю-то, почему бы и не послушать: если это того стоит, я и правда не пожалею."
        "Сомневаться, нужно ли мне это?":
            window show
            un "Ну, я не знаю, я хотела немного отдохнуть."
            th "Нужно ли мне вообще это?"
            show mi normal pioneer at center with dspr
            mi "Ну пожалуйста, пожалуйста, ты отдохнёшь. Там хорошая музыка, правда-правда, соглашайся, ты не пожалеешь."
            
    un "Хорошо, пойдём, я не против насладиться музыкой."
    show mi happy pioneer at center with dspr
    mi "Славненько, как пообедаем, зайдём к нам, я оставила там запись этой песенки, взяла с собой сюда в лагерь, и вот не было кому её спеть, спасибо тебе, что согласилась."
    "Мику улыбнулась самой искренней улыбкой."
    un "Ну тогда не будем медлить и побыстрее покончим с обедом."
    "Улыбнувшись ей в ответ, сказала я."
    window hide
    stop music fadeout 3
    
    show cg d1_food_normal with dissolve
    window show
    "На обед было картофельное пюре, котлета и чай. Всё это весьма приятно пахло, и я принялась радовать свой желудок и себя саму вкусной пищей."
    "Пока я ела, мне в голову пришла одна мысль: что если я захвачу к Мику с собой свою планшетку и нарисую её, когда она будет петь?{w} Это будет моим ей «спасибо» за чудесную песню."
    "Почему я так уверена, что песня будет красивой?{w} Это же Мику, в её музыкальном вкусе можно не сомневаться, она не подведёт. Так что, я думаю, от моего подарка ей будет приятно."
    "Наверное, стоит побольше с ней общаться: если подумать, она всегда одна в своём кружке, а музыку я люблю; хоть и способностей её исполнять у меня никогда не наблюдалось, но если песня хорошая, она непременно принесёт положительные эмоции."
    window hide
    
    scene bg int_dining_hall_people_day with dissolve
    window show
    "За этими мыслями мы вскоре уже отобедали. Мику была быстрее, кто бы сомневался. По части скорости быстрее неё разве что Ульяна может быть, когда бегает."
    "Теперь, когда голод больше не беспокоит, можно было идти к Мику."
    show mi normal pioneer at center with dissolve
    un "Сначала домой, да?"
    "Нужно было уточнить, пока Мику не вспомнила ещё кучу своих дел и не устроила нам обход по всему лагерю."
    mi "Да, возьмём всё, что нужно, и тогда ко мне в кружок."
    un "Пойдём тогда."
    "Не спеша мы направились к себе домой."
    window hide
    stop ambience fadeout 1
    
    scene bg int_house_of_un_day with fade2
    play ambience ambience_int_cabin_day fadein 2
    window show
    "Войдя внутрь, Мику принялась рыться в вещах, а я разыскала планшетку и карандаш."
    show mi surprise pioneer at center with dissolve
    mi "Ой, а зачем это тебе, Лен?"
    "Удивилась Мику."
    un "Всему своё время, увидишь потом."
    "Хитро ответила я."
    show mi happy pioneer at center with dspr
    mi "Хи-хи, ладно, сюрприз так сюрприз! Люблю сюрпризы!"
    "Запрыгала, хлопая в ладоши, Мику."
    "За сим мы переглянулись и вышли из домика."
    window hide
    stop ambience fadeout 1
    
    scene bg ext_house_of_un_day with dissolve
    play ambience ambience_camp_center_day fadein 2
    show mi happy pioneer at center with dissolve
    window show
    mi "А теперь ко мне в кружок. Я так рада, что у меня будет слушатель, и вдвойне рада, что это будешь ты — моя подруга и соседка по домику. Столько много энергии, а куда её девать, если я почти всегда сижу одна, наигрывая разные мелодии?"
    "Мику просто сияла. Вот как мало нужно порой человеку, чтобы осчастливить его."
    mi "Я буду петь одну такую милую песенку, очень люблю её. Она такая красивая и ритмичная."
    un "Буду рада послушать."
    "Пока Мику рассказывала, о чём пелось в этой песне, мы как раз подошли к клубу."
    window hide
    stop ambience fadeout 1
    stop music fadeout 3
    
    scene bg int_musclub_day with dissolve
    play ambience ambience_music_club_day fadein 2
    show mi normal pioneer at center with dissolve
    window show
    mi "Вот мы и пришли, заходи. Осторожно, в углу тромбон. Его часто роняют, так что не споткнись об него."
    "Надо признать, предупредила она вовремя. Музыкальный инструмент стоял так, что обойти его не задев было крайне сложно. Ещё бы секунда, и он с грохотом рухнул бы на пол."
    "Клуб был не слишком просторным, поэтому музыкальные инструменты стояли довольно близко друг к другу. Но именно это место стало для нашей болтушки Мику домом родным. Вот что значит — нашла своё призвание."
    "Удивительно было то, что в этой тесноте она порхала, как бабочка, грациозно обходя препятствия. Наверняка, если ей завязать глаза, эффект был бы такой же."
    mi "Вот, присаживайся здесь."
    "Мику указала на стул, любезно убранный от нот и прочих мелочей."
    un "Мику, а ты сама играть будешь или петь под запись?"
    "Поинтересовалась я."
    "Мику взглянула на меня полными печали глазами."
    show mi sad pioneer at center with dspr
    mi "Эх, Леночка, как бы я хотела исполнить эту и другие песни на живом концерте, но мало кто соглашается играть на необходимых инструментах. Пока есть только Алиса, она на бас-гитаре играет, ещё бы двоих ребят на ударные и клавишные найти."
    mi "С этим Алиса обещала помочь. Быть может, если всё получится, сделаем концерт перед отъездом. Но это пока только мечты, сейчас я могу петь такие сложные по структуре композиции только под запись."
    "Мне было неловко, что я не могу ей помочь, но я не стала давать ей нереальных обещаний."
    un "Прости, Микуль, я не умею играть на ударных или клавишных, да и вообще к музыке у меня нет способностей."
    show mi smile pioneer at center with dspr
    mi "Ничего, Лен, я понимаю, у тебя талант к рисованию."
    "Мику снова засияла."
    th "Надеюсь, она не станет обижаться на меня за отказ."
    "Немного повозившись с питанием, Мику подключила магнитофон к электричеству и, вставив аудиокассету, включила песню."
    window hide
    stop ambience fadeout 2
    scene cg d4_mi_sing with dissolve
    $ persistent.aonl_m_hazy_moon = True
    play music aonl_mus_hazy_moon fadein 3
    window show
    "Из динамиков зазвучала приятная и бодрая мелодия, и с первыми аккордами Мику начала напевать текст на японском."
    "Музыка сливалась с её голосом в унисон, создавая эффект, будто Мику смогла попасть прямо на запись песни."
    "Такая приятная и жизнерадостная мелодия наполняла теплом моё сердце, пробуждая обычно дремлющие в глубине души чувства: радость жизни, уверенность в будущем и мимолётное счастье."
    "Такое чисто человеческое счастье, которое будет длиться ровно до тех пор, пока последние ноты мелодии не затихнут, погрузив клуб в тишину."
    window hide
    
    scene cg aonl_un_paints_mi with dissolve
    window show
    "Захватившие меня в вихрь эмоций музыкальные ритмы, казалось, завладели моим телом, и я, взяв карандаш, начала отрисовывать линию за линией, превращая скопление чёрточек и изгибов в портрет поющей Мику."
    "С каждым новым куплетом всё больше черт начинало походить на её образ. Музыка в сочетании с пением настолько завлекли меня, что, казалось, песня будет звучать целую вечность, пока я не закончу её портрет."
    window hide
    $ renpy.notify("Hatsune Miku — Hazy Moon\nНаслаждайтесь музыкой или продолжайте читать дальше.")
    $ renpy.pause()
    stop music fadeout 3
    window show
    "Но, как и всё хорошее в нашей жизни, песня вскоре подошла к своему завершению. Мне оставалось не так много дорисовок, и посему я, не отвлекаясь, продолжила, старательно скрываясь от любопытно поглядывавшей Мику."
    window hide
    
    scene bg int_musclub_day with dissolve
    play ambience ambience_music_club_day fadein 2
    window show
    "Спустя десять минут я была уверена в надлежащем виде портрета и вручила его Мику."
    show mi normal pioneer at center with dissolve
    un "Держи, это тебе, спасибо за песню."
    show mi happy pioneer at center with dspr
    mi "Ой! Я так красиво получилась! Спасибо, спасибо, аригато, Ленуся!"
    "Похоже, картина ей очень понравилась. Её голубые глаза заблестели, а она сама стала ярче, чем восходящее солнце её родины."
    "Девичий восторг перемешался с целым спектром положительных эмоций: огромной радостью, искренней благодарностью и сильной признательностью. Мне всегда казалось, что именно такие эмоции и преобладают в её голове."
    "Мику продолжала рассматривать себя на картине и осыпать меня уже лишними благодарностями. Она была очень счастлива."
    th "Неужели ей никто никогда не дарил подарки, если она так радуется?"
    un "Ну… я пойду?"
    mi "Да-да, конечно! И ещё раз спасибо тебе большое!"
    window hide
    
    play ambience ambience_camp_center_day fadeout 1 fadein 2
    scene bg ext_musclub_day with dissolve
    window show
    "Покидав музыкальный клуб, я мельком через окна клуба заметила, как Мику уже присматривает местечко, куда бы повесить картину."
    window hide
    
    scene bg ext_houses_day with dissolve
    window show
    "Я всё дальше удалялась от клуба, но из головы всё не выходила песня, что пела Мику, и её радостное лицо."
    "Всё же я теперь её понимаю: ведь я не менее рада.{w} Приятно было лишь от одной мысли о том, что я здесь и моя старая жизнь уже идёт по иному, новому пути."
    "Для каждого человека слово «счастье» означает разное. Для Мику это всего лишь картина, для Слави это, наверно, благодарности от вожатой, для Алисы — я уверена почти на все 100%% — будет большое счастье, если она перестанет вести себя так, как сейчас."
    "Она ведь на самом деле довольно чувственная, понимающая и добрая. Но, к сожалению, она давит себя такую."
    "Я бы очень хотела, чтобы тот человек, что поддержал меня в столь трудный час, тоже обрёл счастье."
    # здесь музыку бы к этому эпизоду
    aonl_voice "Как же изменились твои мысли и всё видение мира в целом. Никогда бы не подумала, что ход твоих мыслей настолько легко направить в совершенно неправильное русло."
    aonl_voice "Поверить не могу, что мы вообще оказались в подобном положении! Счастье, счастье…{w} тьфу! Уже слушать противно."
    "Меня просто перетрясло. Холод начал бежать по телу, стремительно охватывая каждый сантиметр."
    "Но больше всего досталось голове: она была готова разорваться на кусочки от невыносимой, пульсирующей боли в черепе и стука в висках, который был сильнее, чем отбойный молоток."
    un "Кто…{w} кто ты? Зачем ты мне всё это шепчешь?.."
    "Пыталась узнать я с остатками сил, но голос всё не унимался и не давал мне сказать ни слова, продолжал мне нашёптывать."
    aonl_voice "А ты не находишь, моя дорогая, что в этом лагере, который ты столь поспешно назвала «своим домом», всё слишком идеально? Нет?!"
    aonl_voice "А ты не задумывалась и не встречала в книгах и романах сотни, нет, тысячи подобных случаев? Разве ты не усвоила за всю свою жизнь несколько простых законов:"
    aonl_voice "«Никакое кристально-чистое счастье не даётся просто так. Вместе с ним всегда прилагаются непроглядная и всепожирающая боль и страдания»;"
    aonl_voice "«Истинное счастье должно познаваться лишь в покое и полном одиночестве»;"
    aonl_voice "«Не бывает сказки, где нет старого доброго злодея»?"
    aonl_voice "Примеров ты знаешь очень много, не буду напоминать."
    aonl_voice "Хотя тебе стоило бы ещё много чего напомнить. Например, твои обещания, твою ненависть, твои слёзы и боль!"
    aonl_voice "Где же та ненависть, скрытность и оболочка, в которой ты клятвенно обещала просидеть всю свою жизнь?{w} А тут — попади она в другой мир, и смотри, как она пляшет под его дудку, наивно надеясь, что этот мир другой: без зла, боли предательства и жестокости."
    aonl_voice "Спешу тебя огорчить, моя милая: даже этот лагерь не чист и не идеален, как может показаться на первый взгляд!" 
    "Меня просто всю трясло. Голова кружилась, а мир вокруг меня как будто рассыпался как паззл.{w} Из-за головокружения и пульсирующей боли я теряла ориентацию в пространстве. Ещё немного, и я совсем упаду."
    un "Что тебе нужно? Зачем…"
    aonl_voice "Скоро узнаешь, глупышка. Очень скоро…"
    "Загадочный, холодный и почему-то очень навязчивый голос затих так же внезапно, как и начался. Он даже сумел перебить всю песню Мику, и я теперь не помнила ни одного куплета."
    "Страх медленно, но верно сходил на нет. Наверное, не скоро я смогу забыть это чувство."
    window hide
    stop ambience fadeout 1
    
    $ sunset_time()
    $ persistent.sprite_time = "sunset"
    scene bg ext_square_sunset with dissolve
    play ambience ambience_camp_center_evening
    window show
    "Пытаясь прийти в себя, я и не заметила, как подошла к пустующей скамейке рядом с Гендой."
    "Кто бы мог знать, что этот лагерь, который ещё два дня назад показался мне самым безобидным местом на земле, приютит у себя таких странных личностей?"
    "Всё же я была права: не бывает рая на земле, есть лишь попытки создать для себя его, закрывая глаза на всё плохое."
    play music music_list["you_won_t_let_me_down"] fadein 3
    show dv normal pioneer close at center with dissolve
    dv "Привет, подруга, скучаешь?"
    "Я не заметила, как рядом со мной села Алиса."
    un "Нет, всё нормально, спасибо за заботу."
    th "Не рассказывать же ей о странных голосах в голове."
    dv "Ой, мне можешь не рассказывать, я же знаю, какая ты, когда напугана."
    th "Ну вот, опять она знает обо мне слишком много, чтобы я могла скрыть свои эмоции от лишних вопросов."
    un "Алис… у тебя никогда не бывало резких головных болей?"
    th "Так уж и быть, расспрошу её хотя был об этом. Быть может, я просто заболела?"
    dv "Ну да, бывало пару раз. Особенно в детстве, я тогда весьма часто болела, особенно зимой.{w} А что не так? У тебя болит голова?"
    un "Ну…{w} Я просто шла сейчас из музыкального клуба Мику и меня будто молнией прошибло от боли."
    dv "Вот как? Обратись к медпункт к Виоле."
    "Встревожено сказала Алиса."
    un "Да, пожалуй обращусь в ближайшее время."
    "И что я ей скажу? О голосах в голове и невыносимой боли при этом? Да мне тут же выдадут повестку в жёлтый дом с такими диагнозами."
    dv "Может, мне стоит пойти с тобой?"
    un "Да ладно тебе, со мной уже всё нормально."
    un "Ну-ну, иди ко мне, моя маленькая, я тебя обниму и успокою."
    "Я решила и дальше подтрунивать над ней и пыталась обнять её и потрепать волосы."
    show dv shocked pioneer close at center with dspr
    dv "Тьфу. Что ты делаешь?!{w} Хочешь, чтобы весь лагерь видел хулиганку Алису в обнимку с тихоней, которая её утешает?{w} Ну уж нет, давай сохраним наши роли нетронутыми!"
    un "Попа-а-а-лась."
    show dv laugh pioneer close at center with dspr
    "Мы обе рассмеялись с ней от этого. Здорово всё-таки, что мы с ней давно знакомы. Алиса — тот человек, благодаря которому я могла забыть о неприятностях и хоть немного, но повеселиться."
    stop music fadeout 2
    play sound sfx_dinner_jingle_normal
    "Неожиданно зазвучал горн, стало быть, пора собираться на ужин. Впереди грядёт поход, а значит — к нему должны быть все готовы, и наши желудки тоже."
    dv "Пойдём уж, стесняша, пора перекусить."
    un "Угу, держим курс в столовую, не так ли, гроза всего лагеря?"
    "Нас обеих опять пробрал смех. И на этой позитивной ноте мы выдвинулись в царствие всея пищевых благ."
    window hide
    stop sound fadeout 1
    
    scene bg ext_houses_sunset with dissolve
    show dv normal pioneer at center with dissolve
    window show
    un "А где Ульянка пропадает, обычно вы не разлей вода?"
    th "И ведь действительно, давно я её не видела."
    dv "Ах да, она сказала мне, что будет готовить нечто особенное для похода, и убежала. Так что считай, что я по дружбе тебя предупредила.{w} Если увидишь её рядом, будь готова к чему-то неожиданному."
    un "Спасибо за предупреждение. Я буду осторожной.{w} Никогда не знаешь, что пойдёт не так, если Ульяна неподалёку."
    dv "Твоя правда, даже я иногда опасаюсь её выходок. В фантазии ей не откажешь, каждый раз что-то новое."
    un "Девочка-внезапность."
    "Засмеялась я."
    dv "Странно, что именно меня грозой всего лагеря прозвали. Если подумать, Ульяна куда больше бедокурит."
    un "Просто она ещё не полностью раскрыла свой потенциал."
    dv "А когда раскроет, лагерь точно будет разобран ей лично по кирпичику."
    "За обсуждениями маленькой егозы мы добрались до столовой."
    window hide
    stop ambience fadeout 1
    
    scene bg aonl_int_dining_hall_people_sunset with dissolve
    play ambience ambience_dining_hall_full fadein 2
    window show
    "Как всегда, многолюдно и даже не знаешь, куда присесть."
    "На раздаче мы с Алисой взяли по булочке с кефиром. Алиса очень любила это сочетание и, будучи голодной, могла пойти на всё, лишь бы получить свою заветную порцию."
    "Мест оказалось не так много, и мы присели за свободный столик, где осталось ещё одно."
    show dv normal pioneer at center with dissolve
    dv "Приятного аппетита!"
    "Произнесла Алиса с явным нетерпением, готовая моментально вцепиться в булочку."
    un "И тебе приятного."
    "Ответила ей я."
    play music music_list["always_ready"] fadein 3
    "И только Алиса уже собралась выпить кефиру, как чуть не подавилась от внезапного крика на всю столовую."
    mt "УЛЬЯ-Я-Я-Я-НА-А-А-А-А-А-А-А-А! А НУ СТОЙ!"
    show us laugh pioneer:
        xpos -0.3
        linear 1.0 xpos 1.2
    "Тут же мимо нас пронёсся красноволосый вихрь. В руках Ульяны красовались три булочки."
    th "И куда ей столько?"
    "Следом за ней бежали три мальчика из младших отрядов. А как только это безумие заметила Ольга Дмитриевна, участников марафона стало пятеро."
    show dv smile pioneer at center with dspr
    dv "Вот о чём я и говорила: с ней надо всегда быть начеку."
    "Алису, похоже, эта ситуация забавляла: ну ещё бы, наблюдать за бегающей по столовой вожатой."
    un "Если слишком долго тихо, это не всегда значит тихий час, это также значит, что Ульяна готовит очередную проделку."
    "Весело сказала я."
    stop music fadeout 3
    hide dv with dissolve
    "Пока Ульяна проверяла физическую подготовку вожатой и других пионеров, мы закончили с ужином. В какой-то момент Ульяна оказалась разоружена, и вернувшаяся запыхавшаяся вожатая скомандовала:"
    show mt normal pioneer at center with dissolve
    mt "Все, кто закончил ужинать, стройтесь на площади по парам, когда все будут готовы — отправляемся в поход."
    window hide
    stop ambience fadeout 1
    
    scene bg ext_square_sunset with dissolve
    play ambience ambience_camp_center_evening fadein 2
    window show
    "На удивление, толпа пионеров довольно быстро построилась на площади."
    "Алиса, попрощавшись со мной, пошла узнавать у Ульяны о её проделках.{w} Мику о чём-то весело болтала с тем странным парнем. Беспокоить их у меня не было никакого желания."
    "К сожалению, правила построения были таковы, что все пионеры должны идти в парах. Со мной предстояло идти Славе."
    "Не лучший вариант, особенно если вспомнить нашу дневную ссору. Надеюсь, она не станет читать мне нравоучения."
    "Славя бегло посмотрела на меня и тут же отвела взгляд, сделав вид, что отвлеклась на что-то. Заводить с ней разговор первой я не считала нужным: всё же это она влезла не в своё дело, а не я."
    show mt normal pioneer at center with dissolve
    mt "Отряд, равняйсь, нале-е-е-во! На поход в лес шаго-о-о-м марш!"
    "Ольга Дмитриевна скомандовала, и весь строй резво выдвинулся на природу."
    window hide
    stop ambience fadeout 1
    
    scene bg ext_path_sunset with dissolve
    play ambience ambience_forest_evening fadein 2
    window show
    "Я шла молча, в то время как вокруг разносились по ветру голоса других пионеров. Наверное, я могла бы снова уйти в мысли, если бы на полпути меня не окликнула рядом идущая Славя."
    show sl normal pioneer at center with dissolve
    play music music_list["you_won_t_let_me_down"] fadein 3
    sl "Лена, я…{w} Поговорить хотела с тобой."
    un "О чём же, снова начнёшь учить меня что мне делать, будто я маленькая?"
    "Съязвила ей в ответ я."
    sl "Ну, зачем же так сразу. Я хотела бы как раз извиниться за сегодняшний инцидент. В общем, прости, что слишком многое взяла на себя."
    un "Допустим, прощаю, дальше-то что?"
    "Видя, как старательно активистка выдавливает из себя слова, я даже и не думала таять от её извинений."
    sl "В общем, я повела себя неправильно, прости. Я думала, что ты наивный ребёнок и за тобой нужно присматривать. Я не учла, что ты могла вспомнить себя настоящую."
    "Кажется, я готова была вспыхнуть как порох и на этот раз врезать выскочке. Не зря я посещала курсы самообороны: там меня научили парочке приёмов."
    "Конечно, они нужны были, чтобы защитить себя от физического нападения, но если в твою жизнь лезут со своей моралью и при этом оскорбляют, разве это не повод поставить на место негодяя?"
    "Сжав кулак, я была готова нарушить пионерский устав и уже собиралась взять замах, чтобы ударить Славю. Её спасло то, что в этот момент вожатая скомандовала отрядам построиться, а затем отпустила всех занимать место у кострища."
    "За несколько секунд до того, как Славю позвала Ольга Дмитриевна разжигать костёр, я, едко улыбнувшись, сказала ей:"
    un "Иди к чёрту! Ещё раз будешь ошиваться около меня, словами я не ограничусь."
    "Она явно такого не ожидала и бледная как смерть попятилась к вожатой."
    th "Скатертью дорога. Нашла тоже наивного ребёнка! Такой, как она, никогда не понять меня."
    window hide
    
    play music music_list["dance_of_fireflies"] fadeout 1 fadein 3
    scene bg ext_polyana_sunset with dissolve
    play sound_loop sfx_forest_fireplace fadein 2
    window show
    "Потихоньку-помаленьку каждый нашёл себе местечко у костра."
    "Я расположилась напротив Мику. Рядом с ней сидела Алиса с гитарой. Я даже не заметила, когда Алиса успела прихватить её с собой."
    "Приятный тёплый вечер всё больше напоминал дивный сон. Искры костра взмывали вверх, уносясь в звёздное небо, вскоре затухая и сменяясь другими такими же искрами."
    "Треск веток служил аккомпанементом гитарным мотивам Алисы и звонкому голосу Мику."
    "Думаю, из них вышел бы отличный музыкальный коллектив. Они здорово смотрелись вместе — два человека, обожающие музыку, проживающие каждую ноту, исполняемую ими, будто это одна большая жизнь."
    "Надеюсь, в будущем они не уничтожат свои таланты, погнавшись за амбициями наподобие: хочу больше, нужно выше, сильнее, дальше."
    "Обычно в такие вечера ты чувствуешь себя по-особому. Нет прошлого, нет будущего. Есть лишь приятные часы, когда ты смотришь на горящее пламя, наслаждаешься каждым звуком вокруг, и тебе всё равно, что будет завтра."
    "Важное умение — запомнить самые яркие моменты своей жизни, чтобы потом, когда серость типичных будней снова захватит тебя в свои клешни, можно было подумать о чём-то хорошем."
    "Мне, как мечтательной натуре художника, это всегда позволяло выстроить в памяти картины прошлых событий, которые я запомнила больше всего, именно так, зачастую по памяти, я и рисовала свои картины."
    "Только так я могла не позволять серости захватывать меня без остатка. Хоть я и жила типичной серой мышью даже в школе, я никогда не переставала мечтать о вот таких редких моментах, как этот чудесный вечер."
    "Нужно ловить момент, пока можешь, иначе он просто пройдёт мимо тебя, угаснув, как искра от пламени."
    show blink
    "Кажется, замечтавшись, я закрыла глаза."
    window hide
    $ renpy.pause(1, hard=True)
    hide blink
    show unblink
    $ renpy.pause(1, hard=True)
    window show
    "Наконец вернувшись из мира грёз, я увидела: разрозненные кучки пионеров, пытающихся напевать песню в такт с Мику, Ольгу Дмитриевну, которую, похоже, также захлестнула волна приятных эмоций, Ульяну, держащую в руках какой-то надутый пакет…"
    stop music fadeout 1
    play sound sfx_muffled_explosion # поискать другой звук бума для Ульяны
    "УЛЬЯНА!{w} Понимать, что сейчас будет, уже было поздно, поскольку Ульяна привела очередную свою проделку в действие."
    "Пакет оказался пустым и взорвался воздухом. Казалось бы, ничего особенного, но лесное эхо сделало своё дело. Получился эффект бомбы, и пионеры попадали со своих мест."
    play sound sfx_bush_body_fall
    "Так же и я, не удержавшись на брёвнышке, от неожиданности упала назад." with vpunch
    "Боль в затылке не заставила себя долго ждать."
    "И снова этот шёпот, и снова эти слова, правда они были более очевидны, чем в прошлый раз."
    aonl_voice "Не верь тому, кто завтра будет новым. Старайся избегать его, ты ведь одиночка и всегда ею будешь! Такова твоя природа!"
    aonl_voice "Так и не сопротивляйся своей же собственной сущности, оставайся на этой стороне, не стоит доверять кому попало."
    window hide
    
    menu:
        "Поверить голосу":
            window show
            "Я до сих пор не знаю, что это за голос. Я до сих пор не имею понятия, почему моя голова буквально трещит по швам. когда я его слышу."
            "Я по-прежнему не осознаю, зачем и каким образом он мне даёт подобные советы."
            "И тем ни менее, он почему-то казался мне знакомым и родным. Поэтому я поверила ему и решила быть более осторожной в общении в будущем."
        "Не верить ему": # возможно заменить пункт меню, чтобы он был новым.
            window show
            th "Откуда эти голоса? Почему они столь навязчивы? И я просто не имею представления, из-за чего моя голова просто чудовищно болит, когда я его слышу, а перед глазами всё становится серым и расплывается."
            th "Нельзя им верить. Они всё равно скорее всего лишь моё воображение. Я надеюсь."
    
    "Увидим, что будет завтра, а сейчас поход был сорван и я чувствовала приближение сна."
    window hide
    stop sound_loop fadeout 2
    stop ambience fadeout 2
    
    scene bg black with dissolve
    window show
    "Все пионеры, построившись, выдвинулись обратно в лагерь. Я буквально засыпала в пути, потому даже не заметила, как мы пришли на площадь и вожатая скомандовала расходиться. Домой я вернулась раньше Мику и, подготовив постель, легла спать."
    window hide

    $ prolog_time()
    pause 1
    show prologue_dream with dissolve
    window show
    "Мне снова приснился сон, как я нахожусь в этом тёмном месте; похоже, это становилось традицией."
    show bg int_mine behind prologue_dream with dissolve
    "Мимо меня пролетали образы каких-то заброшенных шахт и пары людей."    
    "Один из них был с фонарём. Они держались за руки, а один из них явно давал понять, что защитит того, что прикрывает даже ценой своей жизни."
    "Я видела, как эти шахты становятся самым настоящим лабиринтом и запутывают путников всё больше. Чем глубже они заходили, тем непроходимее становилась сеть подземных пещер."
    "Лишь я видела из него выход, и он был очень близко от них. но они всякий раз неверно выбирали дорогу, уходя всё глубже в катакомбы и уменьшая вероятность на успешное спасание с каждым сделанным шагом."
    "Я очень хотела им помочь и указать верный путь, но снимки исчезли, вновь погрузив пространство во тьму."
    show bg black behind prologue_dream with dissolve
    "А после, как по мановению волшебной палочки, снова появились буквы, сложившиеся в предложение:"
    window hide
    $ aonl_feelings = "ИСТИННЫЕ ЧУВСТВА СИЛЬНЕЕ\nЛЮБЫХ ПРЕГРАД И СТРАХА."
    show aonl_credits aonl_feelings at truecenter behind prologue_dream with dissolve:
        linear 5.0 xzoom 2.0 yzoom 2.0
    $ renpy.pause (5, hard=True)
    hide aonl_credits with dissolve
    window show
    "Это всё, что я запомнила из этого сна перед тем, как проснулась."
    window hide
    scene bg black with fade2
    jump aonl_day4
    