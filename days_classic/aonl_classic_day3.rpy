label aonl_day3_alt:

    call aonl_new_chapter(3, "третий", "Руки его украдкою касаться,\nТепло его дыханья ощущать,\nКружиться с ним в неспешном белом танце –\nОтрадней, чем в небесный рай попасть!")
    $ prolog_time()
    $ persistent.sprite_time = "night"

    if aonl_sl_story:
        scene bg aonl_fog_everywere
        show prologue_dream
        with fade2
        play music aonl_Lena_dream_1 fadein 3
        window show
        "Туман перед глазами, ничего не видно."
        th "Неужели опять сплю и снова встречу саму себя?"
        "Я осмотрелась, по-прежнему никаких ориентиров."
        th "Ну да, так оно и есть. Только в этот раз не буду пугаться."
        "Туман... Даже не туман, а какой-то белый эфир, разошелся, обнажив асфальтовую дорожку..." 
        "Вот только на другом конце никого не оказалось."
        "Видимо, остаётся идти вперёд."
        "Казалось путь будет очень долог, но стоило сделать шаг, и асфальт словно ринулась мне навстречу как эскалатор, значительно сократив путь."
        window hide
        
        #фон площади ночной с туманом по границам площади
        scene bg aonl_ext_square_fog
        show prologue_dream
        with dissolve
        window show
        "Вскоре я вышла на площадь, границы которой всё так же опутывал белый эфир." 
        "Вот только в этом тумане были прорехи."
        "Такие же туннели как мой, 4 штуки."
        th "Значит, здесь ещё кто-то есть?"
        "Я присмотрелась." 
        "У памятника Генде, действительно, стояла группа людей."
        th "Надеюсь они не причинят вреда?"
        # Девушка
        aonl_woman "Лен, давай быстрее."
        "Одна из фигур помахала мне рукой."
        th "Ну что ж подойду."
        show mi normal pioneer at fleft behind prologue_dream
        show sl normal pioneer at cleft behind prologue_dream
        show dv normal pioneer at cright behind prologue_dream
        show us normal pioneer at fright behind prologue_dream
        with dissolve2
        th "Приблизившись, увидела, что махала Славя, а с ней стоят Алиса, Ульяна и Мику."
        un "Девочки, что вы здесь делаете?"
        show us smile pioneer with dspr
        us "Тебя ждём конечно, чего не понятного?"
        show sl smile pioneer with dspr 
        sl "Я подозревала, что она как мы."
        show dv surprise pioneer with dspr
        dv "Подожди подруга."
        "Алиса посмотрела куда-то за мою спину."
        hide us
        hide dv
        hide sl
        hide mi
        with dissolve
        "Я обернулась. Никого."
        th "А разве кто-то должен быть?"
        show mi shocked pioneer at fleft behind prologue_dream
        show sl scared pioneer at cleft behind prologue_dream
        show dv sad pioneer at cright behind prologue_dream
        show us upset pioneer at fright behind prologue_dream
        with dissolve2
        mi "Она совсем одна пришла, совсем никого нет, девчат, а как же мы без неё будем!"
        un "О ком вы?"
        show aonl_mi25 shadow far behind mi at fleft
        show aonl_sl25 shadow far behind sl at cleft
        show aonl_dv25 shadow far behind dv at cright
        show aonl_us25 shadow far behind us at fright
        with dissolve
        #Спрайты темных силуэтов взрослых девушек из гаремной концовки
        "Я посмотрела девочкам за спины и только сейчас поняла, что за ними стоят тёмные силуэты, а не тени, как по началу думала."
        th "Что за мистика?"
        show sl sad pioneer with dspr
        sl "Лена, послушай меня внимательно. Ты рано пришла, ты ещё не готова."
        un "Не готова? Вы о чём, что вообще происходит?"
        window hide
        
        #scene bg aonl_фон с трещинами
        window show
        "Мир вздрогнул и пошёл трещинами, словно картина, рвущаяся на части."
        sl "Времени мало! Лена, настанет момент, и ты всё поймёшь, уже скоро, главное сделай правильный выбор!" 
        sl "Тебе помогут, ты не одна!"
        un "Какой выбор? Что мне нужно сделать?"
        sl "Ты поймёшь, Лен." 
        sl "Найди саму себя, она подскажет. Верь ей!"
        window hide
        
        #scene bg aonl_slom фон с темными разрывами
        window show
        "Мир окончательно раскололся на части."
        window hide
        stop music fadeout 3
    

    if aonl_un_story:
        #scene bg aonl_tuman with dissolve
        scene bg aonl_fog_everywere
        show prologue_dream
        with fade2
        play music aonl_Lena_dream_1 fadein 3
        window show
        "Туман перед глазами ничего не видно."
        th "Неужели, опять сплю и снова встречу саму себя?"
        "Я осмотрелась, по-прежнему никаких ориентиров."
        th "Ну да, так оно и есть. Только в этот раз не буду пугаться."
        "Туман, даже не туман, а какой-то белый эфир, разошелся, обнажив асфальтовую дорожку."
        "На другом конце всё так же стояла взрослая женщина."
        "Увидев меня, она тихонько улыбнулась и помахала рукой."
        "Пора знакомиться."
        "Я двинулась ей на встречу, а она всё так же стояла, ожидая."
        "Казалось путь будет очень долог." 
        "Но стоило сделать шаг, и асфальт словно ринулся мне навстречу как эскалатор, значительно сократив путь."
        "Вскоре я вышла на площадь, границы которой всё так же опутывал белый эфир." 
        "Вот только в этом тумане были прорехи. Такие же туннели как мой, 4 штуки."
        th "Значит, здесь ещё кто-то есть?"
        aonl_un25 "Наконец, ты пришла и ещё вместе со мной."
        un "Кто вы?"
        "Женщина понимающе улыбнулась."
        aonl_un25 "Разве не очевидно? Я это ты, только чуть старше."
        un "Вы из будущего?"
        aonl_un25 "Не совсем. Это сложно объяснить..."
        un "Ну попробуйте."
        aonl_un25 "Так будет быстрее."
        "Женщина положила ладонь мне на лоб."
        window hide
        
        #scene cg aonl_cards картинки реального мира. Какие именно? Следует подумать над этим моментом
        window show
        "Перед глазами замелькали картины другого мира."
        "Точнее моего, мира из которого я пришла." 
        "Словно восполнила недостающую часть себя. Но как это возможно?!" 
        "Я стала одновременно и взрослой женщиной, и девушкой пионеркой... Двумя личностями одновременно!"
        "Когда калейдоскоп перед глазами прекратился, женщина превратилась тень, лишь силуэтом напоминавшую себя прежнюю..." 
        "А я всё вспомнила."
        aonl_un25 "Пойдём, нас ждут."
        un "Да."
        "Она двинулась следом за мной."
        "Хотя не понимала до конца случившегося..." 
        "Но теперь твёрдо знала что я здесь не просто так, а моя взрослая версия союзник."
        window hide
        
        #фон площади ночной с туманм по границам площади
        scene bg aonl_ext_square_fog
        show prologue_dream
        with dissolve
        window show
        "У памятника Генде всё так же стояли девушки."
        show mi normal pioneer at fleft behind prologue_dream
        show sl normal pioneer at cleft behind prologue_dream
        show dv normal pioneer at cright behind prologue_dream
        show us normal pioneer at fright behind prologue_dream
        with dissolve2
        "Когда приблизилась, они дружно уставились мне за спину."
        show us smile pioneer with dspr
        us "Ну наконец-то они вместе, я уж не надеялась." 
        dv "Кто бы говорил, мы тебя совсем недавно дожидались."
        show us dontlike pioneer with dspr
        us "Бе."
        "Улька высунула язык."
        show mi upset pioneer with dspr
        mi "Девочки не ссорьтесь!"
        show sl sad pioneer with dspr
        sl "Вот именно, нам предстоит ещё много работы, поэтому отставить разборки!"
        un "Здрасти. Я ещё не совсем понимаю, что происходит!" 
        un "Вы можете объяснить?"
        show mi normal pioneer
        show us normal pioneer
        show sl normal pioneer
        with dspr
        sl "Конечно Лен, прежде всего расскажем тебе о лагере." 
        sl "На самом деле это пространственный пузырь, из которого нет выхода." 
        sl "Если идти по дороге, то она снова приведёт к воротам, словно замкнута по кругу…"
        stop music fadeout 2
        window hide
    
    scene bg black with dissolve
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_int_cabin_day fadein 2
    scene bg int_house_of_un_day
    show unblink
    pause 1
    window show
    if aonl_sl_story:
        "Я открыла глаза и увидела потолок." 
        "Никаких трещин и гула, только щебет птиц за окном и мой домик." 
        "Как-то сильно светит солнце."
        
    if aonl_un_story:
        "Открыла глаза и увидела потолок." 
        "Щебет птиц за окном и мой домик." 
        "Как-то сильно светит солнце."
        
    "Скосила глаза на постель Мику." 
    "Её нет." 
    "Посмотрела на будильник, 9:12 утра."
    th "Я проспала!"
    "Вскочив с кровати, схватила будильник и не веря собственным глазам уставилась на цифры – 9:13."
    th "Как? Почему? И никто не разбудил!"
    show mi smile pioneer with dissolve
    mi "О, ты проснулась, а мы уже начинали беспокоиться! Вожатая Виолу хотела позвать, но теперь я вижу, что всё хорошо. Не волнуйся Лен, я скажу, что ты в порядке."
    un "Мику, почему ты меня не разбудила? Линейка уже закончилась?"
    show mi upset pioneer with dspr
    mi "И линейка и завтрак. Я пыталась тебя растолкать, но каждый раз ты, что-то бубнила во сне, отталкивала меня рукой, переворачивалась и снова засыпала. Я очень переволновалась."
    show mi shocked pioneer with dspr
    extend "Ой, сейчас же Виола придёт! Иди умойся, я её встречу и скажу, что всё хорошо."
    hide mi with dissolve
    "Я встала и накинула форму."
    "Нужно сходить умыться, вот только сначала запишу всё в дневник."
    window hide
    
    play music aonl_lena_diary_writing fadein 2
    scene cg aonl_un_diary with dissolve
    $ set_mode_nvl()
    window show
    
    if aonl_sl_story:
        # play sound (Пишет ручкой, можно взять из Четыре Горизонта: Зимней Сказке)
        "«Дорогой дневник.{nw}"
        "Теперь мне снятся сны с участием моих подруг. Это выглядело так, будто они что-то хотят донести мне, что-то важное. Ко всему прочему я ещё и проспала! Такого не было ни разу! Порой кажется, что я медленно схожу с ума. Нужно успокоиться и как-то прочистить голову от этих странных снов. Может, помогу Мику в кружке, чтобы отвлечься? А ещё мысли о Семёне и Слави занимают постоянно. Будто это меня касается...{nw}"
        "Но я всё равно ревную.»"

    if aonl_un_story:
        # play sound (Пишет ручкой, можно взять из Четыре Горизонта: Зимней Сказке)
        "«Дорогой дневник.{nw}"
        "Теперь мне снятся сны с участием моих подруг! Они рассказали мне что-то крайне важное. Но проснувшись, я всё забыла. Ко всему прочему, я ещё и проспала. Такого не было ни разу! Все попытки вспомнить не окончились успехом. Порой кажется, что я медленно схожу с ума. Нужно успокоиться и как-то прочистить голову от этих странных снов. Может помогу Мику в кружке, чтобы отвлечься? А ещё мысли о Семёне и Слави занимают постоянно, будто это меня как-то касается...{nw}"
        "Но я всё равно ревную, ведь мне хочется быть вместе с Сёмой, он такой сильный. Кажется, рядом с ним я набираюсь уверенности.»"

    window hide
    $ set_mode_adv()
    stop music fadeout 2
    play ambience ambience_camp_center_day fadeout 1 fadein 2
    play sound [sfx_open_dooor_campus_1, sfx_close_door_campus_1]
    pause 1
    scene bg ext_house_of_un_day with dissolve
    window show
    "Закончив свою исповедь, я направилась умываться."
    window hide
    
    scene bg ext_houses_day with dissolve
    window show
    "Птицы щебечут, солнечные лучи ласково освещают цветы, дорожная плитка уже прогрелась и приятно греет стопы через туфли. Идиллия."
    window hide
    
    scene bg ext_washstand_day with dissolve 
    window show 
    "Вот только мне почему-то не до радости. Если раньше, я наслаждалась покоем в лоне природы, то теперь мне не до этого. Странные события во снах не дают покоя."
    window hide
    
    scene bg ext_washstand2_day with dissolve
    play sound sfx_open_water_sink
    pause 1
    play sound_loop sfx_water_sink_stream
    window show
    "Воспользовавшись моментом, постирала спортивную форму."
    th "И Семён ещё. Почему мне так тоскливо от его дружбы со Славей?"
    window hide
    
    stop sound_loop
    play sound sfx_close_water_sink
    pause 1
    scene bg ext_houses_day with fade2
    window show
    th "Вожатая наверняка работой нагрузит за прогул."
    show sl normal pioneer with dissolve
    sl "Привет Лен, ты в порядке?"
    th "Легка на помине."
    show sl scared pioneer with dspr
    un "Чего тебе?"
    "Буркнула я не довольно."
    show sl sad pioneer with dspr
    sl "Я… Я хотела узнать, всё ли хорошо?"
    un "Со мной всё в порядке."
    "Ответила в той же манере."
    sl "Уверена? Мику говорила тебя невозможно поднять. Мы все беспокоились. Лен, что с тобой?"
    un "Всё хорошо. Наверно, просто сильно устала."
    show sl smile pioneer with dspr 
    "В этот раз ответила мягче, не стоит ругаться с утра."
    sl "Хи-хи."
    "Славя мило рассмеялась, не саркастически не грубо, а именно мило, словно прощая мою грубость."
    sl "Ладно, соня наша. Иди завтракай."
    un "А разве завтрак не закончился?"
    sl "Конечно закончился, но я попросила поварих оставить тебе и другим опоздавшим порцию. Так что не волнуйся."
    un "С-спасибо, Славя."
    "Как-то неловко, что ей приходится обо мне заботиться, а я грубить начала на ровном месте."
    show sl smile2 pioneer with dspr
    sl "Никаких проблем. Иди в столовую. Твои умывальные принадлежности сама занесу."
    un "Спасибо Славя не стоит. И у меня тут…"
    "Я покраснела и опустила глаза. Стало ещё неудобней от моей недавней грубости."
    sl "О, ты форму постирала. Лен, не стесняйся. Лучше поспеши, поварихам ещё за тобой посуду мыть, обед готовить, задерживаются же."
    "Иди, я занесу и форму мне не сложно."
    "Не дожидаясь ответа, Славя выхватила пакеты и заспешила прочь."
    show sl smile pioneer with dspr
    sl "У нас, кстати, танцы сегодня, на них не опоздай!"
    hide sl with dissolve
    "Крикнула она, прежде чем скрыться из виду."
    th "Танцы? А зачем мне туда идти? Я же их с детства не люблю."
    th "Ох, только не это, а если меня пригласят? Что мне делать?" 
    th "Хотя…" 
    th "А если пригласит Семён? Мы будем танцевать белый танец, и он…"
    th "Сердце бешено заколотилось, а щёки опять залились румянцем. Я помотала головой, отгоняя непрошенные мысли."
    th "Хватит, надо идти столовую."
    window hide
    
    play ambience ambience_dining_hall_empty fadeout 1 fadein 2
    scene bg int_dining_hall_day with fade2
    window show
    "Повариха, недовольно посмотрев, выдала мою порцию."
    th "Стыдно, что я её так подвела своим прогулом. Завтра встану пораньше."
    "Неспешно села ковырять вилкой нечто похожее на кашу. Кусок в горло совсем не лез."
    th "Эх, зря Славя так беспокоилась. Стоило остаться дома до обеда и просто полежать."
    play sound ["<to 1.0>" +  sfx_open_door_2, sfx_close_door_1]
    show pi normal aonl_pioneer far at left
    show mz normal pioneer glasses far at center 
    with easeinright # фикс
    "Вдруг в столовую зашла Женя, а за ней Семён."
    th "Они тоже проспали?"
    show mz normal pioneer glasses far at fleft with ease
    th "А вдруг Семён сядет со мной, мы сможем лучше узнать друг друга? Ой, нет!"
    "Я опустила взгляд в тарелку стараясь не замечать парня, но глаза помимо воли изредка косились в его сторону."
    
    if aonl_sl_story:
        # show pi normal aonl_pioneer far at cleft with ease
        "Вскоре юноша сел с Женей."
        th "Он выбрал её? А хотя что мне возмущаться, я же сама боялась его компании и всё же…"
        window hide
        
        play ambience ambience_camp_center_day fadeout 1 fadein 2
        play sound ["<to 1.0>" + sfx_open_door_2, sfx_close_door_1]
        pause 1
        scene bg ext_dining_hall_near_day with dissolve
        window show
        "Выскочив из-за стола, я отдала свой недоеденный завтрак и поспешила на улицу."
        "У дверей я сделала глубокий вдох, пытаясь угомонить бешено колотящееся сердце."
        th "Почему мне так плохо, почему?"
        window hide

    if aonl_un_story:
        hide mz
        show pi normal aonl_pioneer 
        with dissolve
        play music music_list ["your_bright_side"] fadein 2
        #Вариант не окончательный, не уверен в нём
        "Мамочки, он идёт ко мне!"
        show pi smile aonl_pioneer with dspr
        me "Привет, доброе утро!"   
        un "Доброе…"
        "В этот раз у меня получилось даже слегка улыбнуться."
        me "Можно к тебе присесть? Я сейчас сбегаю за завтраком."
        un "Да, конечно."
        hide pi with dissolve
        "Сёма ушёл, а я попыталась привести себя в порядок."
        th "Резиночки в волосах, галстук в порядке. Рубашка не заляпана едой, юбка оправлена."
        "На всякий случай заглянула под стол и проверила чистоту гольфов и туфлей." 
        "Не то чтобы он туда заглянет, но подстраховаться стоит."
        "Подышала на руку для проверки свежести дыхания."
        th "Вроде всё в нормально."
        "Расположилась за столом поудобнее и постаралась выпрямить спину."
        window hide
        
        show pi normal aonl_pioneer with fade
        window show
        "Через минуту Сёма сидел напротив с нехитрым набором блюд на подносе: каша овсяная; два яйца варёных; хлеб белый четыре куска; сосиска варёная одна штука; компот из непонятного набора фруктов и ягод, один стакан."
        show pi smile aonl_pioneer with dspr 
        me "Приятного аппетита!"
        un "Спасибо."
        "Хотела с ним заговорить, но не получилось выдавить ни слова." 
        "Поэтому продолжила жевать кашу, опустив взгляд в тарелку, лишь изредка поглядывая на юношу." 
        "Но улыбка так и не сходила с моего лица, ведь он рядом!"
        "Машинально отметила, насколько аккуратно он ест. Видно, мальчик воспитан."
        me "Пойдёшь сегодня на танцы?"
        "Я внутренне напряглась, ведь боялась этого вопроса."
        un "Да, наверное…"
        "Я замолчала, ожидая продолжения, но его не последовало. Может, лучше самой спросить?"
        un "А ты?"
        show pi upset aonl_pioneer with dspr
        me "Не знаю ещё…"
        "Сёма неопределённо пожал плечами."
        "Он не хочет идти?"
        "Или дело во мне, и я ему не нравлюсь?!"
        un "А почему?"
        me "Что почему?"
        "Юноша уставился недоумённым взглядом, словно до этого был не здесь."
        un "Почему не хочешь идти?"
        show pi normal aonl_pioneer with dspr
        me "Я же не сказал, что не хочу…"
        un "Ну, ладно тогда."
        "Видимо, я зря себя накручиваю."
        show pi smile aonl_pioneer with dspr
        me "Если, конечно, ты меня приглашаешь..."
        "От неожиданности я едва не выронила вилку."
        #Небольшая тряска экрана. Не знаю, как реализовать. Нужна помощь.
        th "ЧТО???!!! Он хочет, чтобы я его пригласила???!!!"
        th "Сёма, конечно мальчик хороший, но ведь юноши приглашают девочек, а не наоборот, хотя ведь…"
        "Я покраснела и отвела взгляд."
        un "Ну, я не знаю насчёт..."
        th "Как же хочется пригласить... Но язык опять свело, а в горле словно понизили громкость, от чего на свет выходило, что-то невнятное."
        show pi normal aonl_pioneer with dspr
        me "Извини, это была шутка."
        th "Он не хочет, наверно, не нравлюсь..."
        un "Хорошо."
        "Снова уставилась к себе в тарелку, чувствуя, как покраснела уже до кончиков ушей."
        me "А ты не знаешь, что Женя хотела?"
        un "А?"
        "Вопрос Сёмы вывел из прострации."
        me "Ну, я, когда в столовую зашёл, она... Ну, что-то хотела от меня."
        un "Не знаю..."
        me "Ясно."
        "Разговор зашёл в тупик, остаётся только сбежать, дабы не затягивать неловкий момент."
        "Я закончила со своей порцией побыстрее."
        un "Ладно, я пойду. Увидимся!"
        show pi smile aonl_pioneer with dspr
        me "Да, пока!"
        window hide
        stop music fadeout 2
        play ambience ambience_camp_center_day fadeout 1 fadein 2
    
    scene bg ext_square_day with dissolve2
    window show
    "Чтобы немного отвлечься я присела на лавочку и открыла книгу, которую захватила из дома." 
    "Перечитать будет не лишним."
    play music music_list ["eternal_longing"] fadein 2
    show cs normal with dissolve
    cs "Лена, вот ты где. Мне сказали тебе не здоровится."
    un "Нет всё в порядке, я просто не выспалась."
    cs "В самом деле? Всё же осмотрю тебя на всякий случай, пойдём."
    un "Да."
    window hide
    
    scene bg ext_aidpost_day
    show cs normal
    with dissolve
    play ambience ambience_medstation_inside_day fadeout 1 fadein 2
    play sound [sfx_unlock_medpunkt_door, sfx_close_door_1]
    pause 2
    scene bg int_aidpost_day
    show cs normal stethoscope
    with dissolve
    window show
    cs "Сними рубашку."
    un "Хорошо."
    "Я развязала галстук и сняла верхнюю одежду. Медсестра начала слушать меня стетоскопом."
    show cs smile stethoscope with dspr
    cs "Сердце и лёгкие вроде в порядке. Я уж боялась какой-нибудь вирус." 
    cs "Новенький тоже проспал сегодня." 
    cs "На всякий случай и его осмотрю."
    th "Семён будет с ней здесь, без рубашки!"
    #Покраснение фона надо сделать
    "От мысли, как она трогает его крепкую мускулистую грудь, моё сердце вновь заколотилось, щёки залило румянцем, а уши зажгло огнём."
    "Медсестра, тем временем слушавшая сердце, удивлённо подняла глаза."
    show cs shy stethoscope with dspr
    cs "Таааак."
    show cs smile stethoscope with dspr 
    "Она положила пальцы на гланды и ощупала их. Надо ли говорить, что они пульсировали как сумасшедшие?"
    cs "Ну-ка милая моя, открой рот скажи АААААААА."
    un "ААААААААААА."
    cs "Угу, инфекции нет."
    show cs shy stethoscope with dspr
    cs "А ну-ка, покажи глазки милая." 
    cs "Не переживай я и Семёна так проверю, всё нормально."
    th "Она будет так его трогать?"
    #Покраснение фона надо сделать. Зачем? Не вижу в этом потребности
    "Картина того, как Виола крепко держит Семёна за лицо, вплотную рассматривая глаза, словно собираясь поцеловать, опять появилась перед глазами."
    "Меня аж затрясло от этой сцены."
    show cs smile stethoscope with dspr 
    cs "Угу, ясно. Зрачки расширены."
    un "И что?"
    cs "Милая моя, у тебя это дня 2 уже верно?"
    un "Я-я не знаю. А что?"
    show cs shy stethoscope with dspr
    cs "Ничего милая, ничего, недуг твой не смертелен."
    un "Какой недуг?"
    show cs smile with dspr
    cs "Никакой, это так к слову. Одевайся."
    "Я надела рубашку и повязала галстук."
    cs "Всё у тебя в порядке милая. А раз так поработаешь за место меня, сегодня ночью в медпункте."
    un "Что нужно делать?"
    cs "Вот, смотри. Видишь коробки?" 
    cs "Сегодня утром лекарства привезли, нужно внести их в картотеку и разложить."
    "Медсестра протянула журнал."
    cs "В эту колонку записываешь наименование, а в эту артикул, вот сюда количеству, ничего сложного. Поняла?"
    un "Да."
    cs "Пойдём, мне закрыть медпункт надо." 
    cs "Ключ будет под ковриком на входе." 
    cs "Я скажу Оле освободить тебя от работы на день."
    window hide
    
    play ambience ambience_camp_center_day fadeout 1 fadein 2
    play sound [sfx_open_door_1, sfx_close_door_1]
    pause 1
    scene bg ext_aidpost_day
    show cs normal at center
    with dissolve
    show pi normal aonl_pioneer far at right behind cs with dissolve
    window show
    "На улице мы увидели Семёна в отдалении, я как всегда опустила глаза, а медсестра наоборот провожала его внимательным взглядом."
    show cs smile with dspr
    cs "Зверь на ловца бежит."
    un "Что?"
    show cs normal with dspr
    cs "Ничего. Пойдём."
    "Даже не закрыв медпункт, она направилась к Семёну. Мне осталось лишь поспешить за ней."
    show cs smile with dspr
    cs "Привет… Пионер!"
    show pi normal aonl_pioneer at right with dissolve
    "Она подошла ещё ближе."
    cs "Никак заболел, подлечиться пришёл?"
    me "Да нет… Я… Гулял просто…"
    cs "Понятно всё с тобой, прогульщик."
    cs "Ну раз уж пришёл, у меня для тебя есть ответственное поручение."
    cs "Вы мне поможете составить опись лекарств, которые сегодня привезли."
    show pi surprise aonl_pioneer with dspr
    un "Привет…"
    "Я выглянула у медсестры из-за спины."
    cs "После ужина приходите сюда, я Лене всё объяснила, она тебе потом расскажет."
    "Семён удивлённо нахмурился."
    me "Получается, сегодня кто-то приезжал в лагерь?"
    cs "Да, с утра из райцентра, а что?"
    show pi normal aonl_pioneer with dspr
    me "Нет, ничего, просто…"
    cs "Вот и славно… Пионер! Значит, вечером, после ужина, сразу сюда!"
    un "Может быть, я…"
    un "Я и одна могла бы справиться."
    cs "Там много – несколько коробок, ты что!"
    cs "Тем более пионер всегда готов!"
    show cs shy with dspr
    cs "Так… Пионер?"

    if aonl_sl_story:

        me "Знаете, меня Ольга Дмитриевна попросила вечером ей помочь…"
        show cs normal with dspr
        cs "Да? И чем это ты собрался ей помогать?"
        "Виола смерила юношу скептическим взглядом."
        me "У неё в домике… В домике уборку сделать, вещи разобрать и всё такое."
        me "Вы же знаете, какой у неё там бардак!"
        cs "Небось и шкафчик свой попросила разобрать!"
        "В голосе Виолы сквозил не скрываемый сарказм."
        cs "Ладно уж… Пионер. Без тебя разберёмся."
        me "Ты извини, просто, понимаешь…"
        th "Кажется, ему стыдно смотреть мне в глаза."
        "Я опустила взгляд в пол, снова покраснев."
        un "Да ничего… Я сама…"
        me "Ну, ладно тогда."
        "Повисла неловкая пауза."
        un "Ну, пока тогда?"
        me "Пока!"
        stop music fadeout 2
        hide pi with dissolve
        "Семён развернулся и быстро зашагал прочь от медпункта."
        "Виола, некоторое время смотрела ему вслед о чём-то размышляя. Наконец, она вздохнула."
        cs "Ну ничего Лен. В жизни всякое бывает."
        un "Вы о чём?"
        cs "Скоро поймёшь милая. Можешь идти, у тебя забот на вечер полно, так что отдыхай."
        un "С-спасибо."
        show cs shy with dspr
        cs "Не за что лапа моя."

    if aonl_un_story:
    
        me "Хорошо, я приду..."
        show cs smile with dspr
        cs "Вот и славно!{w} Так и должен отвечать настоящий пионер!"
        "Семён посмотрел на Виолу как-то скептически."
        stop music fadeout 2
        th "Ему такое не нравится?"
        show cs normal at center with dspr
        cs "Тогда можете идти."
        play music music_list["timid_girl"] fadein 5
        hide cs with dissolve
        "Оставшись наедине, мы направились к площади."
        window hide
        
        scene bg ext_square_day 
        show pi normal aonl_pioneer
        with dissolve
        window show
        un "А куда ты дальше?"
        "Сёма дёрнулся, сфокусировав на мне взгляд."
        "Мы уже давно просто стояли на площади, так и не разойдясь по делам."
        un "Только ты не забудь, хорошо?"
        show pi upset aonl_pioneer with dspr
        me "Не забыть чего?"
        th "Он что забыл, да о чём только думает?" 
        th "Где он всё время витает?!" 
        th "О Славе, наверно, думает!"
        un "Ну, как же… Вечером, после ужина… В медпункт…"
        "Под моим сердитым взглядом юноша стушевался."
        show pi smile aonl_pioneer with dspr 
        me "Конечно, конечно! Сразу после ужина, я весь в твоём распоряжении."
        th "В моём распоряжении??!!"
        "От этой фразы и мыслей, последовавших за ней, я опять смутилась" 
        "Семён тоже пребывал в аналогичном состоянии." 
        "Видимо понял, что ляпнул."
        me "А почему медсестра именно тебя попросила?"
        un "Не знаю.{w} Я просто сидела на лавочке, читала книжку, а она подошла и…" #добавить что Лена взяла новую книгу в дне 2(Вовины слова)
        show pi normal aonl_pioneer with dspr
        me "Ясно.{w} Ладно, тогда после ужина…"
        un "Хорошо.{w} Я пойду тогда?"
        me "Да, конечно!"
        stop music fadeout 3
        "Я направилась в сторону библиотеки, а Сёма остался на площади."

    window hide
    scene bg ext_library_day with dissolve
    window show
    th "Снова освободили от работы, прям везёт в последнее время." 
    th "Теперь свободна до вечера." 
    th "Может сходить к Жене в библиотеку?"
    th "Хотя, мы её вчера все наверно достали, вряд ли она обрадуется."
    th "Лучше картину напишу в мастерской."
    window hide
    
    play ambience ambience_library_day fadeout 1 fadein 2
    play sound [sfx_open_door_1, sfx_close_door_1] 
    pause 1
    scene bg int_library_day with dissolve
    window show
    "Я на цыпочках прокралась в библиотеку."
    window hide
    
    scene cg d2_micu_lib with dissolve
    window show
    "Девушка, как всегда, изучала лбом стол."
    window hide
    
    play music music_list ["trapped_in_dreams"] fadein 2
    #Аналогично с музыкой, вариант не окончательный. 
    scene bg aonl_artist_day with dissolve2
    window show
    "Стараясь не шуметь, прокралась в мастерскую."
    "Теперь можно и приступить к делу." 
    "Вот только став перед мольбертом, я снова замерла в нерешительности."
    th "А что нарисовать?"
    "Почти за полторы недели в лагере, ничего не осилила." 
    "Каждый раз руку словно парализовывало и я не могла сделать ни штриха."
    "Вот и сейчас рука с кистью замерла в нерешительности."
    "Раньше такого никогда не бывало." 
    "Наоборот, думала, что по приезду сюда, на свежем воздухе начну творить без остановки." 
    th "Может, у меня творческий кризис?"
    # play sound 
    show mz rage pioneer glasses with dissolve 
    mz "А ну кто здесь…!"
    "Библиотекарь влетела резко, распахнув дверь, подобно разъярённому медведю в берлогу."
    un "Ой!!! Жень это я, я."
    "Сама того не заметив, закрылась палитрой и кистью от девушки."
    show mz normal pioneer glasses with dspr 
    mz "Лена? Что ты тут делаешь?"
    un "Хотела порисовать."
    show mz bukal pioneer glasses with dspr
    mz "Могла б предупредить, я подумала опять кто-то из младших отрядов безобразничает."
    un "Извини."
    show mz smile pioneer glasses with dspr 
    mz "Да ладно уж."
    "Девушка махнула рукой."
    mz "Как успехи?"
    un "Да вот…"
    "Я расстроено указала на мольберт."
    un "Совсем нет вдохновения. Даже начать не могу."
    mz "Мда, так ты точно далеко не уедешь. Хотя…"
    mz "Скажи, а стенгазету оформить сможешь?"
    un "Что именно?"
    mz "Ну вот, смотри."
    "Девушка достала из кармана сложенные листочки и разложила их на столе."
    show mz shy pioneer glasses with dspr 
    mz "Представь, что стол — это ватман, нужно на нём именно в такой последовательности расставить надписи." 
    mz "Текст я сама напишу твоя задача сделать для них красивые рамки ну и вокруг нарисовать, что-нибудь." 
    mz "Например, там, где про футбол мячик и ворота. А вот тут, где про новое меню в столовой можно нарисовать поварёшку и кастрюлю." 
    mz "Справишься?"
    un "Ну… Да, наверно."
    show mz smile pioneer glasses with dspr
    mz "Вот и отлично, приступай."
    un "Жень постой…"
    show mz normal pioneer glasses with dspr 
    mz "Что?"
    un "Мне бы помощь, кисточки помыть краски размещать, ватман обрезать и придержать…"
    show mz bukal pioneer glasses with dspr
    mz "ОХ!"
    "Женя картинно подняла глаза к потолку."
    show mz normal pioneer glasses with dspr 
    mz "Давай!"
    "Всё время до обеда мы занимались стенгазетой. Женя хоть и бубнила недовольно под нос, но свои обязанности выполняла чётко и своевременно. Так что работа спорилась."
    play sound sfx_dinner_horn_processed
    un "Как раз к обеду закончили, осталось лишь написать текст."
    un "Жаль с текстом не успели."
    show mz smile pioneer glasses with dspr
    mz "Ничего страшного Лен, вечером закончу."
    un "Вечером? А ты разве не пойдешь на танцы?"
    show mz normal pioneer glasses with dspr
    mz "Для чего? Дрыгать ногами под музыку?"
    show mz angry pioneer glasses with dspr
    extend  " ФУ!"
    "Девушка презрительно сморщила носик."
    un "Ну, может пригласит кто…"
    mz "Меня не кому приглашать!"
    "Резко оборвала Женя."
    mz "А тебя кто-то пригласил?"
    un "Н-нет…"
    show mz bukal pioneer glasses with dspr
    mz "Ну так зачем идти? А хотя..."
    mz "Делай, что хочешь."
    hide mz with dissolve
    "Женя резко развернулась и ушла из комнаты, оставив меня в растерянности."
    stop music fadeout 2
    th "Видимо, не стоило трогать эту тему."
    "Убрав вещи и, прижав ватман пеналами и стаканами, чтобы просох, я поспешила в столовую."
    window hide
    
    play ambience ambience_camp_center_day fadeout 1 fadein 2
    scene bg ext_dining_hall_near_day with fade2 
    window show
    "К счастью, очереди снаружи уже не было."
    window hide
    
    play ambience ambience_dining_hall_full fadeout 1 fadein 2
    play sound ["<to 1.0>" + sfx_open_door_2, sfx_close_door_1] 
    pause 1
    scene bg int_dining_hall_people_day with dissolve
    show us smile sport at left
    show sl normal pioneer at right
    with dissolve
    window show
    "Осмотревшись, я заметила свободное место за столиком Ульяны и Слави." 
    th "Вариант вполне приемлемый."
    un "Можно?"
    sl "Садись, конечно."
    show us laugh sport with dspr
    us "Если не боишься. Хи-хи."
    show sl serious pioneer with dspr
    sl "Ульяна!"
    show sl normal pioneer
    show us smile sport
    with dspr
    "Мелкая притихла."
    "Я расположилась поближе к Славе, следя за своим обедом, дабы там не появились элементы китайской кухни."
    show pi normal aonl_pioneer at center behind us, sl with dissolve
    me "Не возражаете, если я присяду?"
    "За своими наблюдениями, совершенно не заметила, как подошёл Семён."
    th "Что же делать? Вдруг я опять начну краснеть? Тут ещё и девочки!"
    show sl smile pioneer with dspr
    sl "Да, конечно!"
    us "Будь любезен."
    "Я промолчала, изучая взглядом свою тарелку."
    sl "Пойдёшь сегодня на танцы?"
    th "Зачем ей это знать? Неужели планирует пригласить?"
    "Я искоса посмотрела на Славю."
    show pi upset aonl_pioneer with dspr
    me "Не знаю."
    show us grin sport with dspr
    us "Пойдёт, пойдёт! Куда он денется!"
    me "Ты-то точно пойдёшь…"
    us "Конечно, не упущу случая посмотреть, как ты опростоволосишься."
    show pi normal aonl_pioneer with dspr
    me "А ты?"
    th "Он это меня спросил?"
    th "Надо что-то ответить!"
    un "Да…"
    th "Вдруг он хочет меня пригласить!"
    "На лице сама собой появилась улыбка."
    show sl smile2 pioneer with dspr
    sl "Видишь, значит, и тебе стоит пойти."
    th "Она явно его зазывает, но может просто как помощница вожатой?"
    show us smile sport with dspr
    us "Не забудь фрак надеть."
    me "Ты-то в чём придёшь, массовик затейник?"
    us "Се-к-рет!"
    show pi grin aonl_pioneer
    show us angry sport
    with dspr
    me "Платьице как на детском утреннике небось?"
    th "А вот это он зря."
    "Ульяна раскраснелась от злости."
    us "Нет, костюм химзащиты! Специально, чтобы от тебя не заразиться!"
    show pi angry aonl_pioneer with dspr
    me "И чем ты планируешь от меня заразиться, интересно?"
    th "Дело принимает плохой оборот."
    show sl sad pioneer with dspr
    sl "Ребята, хватит! Не ссорьтесь!"
    th "Похоже и Славя почувствовала неладное."
    us "Слабоумием, конечно же!"
    th "Мелкая слов не понимает."
    me "Знаешь, если ты уже болеешь гриппом, то простудиться не получится."
    us "Это ты на что намекаешь?"
    me "Да так, ни на что…"
    "Юноша отвернулся, скрывая ехидную улыбку."
    th "Ох, Сёма куда ты лезешь!"
    us "То есть ты хочешь сказать?.."
    show pi normal aonl_pioneer with dspr 
    me "Я ничего не хочу сказать."
    "Ульяна стала краснее помидора."
    th "Нужно вмешаться пока не поздно!"
    un "Ребята…"
    "Вот и всё на, что меня хватило, но Семён вроде насторожился, кинув на меня обеспокоенный взгляд."
    us "Ты допросишься!"
    show pi smile aonl_pioneer with dspr
    me "Чего? Что ты поумнеешь наконец?"
    play music music_list ["awakening_power"] fadein 2
    th "Катастрофа!"
    play sound sfx_borshtch
    "Я хотела встать, чтобы отгородить Семёна от Ули, но мелкая уже вылила тарелку борща ему на голову."
    show pi angry aonl_pioneer with dspr
    me "Ах ты, маленькая…"
    "Рыжая выскочила из-за стола, но Семён схватил её за руку. Ульяна плеснула парню компотом в лицо, и он отпустил егозу."
    hide us
    hide pi
    hide sl
    with dissolve
    "Дальше в столовой началось действо, которое иначе как повторное нашествие Мамая на Русь не назвать." 
    "В стороны полетели столы, стулья, посуда, продукты и несколько пионеров."
    window hide
    
    show blink     
    pause 1
    window show
    "Мне пришлось отбежать в угол и присев на корточки закрыть голову руками, как раз пропуская разбившуюся надо мной тарелку." 
    "Когда в столовой повисла тишина, я рискнула убрать руки от головы и открыть глаза."
    window hide
    
    hide blink
    show unblink
    pause 1
    window show
    "Семён и Уля раскрасневшиеся стояли напротив друг друга, тяжело дыша и в ярости сжимая кулаки."
    show pi angry aonl_pioneer
    show us angry sport at cleft
    with dissolve
    me "Больше не будешь так?!"
    us "А ты?!"
    show mt angry pioneer behind us, pi at cleft with dissolve
    "Ольга Дмитриевна неспешно и хладнокровно подошла к ним сзади, как кобра перед броском."
    show us fear sport
    show pi surprise aonl_pioneer
    with dspr
    mt "Ну что, довольны собой?!"
    "Её голос твёрд и холоден, как сталь гильотины перед казнью."
    th "Ох Сёма куда ты ввязался!" 
    show mt rage pioneer with dspr
    mt "И кто всё это должен убирать теперь?!"
    mt "Кто, я спрашиваю, кто?!"
    th "Вот и прогремел гром."
    show us dontlike sport with dspr
    us "Он!"
    show pi normal aonl_pioneer with dspr 
    me "Она."
    mt "Оба!"
    show us angry sport with dspr
    us "Вот ещё! Не буду я ничего убирать!"
    us "Это он всё! Он начал!"
    show pi angry aonl_pioneer with dspr
    me "Вот и нет!"
    us "Вот и да!"
    show mt angry pioneer with dspr
    mt "Я не собираюсь разбираться в этих глупостях!"
    mt "Приговор окончательный обжалованию не подлежит."
    th "Лучше уйти от греха подальше."
    window hide
    
    stop music fadeout 2
    play ambience ambience_camp_center_day fadeout 1 fadein 2
    play sound [sfx_open_door_1, sfx_close_door_1]
    pause 1
    scene bg ext_dining_hall_near_day with dissolve
    window show
    th "Надеюсь, вожатая не сильно поругает Сёму." 
    th "А Ульянка давно напрашивалась." 
    th "Мелкая наверняка будет в ярости после случившегося." 
    th "Если попадусь на глаза ей на глаза, то она отыграется на мне." 
    th "Лучше до вечера спрячусь в лесу."
    window hide
    
    play ambience ambience_forest_day fadeout 1 fadein 2
    scene bg ext_path2_day with dissolve2
    window show
    #Здесь не тот фон, нужен другой
    "Я вышла на полянку, которую давно приметила." 
    "Лёгкий шелест листвы от ветра, иногда пробегающие белочки в ветвях, где-то вдалеке чирикает птица, а зелёная сочная трава будто сама ложится под ноги."
    "В таких местах кажется, что никакого лагеря, да и самого мира не существует." 
    "Есть только я и девственная природа, не тронутая человеком."
    "Облокотившись о ствол дерева и закрыв глаза, придалась мечтам о тишине и уюте вдали от людей."
    
    if aonl_sl_story:
    
        sl "Как погода?"
        "Неожиданный вопрос вывел из идиллического настроения." 
        "Я подскочила так резко, что едва не упала на землю, зацепившись за корень."
        "Оглядевшись никого не увидела."
        "В ответ на вопрос раздалось мужское бормотание."
        th "Кто здесь?"
        window hide
        
        play music music_list ["just_think"] fadein 2
        scene bg ext_polyana_day
        show pi normal aonl_pioneer at right 
        show sl normal pioneer at left 
        with dissolve
        window show
        "Выглянув из-за дерева, я увидела сидящего на поляне Семёна и стоящую рядом Славю."
        #Нужен звук ветерка
        "Поднявшийся ветер заглушил их голоса."
        th "Что они здесь делают?"
        hide sl
        hide pi
        with dissolve
        "Через некоторое время Семён поднялся и пошёл следом за девушкой."
        th "Куда она его ведёт?"
        "Немного выждав, чтобы меня не заметили, пошла за ними."
        "Не то чтобы я шпионила, но…" 
        "Очень интересно, что на поляне делал Семён и зачем он Славе?"
        window hide
        
        play ambience ambience_camp_center_day fadeout 1 fadein 2
        scene bg ext_square_day with dissolve
        window show
        th "Блин, я их потеряла!"
        th "Куда же они могли направиться?"
        show dv normal pioneer with dissolve
        "Пока размышляла на площадь пришла Алиса."
        th "Не было печали, сейчас опять будет издеваться."
        window hide
        show dv grin pioneer with dissolve
        pause 1
        hide dv with dissolve
        window show
        "Но Алиса, глянув на меня, лишь ехидно улыбнулась и ушла."
        th "С чего это она вдруг, не заболела ли часом?" 
        th "А это идея." 
        th "Может Славя отвела Семёна в медпункт?"
        window hide
        
        scene bg ext_aidpost_day with dissolve
        window show
        "Заглянув через окна, я никого не обнаружила."
        th "И тут мимо. Что ж, схожу в библиотеку и посижу там до ужина."
        th "Может, интересная книжка под руку подвернётся."
        window hide
        
        scene bg ext_library_day with dissolve
        # play sound
        window show
        "Подходя к зданию, я вдруг услышала грохот и падение чего-то тяжёлого."
        "Уже через секунду была у ближайшего окна, а там…"
        window hide
        
        scene cg d3_sl_library with dissolve
        window show
        th "Нет, нет, нет!!! Этого просто не может быть, Семён не мог так поступить!" 
        th "Или мог? Я ведь его плохо знаю, и мы почти не общались..." 
        th "Но даже если и мог, то Славя! Она же отличница, активистка, помощница вожатой..." 
        th "Нет, она не такая, она бы не стала так кидаться на шею, но всё же…"
        play sound sfx_dinner_horn_processed
        "Парочка на полу зашевелилась, пытаясь подняться." 
        "Я отпрянула от окна и побежала прочь."
        stop music fadeout 2
        window hide
        
        scene bg ext_square_day with fade2
        pause 1
        scene cg d3_sl_library 
        show prologue_dream
        with flash
        window show
        "Эта картина снова встала перед глазами."
        th "Нет, нет, нет... Не хочу думать…" 
        th "Но почему… Чем я хуже?!"
        window hide
        scene bg ext_square_day with fade2
    
    if aonl_un_story:
        "Вдруг рядом раздался топот ног, словно попала на тропу миграции слонов."
        "Затем звук бега одного из участников резко прекратился и послышался грохот от столкновения, падения кого-то на землю, перекатывание по траве и тяжкое дыхание."
        play music music_list["eternal_longing"] fadein 2
        me "Догнал!"
        "Крикнул кто-то с поляны."
        "Аккуратно выглянула из-за дерева, не выдавая себя."
        window hide
        
        # Ульяна и Семён на траве (оригинал не подойдёт - глаза семёна)
        scene cg aonl_us_and_se_d3 Ульяна with dissolve 
        window show
        us "И ничего не догнал…"
        "Смущённо ответила мелкая."
        "Ульяна лежала прямо под ним, их лица совсем рядом."
        th "Чем они занимаются? Нет, не может быть! Сначала Славя теперь Ульяна…" 
        th "Сёма да ты кобель и она же ещё маленькая!"
        "Я глазам своим не верила."
        us "Насиловать будешь?"
        "Она согласна?!"
        me "А ты хочешь?"
        "Мелкая ответила без раздумий."
        us "А то!"
        "Егоза хитро улыбнулась и тихо хрюкнула."
        me "А вот я что-то не очень…"
        us "Ну и ладно!"
        stop music fadeout 3
        play sound sfx_punch_medium
        with vpunch
        pause(1)
        "Ульянка больно укусила его за нос."
        "От неожиданности Сёма немного привстал."
        window hide
        
        scene bg ext_path2_day with dissolve
        show pi normal aonl_pioneer at left 
        show us grin sport at center 
        with dissolve
        window show
        "Воспользовавшись случаем, мелкая отбежала на безопасное расстояние."
        show us laugh sport far at center with dissolve
        us "Смотри!{w} Потом пожалеешь!"
        hide us with dissolve
        stop music fadeout 2
        "Она громко засмеялась и скрылась в лесу."
        "Рядом с Сёмой остался лежать пакет с конфетами."
        th "Он поэтому за ней гнался?"
        "Сёма взял их в руки размышляя, а потом направился в лагерь."
        play music music_list ["just_think"] fadein 2
        "Я словно тень двинулась следом."
        window hide
        
        play ambience ambience_camp_center_day fadeout 1 fadein 2
        scene bg ext_dining_hall_away_day
        show pi normal aonl_pioneer far
        with dissolve
        pause 1
        scene bg ext_dining_hall_near_day 
        show pi normal aonl_pioneer far
        with dissolve
        play sound [sfx_open_door_1, sfx_close_door_1]
        pause 1
        scene bg int_dining_hall_day 
        show sl normal pioneer at left
        show pi normal aonl_pioneer at right
        with dissolve
        window show
        "Сёма зашёл в столовую, а я наблюдала через окна, там его уже ждала первая красавица «Совёнка»."
        th "Что ей от него надо?!"
        show sl smile pioneer with dspr
        "Не знаю о чём они говорили, но вскоре Славя забрала конфеты и скрылась у буфета." 
        "Через минуту она вернулась и снова заговорила с Семёном мило улыбаясь."
        th "Да она же кокетничает! А Сема?"
        show pi normal aonl_pioneer with dspr
        "Юноша улыбался в ответ."
        th "Ах ты…"
        "От негодования у меня перехватило дыхание."
        window hide
        
        scene bg ext_square_day with dissolve
        window show
        "Я отпрянула от окна и пошла на площадь, ужинать совершенно расхотелось."
        th "Неужели, Славя всё-таки имеет на его счёт планы?"
        show dv normal pioneer with dissolve
        "Пока размышляла на площадь пришла Алиса."
        th "Не было печали, сейчас опять будет издеваться."
        show dv smile pioneer with dspr
        hide dv with dissolve
        "Но Алиса, глянув на меня, лишь ехидно улыбнулась и ушла."
        th "С чего это она вдруг, не заболела ли часом?"
        th "Впрочем, какое мне дело, посижу в домике до вечера."
        "Встав с лавочки, я быстрой походкой двинулась домой."
    
    show mi shocked pioneer  with dissolve
    window show
    mi "Ой Лен, а куда ты так бежишь?"
    th "Мику не до тебя сейчас."
    show mi upset pioneer with dspr
    mi "Ой Лен, постой."
    th "Не до тебя!"
    show mi dontlike pioneer with dspr
    mi "Лен, ты слышишь?"
    "Девушка схватила меня за руку. Я резко вырвалась."
    show mi shocked pioneer with dspr
    un "Отстань!"
    "Мику в испуге отшатнулась."
    mi "Лен, почему ты такая злая? Это из-за того, что грязная?"
    th "Вот умеет же она влезть, когда не просят..." 
    th "Стоп, что?!"
    un "Ты о чём Мику?"
    show mi upset pioneer with dspr
    mi "У тебя гречка на макушке, и красное пятно на плече, а ещё кажется жирный след сзади на юбке, что случилось?"
    "Я зашарила руками на голове и осмотрела одежду."
    un "Нет!"
    show mi shocked pioneer with dspr
    "От моего крика Мику повторно отшатнулась."
    th "Надо мной же тарелка с едой разбилась, а я дура не осмотрела себя! И так ходила целый день!" 
    th "А если бы Семён увидел такую замарашку?!"
    "От ужаса на глаза навернулись слёзы."
    show mi sad pioneer with dspr
    mi "Лен, бедненькая почему ты плачешь? Кто тебя обидел?"
    un "Мику, прости. Я дура, потом всё расскажу. Сейчас надо в домик бежать, умыться."
    stop music fadeout 2
    show mi upset pioneer with dspr
    mi "Да-да-да конечно Лен, пойдём, я тебя прикрою!"
    "Девушка встала сбоку, стараясь прикрыть от идущих пионеров."
    th "Повезло мне с соседкой, если бы не она так бы и пришла на ужин, став посмешищем всего лагеря." 
    th "Благо никто больше не видел, хотя кто-нибудь из встречных мог и предупредить как Мику…"
    th "Хотя минутку…"
    th "Алиса овца!!! Могла бы сказать, такое слишком даже для неё!!!"
    mi "Лен, почему ты так задрожала?"
    un "Неважно, пойдём."
    window hide
    
    scene bg ext_washstand2_day with dissolve
    play sound sfx_open_water_sink
    pause 1
    play sound_loop sfx_water_sink_stream
    window show
    "Соседка помогла вымыть гречку из волос и расчесала своей расчёской."
    window hide
    
    stop sound_loop
    play sound sfx_close_water_sink
    play ambience ambience_int_cabin_evening fadeout 1 fadein 2
    scene bg int_house_of_un_day #sunset?
    show mi dontlike pioneer 
    with fade2
    play music aonl_wake_up fadein 2
    window show
    "Осмотрев сменную одежду, девушка сразу её забраковала."
    mi "Она ещё не просохла, будет прилипать в разных местах, так нельзя."
    un "Но мне больше нечего надеть, может одолжишь свою сменку?"
    show mi shy pioneer with dspr
    mi "Лен, я бы рада, но у нас комплекция абсолютно разная, особенно в груди и бедрах. На тебя ни один мой наряд не налезет."
    un "Эх, что же делать, мне ещё Виоле в медпункте помогать?"
    show mi smile pioneer with dspr
    mi "А надень платье для танцев!"
    un "У меня его нет."
    show mi shocked pioneer with dspr
    mi "Как нет?"
    un "Я только с формой приехала, у нас с родителями денег мало… Я не хочу об этом говорить."
    show mi sad pioneer with dspr
    mi "Извини милая, не буду. Может в шкафу, что найдётся?"
    play sound sfx_cellar_open
    "Открыв шкаф, я увидела лишь тонкий верёвочный шнурок да небольшую тряпицу синего цвета на одной из полок."
    mi "Ммм, не густо."
    play sound sfx_close_cabinet
    "Мику села на свою кровать, приземлившись на какую-то белую ткань с узором."
    un "Что это?"
    show mi surprise pioneer with dspr
    mi "А это? Старые занавески, ну, в смысле… Уже не годные, потому что порвались, видишь? Не знаю теперь, куда пустить, а выбрасывать жалко."
    show mi grin pioneer with dspr
    "Мику ощупала ткань рукой, у неё вдруг загорелись глаза и, открыв ящик тумбочки, взяла большие портновские ножницы."
    th "Кажется, я догадалась!"
    un "Ты хочешь?!"
    mi "Да! Раздевайся золушка, будем делать тебя принцессой."
    un "Мы можем не успеть и тебе на ужин надо."
    show mi happy pioneer with dspr
    mi "Вдвоём успеем. А ужин бог с ним!"
    "Японочка взяла линейку и стала снимать с меня мерки. Когда закончила, я молниеносно начертила на листе бумаги эскиз наряда своей мечты." 
    "Вооружившись полученным материалом как подсказкой, мы принялись размечать контуры заготовок для платья на двух занавесках и затем вырезать по ним."
    "Недаром, однако, в Школе искусств для меня прошли занятия по дизайну одежды!"
    window hide
    
    with fade2
    window show
    "Пока я по семь раз меряла и отрезала, Мику успела сбегать в свой клуб за швейной машинкой. Она донесла её до дома в одиночку и чуть не упала от усталости, но я вовремя подхватила тяжесть и поставила прибор на пол."
    un "Тяжёлая какая! Это сколько же у тебя там всего накопилось?"
    show mi upset pioneer with dspr
    mi "Много… Когда я сюда приехала, в том здании вообще был склад. Часть вещей Шурик помог утащить в свой с Электроником клуб."
    un "Интересно. А люди у тебя вообще там имеются?"
    show mi normal pioneer with dspr
    mi "Пока только я и Алиса. Ещё Шурик очень хотел, но Электроник возмутился и сказал, что он и так активист в их кибернетическом уголке."
    th "Как же ей, наверное, ужасно одиноко в своём кружке, в который никто, судя по всему, не хочет вступать!" 
    th "И обиднее всего то, что я ничем не могу помочь..."
    window hide
    
    with fade2
    window show
    "Ручная работа с иглой и нитью отняла бы у меня ещё несколько часов кряду, но с машинкой удалось быстро сшить заготовки друг с другом." 
    "Закончив работу, я взяла платье в руки и приложила к своей фигуре, убедившись, что размер такой, какой и был необходим. Только тогда с облегчением выдохнула."
    un "Уф-ф-ф… Ну вот и всё."
    mi "Нет, не совсем. Смотри, как плохо мы отрезали!"
    "Приглядевшись, увидела, что у занавески был крупный разрыв, который как раз пришёлся на переднюю сторону платья. Он шёл сверху посередине и доходил где-то до уровня пупка."
    th "Мне за такое «декольте» Ольга Дмитриевна сразу выговор сделает! Надо что-то срочно предпринять…"
    "Недолго думая, схватила синюю тряпку из шкафа и, подложив под дефект, сшила её с тканью. Тем самым можно было гарантировать, что материал не порвётся сильнее, а будет удерживаться."
    mi "Теперь точно всё! Можешь примерить?"
    un "Да, конечно. Но мы с тобой много трудились, должно быть впору…"
    "Складки на полах сразу давали понять, что послужило материалом для наряда, но они же придавали ему какую-то очень интересную форму." 
    "В качестве последнего штриха я повязала верёвочный шнурок на своей талии для того, чтобы ткань хорошо легла, и подошла к зеркалу."
    window hide
    
    play sound sfx_cellar_open
    scene cg aonl_d6_un_mirror_1 with dissolve
    window show
    "Платье действительно идеально подходило фигуре и моим волосам, подчёркивая их цвет." 
    "Может, оно и не сияло, как те, что носят знаменитости, но создавало уютную атмосферу, комфорта." 
    "Есть такой тип одежды — уютная. В ней чувствуешь себя увереннее, и делать что-либо становится проще."
    scene cg aonl_d6_un_mirror_2 with dspr
    th "Не хватает лишь улыбки…"
    "Пронеслось в голове."
    "Улыбнувшись, я понравилась даже сама себе."
    mi "Ах, как мило! Ну разве не принцесса?"
    "Завосторгалась Мику, зажмурившись и захлопав в ладоши."
    mi "Нет, не принцесса… Королевна!"
    "И мы обе засмеялись."
    window hide
    
    play sound sfx_close_cabinet
    scene bg int_house_of_un_day #sunset?
    show mi smile pioneer
    with dissolve
    window show
    un "Не знаю даже, как и благодарить тебя…"
    mi "Да меня-то и не за что! Это тебе спасибо, не придётся хоть заморачиваться с тем, куда это деть! А то так бы замоталась, знаешь… Я ведь и Алисе предложить собиралась."
    un "Всё равно спасибо! Ладно, мне пора…"
    show mi grin pioneer with dspr
    mi "Удачи! Платье шик! Думаю, Семён и другие тоже его оценят!"
    "Услышав знакомое имя из уст Мику, я вся покраснела."
    stop music fadeout 2    
    "Впрочем, вряд ли она уже могла это видеть, потому что ноги несли по улочкам лагеря на главную площадь."
    window hide
    
    play ambience ambience_camp_center_night fadeout 1 fadein 2
    scene bg ext_square_night_party2 with fade2
    $ night_time()
    $ persistent.sprite_time = "night"
    window show
    th "Только выйдя на площадь задумалась." 
    th "А зачем сюда пришла? Мне же нужно в медпункт."
    th "Видимо, от увлечения шитьём, я так вдохновилась, что как золушка прибежала на бал." 
    th "Ладно, посижу здесь немного, чтобы отметиться и пойду в медпункт."

    if aonl_sl_story:
        th "Пусть и уверяла себя в обратном, но внутри теплилась надежда, что он пригласит."
        th "А с Виолой я потом объяснюсь, завтра всё сделаю."
        th "Вполне возможно случай со Славей просто недоразумение, я же слышала грохот до этого, следовательно, он свободен…"
        th "И всё равно мне неудобно."
    
    if aonl_un_story:
        "Я присела на лавочку ожидая Семёна, надеюсь он не забыл." 
        "Площадь постепенно наполнялась народом."
    
    th "Ох, а я ведь… Никогда в жизни не бывала на подобных мероприятиях и всегда любыми способами избегала их." 
    "Я просто находилась в каком-то непонятном состоянии предвкушения, смешанного с необъяснимым волнением."
    "Ночь встретила меня свежим, прохладным воздухом."
    "Вокруг становилось темно, и только тысячи звезд светили, посылая свет через миллионы километров."
    "Ничто не нарушало тишины, только сверчки переговаривались между собой."
    "Деревья украшали гирлянды, а пионеры со всего лагеря стремительно заполняли площадь. Почти с каждого её уголка был слышен смех и разговоры."
    show pi normal aonl_pioneer far with dissolve 
    th "Ой, вот и Сёма! Слава Богу, он пришёл…"
    show us smile dress far at right with easeinright
    "К нему подбежала наша красная ракета. Они о чём-то поговорили, и мелкая сбежала."
    hide us with easeoutright
    th "Фух, в этот раз без эксцессов."
    "Я старалась быть на виду, но он не замечал меня."
    show sl smile dress far at right with easeinright
    "Тем временем к юноше подсела Славя, и они завели непринуждённую беседу."
    th "Она на танец рассчитывает?"
    hide sl with easeoutright
    "Девушка рассмеялась весёлым звонким смехом и пошла к аппаратуре."
    th "Так дело не пойдёт, нужно действовать."
    show pi normal aonl_pioneer with dissolve 
    un "Привет."
    th "Оригинально."
    show pi smile aonl_pioneer with dspr
    me "Ой, привет, и ты тут?"
    un "Да."
    me "Ясно… Зажжешь сегодняшний вечер?"
    "Его колкая фраза заставила меня снова впасть в краску. Да и умение «зажигать» в список моих талантов не входило."
    me "Ладно, может, и правда не стоит, а то все сгорим тут…"
    "Сначала я подумала, что он стебётся надо мной, но по его унылому выражению лица поняла, что Семёну просто скучно и неуютно среди такого столпотворения." 
    "Возможно, даже моя компания в этот момент лишняя."
    un "Ладно, тогда я…"
    me "Ага…"
    "Рассеянно бросил он в ответ."
    th "Вряд ли юноша захочет танцевать вообще с кем-либо…" 
    th "Как и я, впрочем." 
    th "Не в настроении он." 
    th "Может, перестанет хандрить позже?"
    hide pi with dissolve 
    "Я ушла, стараясь отгородиться от неловкой ситуации."
    window hide
    
    play music music_list["lightness"] fadein 4
    scene bg ext_square_night_party with dissolve
    window show
    "И вот действо началось." 
    "Сначала все стеснялись, но Ульянке стеснение не знакомо." 
    "Она выскочила на центр площади и начала беситься под музыку."
    "Вскоре за ней подтянулись другие."
    "Люди полностью отдались заигравшей музыке и атмосфере типичной летней дискотеки." 
    "О чём, впрочем, ни капли не жалели." 
    "А вот мне было не весело."
    th "Никогда не понимала, что может быть весёлого в нелепой тряске руками, ногами и другими частями тела под иногда совершенно нелепую музыку." 
    th "Да ещё и с таким же нелепым текстом песни…"
    th "Впрочем, Ульянка, например, не знает этой простой истины." 
    th "Пускай то, что она вытворяет чуть ли не в самом центре площади, сложно назвать танцем, но зато она отдалась этому «веселью» сполна."
    "Немного побродив в толпе, от которой понемногу становилось противно, нашла Сёму." 
    "Он спокойно сидел на той же лавочке, где поменьше народу и не особо горел желанием развлекаться на всю катушку."
    "Его позвала Ульянка, но юноша проигнорировал." 
    "Музыка не позволила услышать их диалог, но суть ясна и так."
    "Видимо, в интроверсии мы похожи."

    if aonl_sl_story:
        show pi normal aonl_pioneer at left
        show sl smile dress at right
        with dissolve
        "Но вот подошла Славя." 
        "Несколько фраз, она протягивает руку, и он…"
        window hide
        scene cg d3_sl_dance with dissolve
        #Чёрт, надо искать другую цг. Не сходится со сценарием! 
        window show
        #Вот тут не знаю, как быть с музыкой, честно говоря. 
        "И ОН ВЗЯЛ РУКУ!!! Музыка сменилась на медленную."
        "Девушка увела его на площадь, и они закружились в танце!"
        "Минуту я наблюдала за парой остановившимся взглядом."
        "Славя всячески строила ему глазки, буквально гипнотизируя своим обаянием. Сёма просто таял в её руках, не отрывая взгляд от девушки."
        "Я же тем временем старалась проглотить ком в горле."
        "Наконец, шмыгнув носом, развернулась на месте и пошла в медпункт."
        th "Хватит, с меня хватит!"
        window hide
        
        stop music fadeout 3
        scene bg ext_aidpost_night with dissolve2
        window show
        "Толком не воспринимая происходящее, словно в трансе миновала тёмные улочки."
        window hide
        
        play sound [sfx_medpunkt_door_open, sfx_close_door_1]
        pause 1
        play ambience ambience_medstation_inside_night fadeout 1 fadein 2
        play music aonl_disce_libens fadein 3
        scene bg aonl_int_aidpost_night_nolight with dissolve
        window show
        "Зайдя в медпункт не включая свет, я прижалась спиной к двери и всхлипнула."
        th "Они вместе, это даже слепому ясно!"
        th "У меня никаких шансов, он даже платье моё не похвалил!"
        th "А ведь мы с Мику так старались!"
        "Слёзы ручьём хлынули из глаз."
        window hide
        show blink
        pause 1
        window show
        "Спрятав лицо в руки, тихо зарыдала."
        "Теперь я всё поняла…" 
        "Поняла, то что чувствовала с первой встречи, но боялась признаться самой себе." 
        "Глупо отрицать очевидное, уже поздно…"
        th "Разве могут быть шансы у серой мыши вроде меня?!" 
        th "Кому я нужна?!" 
        th "Мы с Сёмой даже толком не поговорили, не узнали друг друга!!!"  
        th "А Славя, активистка, спортсменка, красавица, такая учтивая, такая заботливая. Всегда с ним рядом, всегда поддержит, он ей верит."
        window hide
        hide blink
        show unblink
        stop music fadeout 3
        pause 1
        window show
        "Не знаю, сколько так просидела." 
        "Но, когда убрала руки с лица в голове маячила лишь одна мысль:"
        th "Нельзя им мешать! Нельзя им мешать! Нельзя им мешать!"
        th "Славя замечательная девушка, им вместе хорошо, я здесь третья лишняя." 
        th "Единственное, что могу сделать это пожелать - счастье влюблённым и тихо уйти в закат, ничем не обременяя пару."
        window hide     
        
        scene bg int_aidpost_night with fade2
        window show
        "Сглотнув слёзы, я занялась тем за чем приходила, двигаясь словно автомат со стеклянным взглядом."
        "Примерно через час всё было отсортировано и записано, а я с абсолютной пустотой в душе отправилась в домик."
        window hide
        
        play ambience ambience_int_cabin_night fadeout 1 fadein 2
        scene bg int_house_of_un_night with fade2
        play music music_list ["a_promise_from_distant_days"] fadein 2
        window show
        "Мику уже спала."
        th "Прости подруга, нашу труды прошли впустую."
        "Повесив платье на вешалку, заметила, что другие мои вещи постираны и повешены сушиться."
        "Я с теплотой посмотрела на Мику." 
        "Подруга, да теперь могу действительно назвать её подругой, постирала их во время танцев, дав мне время побыть в эйфории."
        "Погладила её волосы пытаясь хоть так отблагодарить за помощь."
        un "Спасибо Мику."
        "Раздевшись легла спать, стараясь выкинуть Семёна из головы. Не получилось."
        th "Хорошо, что смена скоро закончится."
        th "Скорее бы уже..."
        window hide
        stop music fadeout 2
        stop ambience fadeout 2
        scene bg black with fade2
        pause 4
        jump aonl_day4_alt

    if aonl_un_story:
        window hide
        show pi normal aonl_pioneer at left
        show sl normal dress at right
        with dissolve
        pause 1
        hide sl with easeoutright
        window show 
        "Вскоре подошла Славя, обменявшись несколькими фразами девушка оставила его одного, а Сёма совсем сник."
        th "Нужно срочно его забрать!"
        un "Может, пойдём?"
        show pi surprise aonl_pioneer at left with dspr
        me "Куда?"
        "Юноша удивлённо посмотрел на меня. Неужели опять забыл?"
        un "В медпункт.{w} Если, конечно, ты хочешь остаться и потанцевать…"
        "Сомнительно, что он на такое решится."
        show pi normal aonl_pioneer at left with dspr
        me "Нет, пожалуй, воздержусь!{w} Пойдём."
        stop music fadeout 3
        "Покинув площадь я почувствовала огромное облегчение, да и Сёма явно приободрился."
        window hide
        
        play music music_list["goodbye_home_shores"] fadein 3
        scene bg ext_dining_hall_away_night
        show pi normal aonl_pioneer
        with dissolve
        window show
        un "Ну что, пойдём в медпункт?"
        "Сёма снова вынырнул из своих грёз."
        show pi smile aonl_pioneer with dspr
        me "Да, конечно… Спасибо тебе!"
        un "За что?"
        "Я удивлённо посмотрела на юношу."
        me "Ну, за то, что забрала меня…"
        "Как я и думала – он не любит танцевать."
        me "А то там…{w} Знаешь, так скучно!"
        un "Ты не любишь танцевать, наверное?"
        show pi normal aonl_pioneer with dspr 
        me "Да, совсем не люблю, не моё это как-то."
        un "И я тоже.{w} Меня никогда никто не приглашает."
        "Сказав это, я покраснела и опустила взгляд к земле."
        th "Он теперь подумает, что я совсем дурнушка раз меня не приглашают." 
        th "И кто за язык тянул?"
        me "Странно."
        un "Что странно?"
        me "Ну… Что тебя не приглашают."
        un "Ты так думаешь?"
        "Я удивлённо на него посмотрела."
        "Он смутился встретив мой взгляд."
        show pi smile aonl_pioneer with dspr
        me "Ну, да! Конечно!"
        me "Если бы я любил танцевать, то обязательно бы тебя пригласил."
        th "Он бы меня пригласил? То есть он считает меня красивой?"
        stop music fadeout 2
        un "Спасибо…"
        window hide
        
        scene bg ext_aidpost_night with dissolve
        window show
        "Остальное время я шла молча, раздумывая, он сказал правду или просто проявил вежливость?"
        show pi smile aonl_pioneer with dissolve
        "Сёма тоже молчал, погрузившись в свои мысли."
        "Тьма вступила в свои права, искажая контуры окружающих предметов."
        "Но я абсолютно спокойна, ведь рядом Сёма, если потребуется он защитит."
        "Взгляд Сёмы выражал полное хладнокровие."
        th "Так и знала он смелый!"
        play sound sfx_owl_far
        "Двинувшись к медпункту, услышала, как где-то рядом ухнула сова, но я и бровью не повела, ведь с Сёмой не страшно. Он двигался чуть позади."
        window hide
        
        stop ambience fadeout 2
        scene bg black
        with dissolve
        $ persistent.sprite_time = "day"
        play music music_list["goodbye_home_shores"] fadein 3
        window show
        "Войдя в медпункт, Семён включил свет."
        window hide
        
        play ambience ambience_medstation_inside_night fadeout 1 fadein 2
        scene bg int_aidpost_night
        show pi normal aonl_pioneer
        with flash
        window show
        me "А медсестра позже придёт, да?"
        un "А она не придёт…"
        th "Сёма вытаращил на меня глаза."
        me "Ааа, ясно…"
        th "С чего он так реагирует. А ладно, нужно заняться делом."
        un "Вот эти коробки."
        "Я показала на кучу коробок."
        "Семён поставил одну коробку перед собой на стол и начал выкладывать бинты."
        un "Вот, возьми."
        "Я протянула листок бумаги и объяснила, что нужно делать."
        hide pi with dissolve
        "Работа текла в полном молчании, не так я себе это представляла."
        th "Может поговорить с ним, спросить что-то?" 
        un "Семён…"
        show pi normal aonl_pioneer with dissolve
        me "Что?"
        "Он поднял взгляд на меня."
        "Я несколько секунд колебалась, но потом опять опустила глаза."
        un "Нет, ничего."
        th "Проклятье!"
        "Я так и продолжила изредка поглядывать на Сёму не решаясь заговорить."
        th "Так, а что он делает?!"
        un "Семён…"
        me "Да?"
        un "Ты их по второму кругу считаешь."
        show pi surprise aonl_pioneer with dspr
        me "Ой, прости."
        show pi normal aonl_pioneer with dspr
        "Сёма спешно начал вносить исправления."
        "Я снова отвернулась."
        me "Слушай, а ты откуда? Ну, то есть откуда приехала? В смысле где родилась? Точнее, где живёшь?"
        un "Ну, я…{w} Тут городок рядом есть…"
        me "Тут рядом – это где?"
        un "Тут…"
        "Проклятье, ну почему я так стесняюсь!?"
        me "Это секрет?"
        un "Нет, что ты…{w} Просто…"
        th "Что он обо мне теперь думает?!"
        me "То есть тоже где-то... На юге?"
        un "Да…"
        "Смогла ответить, только когда пересилила себя."
        th "Да что же я сопли жую!" 
        th "Он ведь пытается наладить со мной общение!" 
        th "Значит, и мне надо поддержать беседу." 
        th "Наверное..."
        un "А тебе здесь что, не нравится?"
        th "Хоть так."
        me "Нет, что ты! Конечно нравится!"
        th "Как-то натянуто звучит."
        me "А тебе?"
        un "Да.{w} Тут спокойно, в библиотеке много книг.{w} И люди здесь хорошие…"
        me "Хорошие… Да не все…"
        un "Почему?"
        me "Ну, как бы сказать…{w} Возьми Ульянку – батарейка Энерджайзер с катастрофически смещённой точкой приложения силы."
        un "Батарейка…{w} Что?"
        me "Неважно."
        show pi angry aonl_pioneer with dspr
        me "Или Алиса! «Пионер – всем ребятам пример!» – этот лозунг явно не про неё. Если все будут брать пример с Алисы, то лет через двадцать такое в стране будет…"
        "Напоминание об Алисе больно резануло ухо, возвращая память о нашем знакомстве..."
        window hide
        play music aonl_Lena_dream_1 fadeout 1 fadein 3
        play ambience aonl_amb_hard_rain_outside fadeout 1 fadein 2
        scene cg aonl_d2_un_child_cry1
        show prologue_dream
        with flash
        window show
        #Нужно откопать где нибудь звук дождя
        "На улице, как и в моём сердце, шла гроза, а я сидела в парке на скамейке и горько плакала." 
        "От осеннего холода слёзы замерзли на лице, платьице промокло до ниточки, маленькие босые ножки были испачканы в грязи…" 
        "Но мне было всё равно..." 
        "И я даже не сразу услышала, как ко мне обратился чей-то голос."
        window hide
        scene cg aonl_d2_un_child_cry2
        show prologue_dream
        with dissolve
        window show
        dv "Что случилось? Ты почему такая грустная?"
        "Я подняла взгляд и увидела рыжеволосую девочку, которая стояла рядом со мной и держала в руке зонтик."
        window hide
        scene cg aonl_d2_un_child_cry3
        show prologue_dream
        with dissolve
        window show
        "Услышав рассказ, как меня обижают в садике, достала платок, вытерев им слёзы с щёк." 
        "Затем присела рядом и сказала."
        dv "Давай дружить, Лена?"
        "Я тут же кивнула, услышав это, и обняла рыжеволосую." 
        "А она тихо прошептала."
        dv "Теперь я буду за тобой присматривать. Помни, что куда бы ты ни ушла, что бы с тобой ни произошло, я найду тебя и помогу. Ведь для того и нужны лучшие подруги, правда?"
        "Затем девочка накинула на меня дождевик и проводила до дома." 
        "Так мы с Алисой и познакомились..."
        window hide
        stop music fadeout 2
        play ambience ambience_medstation_inside_night fadeout 1 fadein 2
        scene bg int_aidpost_night
        show pi normal aonl_pioneer
        with flash
        window show
        "Я сморгнула мелкие слёзы, прикрыв лицо рукой, словно зевая."
        th "Подруга…" 
        th "Когда-то мы были подругами…" 
        th "До того дня…"
        play music music_list["goodbye_home_shores"] fadein 3
        un "На самом деле она не такая."
        me "Не такая – это какая?"
        un "Не такая, как ты о ней говоришь."
        me "Начнём с того, что я ничего ещё не сказал.{w} Просто констатировал факт, что она явно не является примером для подражания."
        un "Ну… Может быть."
        me "Получается, ты её хорошо знаешь?"
        un "Наверное."
        "Собравшись с мыслями, я всё же произнесла."
        un "Мы из одного города…"
        un "У нас общие друзья, хоть Алиса и на год старше."
        me "Понятно. Но это немного странно.{w} То есть не так. Я просто удивился."
        un "Все удивляются."
        "Я слегка улыбнулась."
        "Сёма вернулся к работе."
        th "Общение совершенно не ладится. Что же делать?"
        me "Знаешь, я тут книжку недавно прочитал…{w} Ты любишь фантастику?"
        un "Не очень."
        th "Кто меня за язык тянул могла и соврать. А с другой стороны какую бы фантастику ему назвала?"
        me "Ну, раз не любишь…{w} А какие вообще книги тебе нравятся?"
        un "Разные…"
        th "Да блин!"
        "Сёма задумчиво смотрел на меня словно собираясь с мыслями и силами. Наконец он словно решился."
        me "У нас осталась всего пара коробок…"
        un "Да."
        show pi smile aonl_pioneer with dspr
        me "Слушай, у меня идея возникла.{w} Может быть, когда закончим, пойдём в столовую?{w} Мне кажется, магнитофон после танцев отнесли как раз туда."
        th "К чему это он?"
        un "Зачем?"
        me "Ну… Честно говоря, я не умею танцевать, поэтому и не люблю.{w} И поэтому мне там так неудобно было."
        me "Может быть, я бы смог тебя отблагодарить, потанцевав с тобой."
        un "Но я…"
        "От такого поворота я раскраснелась и уставилась ему в глаза."
        show pi normal aonl_pioneer with dspr
        "Юноша смутился и кажется даже пожалел о своём решении."
        me "Нет, если ты не хочешь, конечно…{w} Я же не настаиваю!"
        un "А если нас кто-то увидит?.."
        me "Да все спят уже давно.{w} Тем более кто ночью пойдёт в столовую!"
        un "А как мы туда попадём?"
        me "Ну, эээ…"
        me "А какая музыка тебе нравится?"
        un "Разная…{w} Я ей не особо увлекаюсь."
        show pi smile aonl_pioneer with dspr
        me "Давай тогда представим, что она играет…{w} Ну, то есть, что ты… Мы её слышим."
        un "Как это?"
        me "Ну, как будто она у тебя в голове играет!"
        "Я попыталась вспомнить мелодию."
        un "Не уверена, что у меня получится."
        me "Но можно же попробовать!"
        un "Наверное…"
        "Сёма кивнул!"
        window hide
        
        hide pi with fade2
        window show
        "Мы продолжили работу и вскоре с последней коробкой было покончено."
        show pi normal aonl_pioneer with dissolve
        "Сёма протянул исписанный листок и странно на меня посмотрел."
        "Я прыснула от смеха."
        me "Что?!"
        un "У тебя такое выражение…"
        me "Какое?"
        un "Забавное."
        me "Да?.."
        un "Ага. Так куда пойдём?"
        me "Куда?"
        un "Ну, как же…{w} Танцы."
        "Я застенчиво покраснела одновременно боясь, что он передумал."
        me "А, да! Прости, я задумался просто.{w} Пойдём на пристань."
        me "Или, если тебе не нравится, мы можем…"
        un "Нет, хорошо, отличное место."
        window hide
        stop music fadeout 3
        play ambience ambience_camp_center_night fadeout 1 fadein 2
        scene bg ext_aidpost_night with dissolve
        $ persistent.sprite_time = "night"
        show pi smile aonl_pioneer with dissolve
        window show
        "Я закрыла дверь на ключ, и Сёма повёл меня к пристани."
        "На лагерь опустилась ночь, он спал."
        un "Пойдём по дорожке?"
        me "Зачем? Через лес же быстрее, там есть хорошая тропинка."
        un "Ну, там может быть…"
        me "Кто? Совёнок?"
        "Он рассмеялся."
        un "Ну, что ты…"
        th "Разве можно так шутить?"
        me "Нет, ничего, прости!"
        me "Если хочешь…"
        un "Ладно…{w} Только можно…"
        window hide
        
        scene cg d3_un_forest with dissolve
        window show
        "Я взяла его под руку."
        un "Можно… Так?"
        "Сёма слегка покраснел."
        me "Конечно!"
        "Мы пошли через лес."
        "Было страшно, но я надеялась, что Сёма защитит."
        play sound sfx_tree_branches
        "Над нами резко колыхнулись ветки, словно по ним кто-то пробежал.{w} Сёма вздрогнул, а я сильнее вцепилась в его руку."
        me "Не бойся, это белка, наверное, пробежала."
        un "Угу…"
        th "Действительно, с ним бояться нечего."
        window hide
        
        play ambience ambience_boat_station_night fadeout 1 fadein 3
        scene bg ext_boathouse_night
        show pi normal aonl_pioneer
        with dissolve
        window show
        "Мы вышли на пристань, освещённую Луной."
        "Сёма подошёл ближе к реке."
        show pi smile aonl_pioneer with dspr
        me "Смотри!"
        th "Какая красота!"
        "В воде отражался мостик, лодочный домик и Луна."
        "Казалось, что это какой-то другой мир, а водная гладь – дверь в Зазеркалье.{w} Прыгни – и окажешься по ту сторону."
        play music music_list["a_promise_from_distant_days"] fadein 3
        me "Позвольте пригласить вас на танец!"
        "Он подал руку и неумело поклонился, как кавалер на балу."
        "На мгновение я опешила."
        me "Не бойся, я тоже не умею."
        th "Видимо, хочет подбодрить."
        un "Хорошо."
        window hide
        
        scene cg aonl_d6_un_dance with dissolve
        window show
        "Я подала руку, и он отвел в сторону, нежно приобняв за талию."
        "Мы просто стояли в таком положении несколько секунд."
        un "А дальше что?"
        me "Ну, я не знаю…{w} Ты же помнишь ту песню?"
        un "С трудом…{w} Но помню."
        me "Вот и отлично! Давай, как в фильмах, вальсировать."
        un "А как это?"
        "Он начал очень медленно и осторожно описывать вместе со мной нечто наподобие круга."
        me "Вот видишь, не так уж и сложно!"
        un "Ага…"
        "Несколько минут мы кружились в танце. Он держал за талию, а я положила руку на его крепкое плечо."
        "Моя ладошка, казалась такой маленькой и хрупкой на фоне его твёрдой руки, а он словно олицетворял собой силу и уверенность."
        "Я поняла, что хочу быть только с ним и если он только скажет, то пойду за ним на край света, не колеблясь ни секунды, лишь бы не отпускать его руку."
        "Моя грудь учащённо вздымалась, лицо всё больше краснело, а в животе словно запорхали бабочки."
        "Я не могла смотреть ему в глаза, отводя взгляд то в одну, то в другую сторону. Мне хотелось чего-то ещё, чего-то большего, я не знала, что именно, хотелось чего-то дерзкого и отчаянного, но чего!?"
        "Вдруг он притянул к себе за талию своей сильной рукой, и тогда я впервые посмотрела ему в глаза, найдя там нежность и заботу."
        "Сначала я удивилась, но потом поняла! Да, вот так, это то что мне нужно! В чём нуждаюсь! Сильный, уверенный мужчина только для меня одной!"
        un "А ты говорил, что не умеешь танцевать."
        me "Так и не умею…"
        "Он немного растерялся."
        me "Ты тоже… Неплохо танцуешь…"
        un "А я знаю!"
        "Я тихонько улыбнулась."
        "Мы продолжали двигаться по пристани боясь разрушить прекрасный миг."
        stop music fadeout 3
        mt "Семён! Семён, ты где?"
        "Голос доносился с опушки леса."
        th "Ну блин!"
        "От обиды хотелось расплакаться. "
        mt "Семён!"
        th "Такой момент испортила."
        window hide
        
        scene bg ext_boathouse_night
        show pi normal aonl_pioneer at center
        with dissolve
        window show
        un "Может, не стоит, чтобы нас видели вместе?"
        me "Почему?{w} Пойдём скажем ей, что всё в порядке."
        un "Нет. Давай спрячемся, а потом вернёмся в лагерь."
        th "Не хочу, чтобы о нас поползли слухи, я ещё не готова."
        me "Ладно, хорошо."
        "Ольга Дмитриевна покричала-покричала и ушла."
        un "Ну, пойдём."
        show pi smile aonl_pioneer with dspr
        me "Да…"
        window hide
        
        play ambience ambience_forest_night fadeout 1 fadein 2
        scene bg ext_path_night
        show pi normal aonl_pioneer
        with dissolve
        window show
        "На обратном пути я молча шла рядом, не решаясь коснуться его. Снова моё стеснение взяло верх."
        th "А ведь нам было так хорошо."
        "Я снова и снова проживала те чувства, которые наполняли меня, когда он рядом."
        th "Как же хочется пережить их вновь, но боюсь сама попросить о танце."
        window hide
        
        play ambience ambience_camp_center_night fadeout 1 fadein 2
        scene bg ext_square_night_party2
        show pi normal aonl_pioneer
        with dissolve
        window show
        "На площади мы остановились."
        un "Ладно, мне пора."
        me "Да…"
        un "Спасибо тебе… За сегодня…"
        show pi smile aonl_pioneer with dspr
        me "Да…"
        hide pi with dissolve
        "Я развернулась и пошла в сторону домика, а Сёма смотрел вслед."
        window hide
        
        scene bg aonl_ext_houses_night with dissolve 
        window show
        "Всю дорогу я только и думала о нём."
        th "Какой же он хороший, сильный."
        window hide
        
        scene bg aonl_ext_house_of_un_night with dissolve
        window show 
        "Очнулась только, когда пришла домой."
        window hide
        play ambience ambience_int_cabin_night fadeout 1 fadein 2
        play sound [sfx_open_dooor_campus_1, sfx_close_door_campus_1]
        pause 1
        scene bg int_house_of_un_night with dissolve
        play music music_list ["a_promise_from_distant_days"] fadein 2
        window show 
        "Мику уже спала."
        "Повесив платье на вешалку, заметила, что другие мои вещи постираны и повешены сушиться."
        "Я тихонько присела возле кровати и погладила девушку по голове."
        "Подруга, да теперь могу действительно назвать её подругой, постирала их во время танцев, дав мне время побыть в эйфории."
        th "Спасибо подруга, ты даже не представляешь, как я тебе благодарна."
        window hide
        
        play sound sfx_blanket_off_stand
        show blink
        pause 1
        window show
        "Раздевшись, завернулась в одеяло, поглаживая свою ладонь, хранящую тепло его руки и заснула лишь с одной мыслью."
        th "ТОЛЬКО НЕ ОТПУСКАТЬ ЕГО РУКУ!"
        stop music fadeout 2
        window hide
        stop ambience fadeout 2
        pause 4
        jump aonl_day4_alt