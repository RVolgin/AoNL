label aonl_day5_alt:

    if aonl_un_story:
        call aonl_new_chapter(5, "пятый", "Я вижу только образ твой\nИ свет, что даришь ты собой.\nЯ им живу, дышу, творю,\nТебе о чувствах говорю.")

    if aonl_sl_story:
        call aonl_new_chapter(5, "пятый", "Возлёг с другой в объятьях нежных\nМы расстаёмся навсегда\nНе мой ты суженый отныне\nНо буду я любить всегда")
 
    if aonl_sl_story:
        #scene Cg Лена и Семён на пляже перед их расставанием он есть в моде «возвращение в совёнок»
        window show
        scene cg aonl_un_farewell
        show prologue_dream
        with dissolve
        play music aonl_i_am_so_sorry fadein 2
        "Легкий бриз шевелит волосы."
        "Вокруг никого, только мы вдвоём." 
        "Нас разделяют несколько сантиметров."
        "Я протягиваю руку к его лицу, чтобы подвинуться ближе и подарить легкий поцелуй."
        "Сёма перехватывает ладонь." 
        "Не грубо, но настойчиво, отводит в сторону."
        th "Неужели так плоха для тебя? {w} Почему?"
        me "Прости."
        show cg aonl_un_farewell1 with dissolve
        "Он уходит в сторону леса, где уже ждёт Славя."
        "Я хотела броситься следом, но на плечо легла рука."
        "Можно не оборачиваться. {w} И так знаю, кто это."
        window hide
        show bg aonl_fog_everywere with dissolve
        window show
        un "Опять сон?"
        aonl_un25 "Да."
        un "Зачем ты всё это показываешь? Зачем мучаешь меня?!"
        aonl_un25 "Чтобы научить."
        un "Чему?!"
        aonl_un25 "Отпускать, когда уже всё потеряно."
        un "Я уже отпустила."
        aonl_un25 "На словах да. {w} Но сердцем ещё нет."
        un "Ты сама-то смогла бы отпустить?"
        aonl_un25 "Сейчас нет, но всё меняется."
        un "Оставь меня в покое, прекрати издеваться!"
        aonl_un25 "Я не издеваюсь. Ты сама себя мучаешь."
        play sound mir_ruhnul
        show treshina:
            zoom 2.0
            xoffset 290
        window hide
        "Мир вдруг поблек и пошёл трещинами."
        aonl_un25 "Пора."
        window hide
        stop music fadeout 2
        play ambience ambience_int_cabin_day fadein 2
        scene bg int_house_of_un_day with flash
        $ day_time()
        $ persistent.sprite_time = "day"
        window show
        "Медленно открыла глаза и уставилась в одну точку."
        th "Это не сны... {w} Уж слишком они реальны."
        th "Хоть раз во сне ощущала ветер? Ни разу!"
        th "И зачем моя версия постарше показывает всё это? Я же обычная пионерка, которая через несколько дней поедет к родителям."
        show mi smile pioneer with dissolve
        play music music_list ["so_good_to_be_careless"] fadein 2
        mi "Охайо годзаимас, Рена!"
        "Пропела подруга."
        th "Рена?! Ах да, в японском же нет звука «л»…"
        un "И тебе доброго утра, Мику-сан!"
        show mi surprise pioneer with dspr
        mi "Ой… Меня никто так никогда не называл. Неожиданно. Особенно от тебя, Леночка! Я так польщена." 
        un "Не за что, Мику-сан! Просто, можно я буду так тебя называть?"
        show mi smile pioneer with dspr
        mi "Конечно-конечно! Я только что от Шурика, сразу как проснулась к нему побежала." 
        mi "Представляешь, он говорит, что толком ничего не помнит, но нашёл ту самую деталь для микрофона и сегодня починит. Обещал лично занести."
        un "А ты что?"
        show mi shy pioneer with dspr
        mi "А я сама не своя от радости за микрофон и спасение, что кинулась на шею и буквально расцеловала в щеки…"
        "Тут Мику осеклась и покраснела."
        mi "Я потом, конечно, сильно смутилась. {w} И он тоже... И я, не зная, что сказать убежала."
        un "А что ты будешь делать, когда он придёт?"
        mi "А что нужно делать?"
        un "Ну… Может чаем угостишь хоть, мальчик же для тебя её искал."
        show mi happy pioneer with dspr
        mi "Ой! Точно Лен, какая я глупая. Конечно, угощу."
        un "И… Вы сможете поговорить."
        show mi shy pioneer with dspr
        "Мику сильнее раскраснелась."
        mi "…Да."
        "Я начала одеваться."
        mi "Ну так, я пойду к умывальникам? Ты со мной? Сегодня на улице такая хорошая погода…"
        un "Я попозже подойду, хорошо?"
        show mi smile pioneer with dspr
        mi "Хай, матанэ!"
        hide mi with dissolve
        stop music fadeout 2
        "Продекламировала она по-японски вновь и скрылась за дверью." 
        "Теперь у меня есть время рассказать всё дневнику." 
        "Быстренько достав его и одевшись, принялась буквально строчить."
        window hide
        scene cg aonl_un_diary with dissolve
        # play sound (Пишет ручкой, можно взять из Четыре Горизонта: Зимней Сказке)
        window show
        $ set_mode_nvl()
        play music aonl_lena_diary_writing fadein 2
        "«Дорогой дневник.{nw}"
        "Сегодня эта взрослая женщина опять приходила во сне. Она словно подвергает меня пыткам, показывая любимого человека с другой. Хотя и ведёт себя так, будто хочет помочь. Научить отпускать.{nw}" 
        "Отпускать...{nw}" 
        "Уже давно отпустила, поняла, что нам ничего не светит, что он с другой, но…{nw}" 
        "Моя мучительница правильно заметила. Я отпустила на словах, а вот сердцем по-прежнему с ним. По-прежнему где-то глубоко-глубоко живёт крохотная надежда. Это так больно любить и не быть любимой.{nw}" 
        "Моя первая любовь. Такая прекрасная и такая жестокая...»"
        window hide
        stop music fadeout 2
        $ set_mode_adv()
        scene bg int_house_of_un_day with dissolve
        window show
        "Я спрятала дневник и смахнула мелкую слезинку."
        th "Так, не раскисать! Впереди ещё три дня, нужно жить дальше."
        window hide
        
    if aonl_un_story:
        scene black
        with fade
        window show
        play music aonl_requiem fadein 2
        "Я бежала, вцепившись в его руку."
        "Из последних сил, сбивая дыхание, он тянул меня за собой."
        "Опасность шла за нами по пятам."
        "Мы оба понимали, что спасения нет, но отчаянно цеплялись за призрачную надежду."
        play sound sfx_metal_door_heavy_close 
        "Сёма из последних сил, закрыл тяжёлую дверь."
        window hide
        scene bg int_catacombs_living:
            linear 0.1 pos (0,20)
            linear 0.1 pos (0,0)
            linear 0.1 pos (20,0)
            linear 0.1 pos (0,0)
            repeat
        show prologue_dream
        with fade
        play sound sfx_nightmare_underground_rumble
        window show
        "Я сидела на кровати и тихонько всхлипывала. {w} Сёма присел рядом и сжал мою руку."
        me "Не бойся…"
        "Стены тряслись, с потолка сыпалась штукатурка." 
        "Ядерный взрыв скоро сметёт наше убежище."
        window hide
        scene bg int_catacombs_living
        show prologue_dream
        with dissolve
        window show
        "Неожиданно наступила тишина, зловещая тишина, несущая ещё больше угрозы."
        un "Пора прощаться, наверное…"
        "Я снова всхлипнула."
        "Сёма обнял меня, пытаясь хоть как-то утешить."
        me "Да…"
        un "Знаешь, я…"
        window hide
        scene bg int_catacombs_living:
            linear 0.05 pos (0,20)
            linear 0.05 pos (0,0)
            linear 0.05 pos (20,0)
            linear 0.05 pos (0,0)
            repeat
        show prologue_dream
        with fade
        play sound sfx_nightmare_explosion
        window show
        "Чудовищный грохот заполнил всё помещение, казалось из ушей брызнет кровь."
        "Нас придавило обрушившимся потолком, но Сёма из последних сил сжимал мою руку."
        window hide
        stop music fadeout 2
        play ambience ambience_int_cabin_day fadeout 1 fadein 2
        $ persistent.sprite_time = "day"
        $ day_time()
        scene bg int_house_of_un_day with flash
        play sound sfx_blanket_off_stand
        window show
        un "Сёма!!!!" with vpunch
        "Я вскочила на кровати, тяжело дыша, оглядываясь затравленным взглядом."
        "Имя сорвалось с губ в страхе, что больше его не увижу. На глаза навернулись слёзы."
        show mi scared pioneer close with dissolve
        play music aonl_calm fadein 3
        mi "Лена, Леночка, что с тобой, почему ты плачешь?!"
        "Девушка обняла меня, прижав голову к своей груди."
        th "Мику? Ах да, я же в домике."
        un "Я, я, я… Всё хорошо Мику, просто плохой сон."
        mi "Бедненькая моя, натерпелась милая."
        "Подруга нежно погладила мои волосы."
        show mi sad pioneer close with dspr
        mi "Не плачь родная, с ним всё в порядке."
        un "С кем?"
        show mi grin pioneer close with dspr
        mi "Ну как с кем?"
        "Мику отстранилась и лукаво посмотрела в глаза."
        show mi grin pioneer with dissolve
        mi "С Семёном конечно, ведь он приснился?"
        "Я покраснела и опустила взгляд."
        "Отвечать бессмысленно, глаза сказали сами за себя."
        show mi smile pioneer with dspr
        mi "Лен, расскажи, что тебе снилось?"
        un "Мы с Сёмой… Бежали, точнее прятались и… {w} И он погиб!"
        show mi cry_smile pioneer with dspr
        mi "Леночка, как же тебе повезло, прям завидую."
        un "Повезло?!"
        "Я удивлённо уставилась на Мику."
        mi "Конечно повезло! В моей стране считается, что если во сне ты теряешь любимого человека, то значит, вы думаете друг о друге."
        un "Ты хочешь сказать, Сёма обо мне думает?"
        show mi happy pioneer with dspr
        mi "Разумеется, иначе и быть не может!"
        "Я опустила взгляд, прижав руки груди."
        th "Сёма обо мне думает? Неужели, я тоже ему нравлюсь?!"
        show mi happy pioneer close with dissolve
        mi "Лен."
        "Мику приобняла меня за плечо."
        mi "Ты любишь Сёму?"
        un "Д-да…"
        show mi smile pioneer close with dspr
        mi "Счастливая. Он знает?"
        un "Ещё нет, я и сама не была до конца уверена, но вчера уговорила Сёму пойти искать Шурика и… {w} И там он меня спас, после этого я всё поняла…"
        mi "Ты такая смелая."
        un "Смелая?"
        show mi cry_smile pioneer close with dspr
        mi "Ну да! Не побоялась пойти ночью искать товарища, ещё и Сёму смогла уговорить." 
        mi "Ты умница, я очень благодарна за спасение Саши. Спасибо тебе!"
        un "На здоровье."
        show mi shy pioneer close with dspr 
        "От похвалы Мику я совсем раскраснелась и смущённо опустила взгляд. Не каждый день меня хвалят, да ещё и смелой называют."
        mi "Ты ему скажешь?"
        show mi grin pioneer close with dspr 
        un "А, что?"
        "Вопрос Мику вернул к реальности."
        mi "Сёме признаешься в любви?"
        un "Я… Я не знаю. Вообще, мальчик должен признаваться."
        show mi normal pioneer close with dspr
        mi "Верно Лен, вот только у нас осталось всего 3 дня, а потом мы разъедемся по домам." 
        show mi upset pioneer close with dspr
        extend " Если он сам не решится, потом будет поздно."
        th "Слова Мику заставили серьёзно призадуматься. Семён мальчик хороший, и всё же его порой нужно подталкивать к действиям." 
        th "Если буду ждать с моря погоды, то могу упустить своё счастье."
        un "Ты права, я… Попробую. В течение пары дней точно скажу."
        show mi smile pioneer close with dspr
        mi "Молодец!"
        "Мику крепко обняла меня."
        mi "И я попробую сказать Саше! {w} Сначала сомневалась, но видя твою решимость, тоже набралась уверенности!"
        mi "Я только что от него, сразу как проснулась к нему побежала." 
        mi "Представляешь, он говорит, что толком ничего не помнит, но нашёл ту самую деталь для микрофона и сегодня починит. Обещал лично занести. Так что побегу готовиться к встрече."
        un "Удачи."
        stop music fadeout 2
        play sound [sfx_open_dooor_campus_1, sfx_close_door_campus_1]
        hide mi with dissolve 
        "Мику скрылась за дверью подобно порыву ветра."
        "Я достала дневник и приступила к записи."
        window hide
        scene cg aonl_un_diary with dissolve
        $ set_mode_nvl()
        window show
        play music aonl_lena_diary_writing fadein 2
        "«Дорогой дневник.{nw}"
        "После вчерашней ночи, я наконец-то чётко поняла и утвердилась в своих чувствах к Семёну. Он мой суженый, я хочу быть только с ним и ни с кем больше. Но у нас осталось мало времени, а потому я признаюсь ему сегодня. Да, именно сегодня, я всё скажу и будь, что будет. Почему-то возникло чувство, будто сегодня могу упустить момент и всё рухнет, а значит медлить нельзя.»"
        window hide
        stop music fadeout 2
        $ set_mode_adv()
        scene bg int_house_of_un_day with dissolve
        window show
        "Я спрятала дневник и отправилась умываться."
        window hide

    play ambience ambience_camp_center_day fadeout 1 fadein 2
    scene bg ext_washstand_day with dissolve
    play sound sfx_open_water_sink
    play music music_list ["two_glasses_of_melancholy"] fadein 3
    pause 1
    play sound_loop sfx_water_sink_stream
    window show
    "Открыв кран, подставила ладони под струю прохладной воды."
    show el normal pioneer at right with easeinright
    "Принять утренние процедуры вместе со мной пришёл и Электроник."
    th "Может, именно он когда-нибудь сделает так, чтобы вода в лагере была всегда тёплой?"
    un "Доброе утро. Шурик в порядке?"
    show el smile pioneer with dspr
    el "И тебе доброго, Лен. Да, в порядке. Он в клубе сейчас, как ни в чем, ни бывало."
    un "А что насчёт прошлой ночи он говорит?"
    show el normal pioneer with dspr
    el "Только то, что это невозможно и антинаучно." 
    el "Забудь лучше, всё хорошо."
    play sound sfx_close_water_sink
    stop sound_loop
    un "Ладно."
    "Закончив приводить себя в тонус, я сделала несколько упражнений из комплекса утренней гимнастики и направилась к площади."
    window hide
    scene cg aonl_lineup_with_mi with dissolve
    window show
    "Пусть и не подавала виду, но слова вожатой нагоняли тоску гораздо быстрее, чем обычно."
    " То, насколько это скучно, видимо, не заботило Ольгу Дмитриевну. Но сказать ей что-то нужно." 
    "Запомнить удалось лишь то, что после завтрака мне снова поручено помогать Жене в библиотеке. Такие дела…"
    window hide
    play ambience ambience_dining_hall_full fadeout 1 fadein 2
    scene bg int_dining_hall_people_day with fade2
    show mi smile pioneer at left with dissolve
    window show
    "На завтрак рисовая каша, к ней прилагался чай и бутерброд с красной рыбой."
    th "Не сильно нравится рыба, да и рис не моя пища, но придется, есть что дают."
    show mi cry_smile pioneer with dissolve
    mi "Ого! Сколько всего вкусного сегодня! Ещё и лосось, м-м-м… В Японии такого не повстречаешь!"
    "А вот дитя востока имело свой взгляд на актуальное меню. Пока она с энтузиазмом употребляла священную крупу, я неторопливо ворочала ложкой."
    th "Зато она сладкая. Сахар придаёт энергии. А ещё увеличивает риск диабета…"
    show sl normal pioneer at center behind mi
    show mz normal pioneer glasses at right
    with dissolve
    sl "Доброе утро, девочки! Мы к вам присоединимся, если вы не против?"
    "Стулья отодвинулись, и рядом присели, Славя и зевавшая Женя, которой всё время будто не хватало лишнего дня на сон."
    show mi smile pioneer with dspr
    mi "И вам привет! Сегодня прекрасный день, не так ли?"
    show sl smile pioneer with dspr
    sl "Да, не поспоришь. И погода хорошая… Так не хочется сидеть в затхлом помещении в такую пору!"
    "На возглас первой красавицы «Совёнка» любительница книжной пыли только закатила глаза."
    "Некоторое время мы ели молча и не спеша, как вдруг Славя собралась с силами и выдала."
    show sl smile2 pioneer with dspr
    sl "Может, в баньку всем вместе сходить, пока делать нечего?"
    "Я в очередной раз удивлялась предложениям, исходившим от Слави. Как правая рука пионервожатой, она могла бы и собственный распорядок дня выстроить."
    un "Ну хорошо, подумаю… Днём или вечером?"
    show sl happy pioneer with dspr 
    sl "Ну зачем же тянуть? Сейчас давайте! Вчера её затопили так-то на ночь, но принять не успела."
    sl "Поэтому, пока она не остыла, можно сходить. Веники от себя лично гарантирую."
    show mi grin pioneer with dspr
    mi "Я согласна! В детстве меня возили на горячие источники, и там очень здорово, но в вашей бане наверняка интереснее."
    sl "Отлично! Лена?"
    un "Я даже не знаю… Меня Ольга Дмитриевна отправила в библиотеку с утра наводить порядок."
    sl "Ну внутрь ты точно не попадёшь без Жени! Ты сама-то как, с нами?"
    "Светловолосая обратилась к своей соседке. Женя подняла глаза, скрытые под линзами очков, и сонно пробубнила."
    show mz smile pioneer glasses with dspr
    mz "Не вижу смысла заниматься всем сразу. А так хоть помыться можно…"
    mi "Вот видишь, Лена? Нас уже как минимум трое. Присоединяйся!"
    "Я оглядела девушек и задумалась."
    th "Причин не ходить у меня нет, можно и попариться."
    un "Ну будь, по-вашему…"
    show mi cry_smile pioneer with dspr
    mi "Ура! Ты не пожалеешь, Ленка!"
    "Мику заблистала от восторга. Очевидно, когда Шурик нашёлся, к ней вернулся упущенный в прошедший день оптимизм."
    stop music fadeout 3
    th "Уж надеюсь на это…"
    window hide
    play ambience ambience_int_cabin_day fadeout 1 fadein 2
    scene bg int_house_of_un_day with fade
    show mi normal pioneer with dissolve
    window show
    "После успешно съеденного завтрака, мы с моей бирюзовой подружкой забежали домой за полотенцами и всем, что ещё может понадобиться для мытья."
    "Поскольку наш домик находился с самого северного краю, путь предстоял сперва до места, где жили Славя и Женя, а затем они сориентировали бы нас, где находится баня." 
    "Некогда для пионеров существовал ещё один способ поддержать личную гигиену — помыться в душевых кабинках неподалёку от площади." 
    "В мою смену они все почему-то были на ремонте — по заверениям вожатой, как минимум до предпоследнего дня нашего пребывания здесь включительно."
    th "Интересно, будет ли к этому моменту в душе ещё кто-то нуждаться?"
    un "А в Японии разве нет своей бани?"
    mi "Есть, но она, мне кажется, сильно отличается от русской."
    un "И для тебя этот раз будет первым?"
    show mi surprise pioneer with dspr
    mi "Да. Но я много слышала о подобном. Думаю, что это очень приятно."
    un "Ну тут как посмотреть…"
    window hide
    play ambience ambience_camp_center_day fadeout 1 fadein 2
    scene bg ext_houses_day with dissolve
    show cs normal at right
    show mi normal pioneer at left
    with dissolve
    window show
    "Навстречу шла медсестра, которая остановилась и с удивлением осмотрела нас."
    show mi smile pioneer with dspr
    mi "Здравствуйте, Виолетта Церновна! А мы в баню идём!"
    show cs smile with dspr
    cs "Ох, удачи вам, красавицы! Чистота — залог здоровья. В бане только ничего экзотического не подцепите!"
    hide cs with dissolve
    "Напутствовала Виола, одарив на прощание двусмысленной ухмылкой."
    window hide
    #with fade
    show sl smile pioneer at center behind mi
    show mz normal pioneer glasses at right
    with dissolve
    window show
    sl "Вот и вы! А мы к вам навстречу пройтись решили!"
    "Знакомые лица показались за поворотом. Уверена, что встретить нас — не столько совместная инициатива, сколько исходившая от самой активной пионерки из тех двоих, но промолчала."
    window hide
    scene bg aonl_ext_bathhouse_day with dissolve
    window show
    "Пунктом назначения оказалась небольшая бревенчатая изба, находившаяся на живописной опушке близ соснового бора. Я сперва напряглась, издали, не разглядев на её крыше трубу."
    "Однако, уже вблизи смогла убедиться, что банька нам предстоит «по-белому»."
    # Скипаю сцену. Извиняюсь, но эту сцену не могу кодить.
    window hide
    scene bg aonl_int_bathhouse with dissolve
    window show
    stop ambience
    play music aonl_honey_bath fadein 3
    show mi aonl_body shy at left with dissolve
    show sl aonl_body smile at center with dissolve
    show mz aonl_body normal at right with dissolve
    "В предбаннике мы полностью разделись и распустили волосы."
    "Я не могла похвастаться длинной волос, в отличие Слави или Мику, но каре всегда носила объёмное, из-за чего заплетала некоторые пряди в хвостики. А за такими нужен особый уход."
    "В зале, совмещавшем моечную и парилку, было ожидаемо много пара."
    
    play sound Par
    "Славя, взяла ковшик и плеснула на камни немного воды из бочки, они зашипели и только добавили воздуху влажности."
    mi "Ух, жа-а-арко!.."
    "Съёжилась аквамариновая певица так, словно ей наоборот стало очень холодно."
    sl "Ничего! Зато кожа распарится, очистится и будет нежной как лепесток розы, вот поверь мне!"
    mi "Нисколько в твоих словах не сомневаюсь, Славя-сэмпай! А можешь мне подать вот тот веник?"
    "Пока Мику со Славей разговаривали между собой, стегая, друг дружку связкой берёзовых ветвей, я сидела на верхней полке и размышляла о прошлом и будущем."
    th "Сегодня всё так хорошо начинается. Но ведь я даже не знаю, долго ли получится скрывать, насколько Сёма мне важен?"
    th "И почему же у меня с утра такое непонятное ощущение, как от предчувствия шторма при полном штиле?"
    show mi aonl_body normal at left with dissolve
    show sl aonl_body shy at center with dissolve
    mi "Да у тебя-то они побольше, чем у меня!"
    "Моё внимание вновь привлекла Мику, тыкавшая пальчиком прямо… В грудь Слави. Та краснела от столь витиеватых комплиментов."
    sl "Ну даже если и так, то, что с того?"
    mi "Ничего. Просто красиво, обычно многие мужчины без ума от больших…"
    sl "Меня не интересуют многие. Я бы вышла замуж за человека, который ценил меня как женщину, а не только как домохозяйку и утешительницу. За порядочного и собранного, в общем."
    mi "Тоже верно. Женя, а ты бы за кого вышла?"
    "Они обратились к девочке, до поры до времени сидевшей тише воды и ниже травы." 
    "В плане форм миниатюрной Жене, к слову, тоже было в чём позавидовать, но она как нарочно сложила локти у себя на груди и сердито ответила звонким голоском."
    show mz aonl_body rage at right with dissolve
    mz "Нашли, о чём спрашивать! За кого сама захочу!"
    sl "Ну не кипятись так! Мы просто говорим о предпочтениях. Это останется между нами."
    "Славя, присела рядом с ней и обняла рукой за плечо." 
    "Библиотекарь задумалась и через полминуты мечтательно изрекла."
    show mz aonl_body shy at right with dissolve
    mz "Я бы вышла за умного человека. И не занудного, в то же время." 
    mz "За того, с кем можно поговорить и всегда получить ответ на все вопросы."
    mi "Ой, мы с тобой одинаковы! Я бы тоже выбрала бы себе такого — умного и общительного…"
    show mz aonl_body normal at right with dissolve
    mz "Но все мальчишки — дураки! Пока не перебесятся и не станут людьми, с ними и дом не построить!"
    "Угрюмо проворчала Женя. Но через секунду она хитро улыбнулась мне."
    show mz aonl_body smile at right with dissolve
    mz "И вообще… У Тихоновой грудь больше, чем у Ясеневой."
    mi "Ого, серьёзно?!"
    "Японка встала со своего места и подсела ко мне." 
    "Её рука легла на одну из моих грудей и стала потискивать, что меня взбудоражило." 
    "А Женя и рада — её больше не беспокоили."
    mi "Слушай, и, правда. Смотри, какие большие! У меня одна-то в ладошке еле помещается!"
    "Блондинка приблизились ко мне." 
    "Славя схватила рукой мою вторую грудь, свободной коснувшись своей, и тоже пожала её." 
    "Пот с меня полился обильнее."
    sl "Да, верно. Поздравляю тебя, Лена, ты выиграла!"
    
    show red_flash  # Показываем красную вспышку
    with Dissolve(0.5)  # Быстрое появление
    pause 0.5  # Задержка для эффекта
    hide red_flash  # Скрываем вспышку
    with Dissolve(0.5)  # Медленное исчезновение
        
    "Пионерка засмеялась, а я впала в краску — неловко, но приятно осознавать, что есть при тебе эдакого."
    un "Да не за что… Какие глупости…"
    sl "Ну как это глупости? Ты ими детей своих кормить будешь! Ну, и мужа покорять…"
    mi "Точно-точно! Наверняка Лена будет из нас самая счастливая в браке!" 
    mi "Кстати, а за какого человека хочешь выйти замуж ты?"

    if aonl_sl_story:
        "Два испытующих взгляда уставились прямо на меня. Я уже знала, кого мне бы хотелось заполучить в мужья, но открывать девочкам тайну не хотелось."
    
    if aonl_un_story:
        "Два испытующих взгляда уставились прямо на меня. Мику знала о моём выборе, но виду не подавала."
    
    un "За честного и доброго…"
    "Заявила я вполголоса. Девочки улыбнулись и выпустили обе мои груди."
    
    play sound sfx_close_door_1
    
    dv "С лёгким паром, дорогуши! А чего меня не позвали?"
    hide mz with dissolve
    show dv body normal at right with dissolve    
    "Мы обернулись. Посреди зала стояла Алиса, стройная и подтянутая, с бронзовой от загара кожей." 
    "Я невольно приподнялась со скамьи."
    mi "Ой, Алиска! А у тебя как с грудью дела?"
    sl "Вижу, она тоже непромах!"
    show dv body shocked at right with dissolve
    dv "Так, что здесь происходит, чёрт побери?!"
    "Сердито недоумевала Алиса, пока девочки изучали её на редкость привлекательные формы."
    th "Ух… Хотя бы отстали. Для полного счастья ещё Ульянки не хватало!"
    hide mi with dissolve
    #С Ульяной проблемы, не получается ничего
    us "Трям-здрасьте!"
    th "Ого! Вспомнишь лучик — вот и солнышко!"
    "Однако на юную налётчицу девушки даже ухом не повели." 
    "Ульяна только развела руками и, подбежав к полкам, присела на нижний ярус."
    "Я на всякий случай отсела подальше. Хотя, при посторонних, она вряд ли что учудит."
    "Вдоволь напарившись, мы наполнили по тазику тёплой воды, вооружились мочалками и стали мыться." 
    "Мыло пару раз попадало мне в глаза и неприятно щипало, но впечатлений от принятия ванны это не испортило. Всё ж лучше, чем пытаться вымыть лицо под ледяной струёй из-под крана!"
    sl "Лена, тебе спинку не потереть?"
    un "Хм… Буду благодарна."
    window hide
    show cg aonl_bath_2 with dissolve
    window show
    "Я присела на скамейку и подставила свою спину." 
    "Руки Славяны с заботой и вниманием направляли мочалку по всей её площади, сперва нанося мыло на кожу, а затем смывая его." 
    "Чувствовался недюжинный опыт пионерки в обслуживании не только себя, но и других."
    show cg aonl_bath_1 with dissolve
    "Поблизости от меня библиотекарь, щурясь, водила ладонями по скамье и полочкам со средствами гигиены."
    th "Без очков ей нелегко. Подсобить надо…"
    un "Ты что-то ищешь, Жень?"
    mz "Ножницы маникюрные и пемзу."
    "Пробормотала она взволнованно." 
    "Помочь ей из остальных было некому: Улька намыливала голову распластавшейся в наполненной ванне Алисе, а Мику, как могла, боролась уже с собственными волосами."
    "Благо, я сразу увидела обе названные вещи, встала со скамейки и подала их близорукой девочке."
    mz "Спасибо…"
    "Кратко и тихо сказала она, после чего продолжила умывание." 
    "С чувством исполненного долга вернулась к Славяне."
    show cg aonl_bath_2 with dissolve
    sl "Может, мне тебя тоже потереть, Женёк?"
    mz "Не-не, я сама…" 
    "Отстранённо ответила Женя." 
    "Возможно, такие ситуации ей были не впервой, и она привыкла сама себя выручать. Тоже важное умение, кстати."
    th "Интересно, что сделало Женю такой нелюдимой?" 
    th "А может, она всегда была такой…"
    stop music fadeout 3
    scene bg aonl_int_bathhouse with dissolve
    "Я боялась, что в предбаннике мы вшестером не поместимся." 
    "Однако все девочки дружно покинули моечную, вытерлись, оделись и наладили причёски, лишь немного потолкавшись перед зеркалом." 
    "Выходя на улицу самой последней, Мику блаженно потянулась."
    window hide
    play ambience ambience_camp_center_day fadeout 1 fadein 2
    scene bg aonl_ext_bathhouse_day with dissolve
    show sl happy pioneer at right
    show mi happy pioneer at center
    show mz shy pioneer glasses at cright
    show dv grin pioneer at left
    show us grin pioneer at fleft
    with dissolve
    window show
    mi "А-а-ах, как же хорошо в баньке с утреца!"
    sl "А я что говорила! Смоешь с себя всё ненужное — и день проходит легче!"
    show mz bukal pioneer glasses with dspr
    mz "Мне это не светит."
    "Твёрдо ответила Евгения, несмотря на то, что заметно повеселела после банных процедур." 
    "Да и мою сонливость как рукой сняло."
    show mi smile pioneer with dspr
    mi "О, это Вы, Ольга Дмитриевна! Здравствуйте!"
    show mt smile pioneer panama at fright with dissolve
    mt "Здравствуйте-здравствуйте, уже виделись утром!"
    "Неожиданно возле крыльца возникла фигура вожатой."
    
    show dv angry pioneer at left
    show us angry pioneer at fleft
    with dissolve
    
    "Не все этому оказались рады так, как Мику, — Алиса с Ульяной от неожиданности вздрогнули."
    mt "Мне медсестра передала, что вы вместе решили банные процедуры принять?"
    show sl smile2 pioneer with dspr
    sl "Да, это я предложила. Перед началом рабочего дня сущее удовольствие помыться — девчонки не дадут соврать!"
    
    show dv grin pioneer at left
    show us grin pioneer at fleft
    with dissolve
    
    "Приветливые голубые глаза окинули взором всю оставшуюся ораву. Мы в ответ только натянули на лица улыбки."
    show mi cry_smile pioneer with dspr
    mi "Я вообще впервые в таком участвую… Мне понравилось, Вы тоже как-нибудь побывайте!"
    mt "Неплохая идея, учту. Кстати, Женя…"
    show mz normal pioneer glasses with dspr
    mz "Да, поняла, сейчас захвачу с собой Тихонову — и вперёд."
    show mt grin pioneer panama with dspr
    mt "О нет-нет, планы поменялись! Теперь вас со Славей ждут возле ворот на стоянке. Опять приехала твоя семья, видишь ли…"
    show mz angry pioneer glasses at cright
    mz "…"
    "Девушку сдавило новостью как гидравлическим прессом. То ли это было приятное потрясение, то ли наоборот — она бы всё равно не сказала."
    show sl happy pioneer with dspr
    sl "О, какая неожиданность!"
    show mt smile pioneer panama with dspr
    mt "Да, для меня самой это форс-мажор, а ещё с младшим отрядом возиться с утра — коллега приболела. Поэтому вся надежда на тебя, Славяна." 
    mt "Пусть наша Евгения встретит родных, а тебе надо будет сопроводить приезжих к гостевому домику и помочь расположиться."
    un "Стойте! Но у нас, же нет свободных…"
    "Моё изумление дало волю языку, но вожатая быстро укротила мой пыл."
    mt "Откуда это ты взяла, Лена? Для гостей всегда есть одно или пара свободных мест! Мало ли, что случится."
    th "Странно тогда, что Семён этой привилегией не воспользовался." 
    th "Ну да кто я такая, чтобы спорить с теми, кто лучше знает, ага…"
    show sl smile pioneer with dspr
    sl "Ой, тогда нужно спешить! Спасибо, Ольга Дмитриевна!"
    hide sl
    hide mz
    with dissolve
    "Славя схватила библиотекаря за руку, даже не спросив её на то разрешения, и побежала так быстро, что вожатая успела лишь крикнуть вдогонку."
    mt "Ключи где — помнишь, надеюсь?"
    sl "Ага!"
    mt "И потом возвращайся! Ты помнишь, что сегодня?"
    "Но эти слова наша быстрая командирша отряда уже не услышала."
    th "Вот это прыть! Даже она рада приезду чьей-то семьи больше, чем непосредственный родственник."
    show dv guilty pioneer with dspr
    dv "Ну ладно, всем хорошего денёчка!"
    stop music fadeout 3
    "Помахала ладонью Алиса и вместе с Ульяной уже собралась покинуть нашу компанию."
    show mt angry pioneer panama
    show mi shocked pioneer
    show dv surprise pioneer
    show us dontlike pioneer
    with dspr
    play music music_list ["into_the_unknown"] fadein 3
    mt "Так, Двачевская! А вас с Советовой я попрошу остаться!"
    "Мы с Мику обменялись опасливыми взглядами."
    th "Похоже, их вчерашние проказы так просто не забудутся. Бедняги!"
    mt "Ну что ж, мастерицы… Вчера весь отряд лицезрел ваше умение организации фейерверков." 
    mt "Полагаю, с украшением ещё одного объекта нашего лагеря вы тоже справитесь на раз-два!"
    show dv guilty pioneer with dspr
    dv "Ой, Вы нам льстите, Ольга Дмитриевна, право…"
    mt "Цыц! Вы обе недооцениваете себя! Не позволю зарывать талант в землю таким, как вы!"
    th "Так осыпала ли руководительница провинившихся сарказмами?" 
    th "Или, в самом деле, благодарила их?"
    mt "Значит так — идёте к медсестре и берёте у неё банку краски…"
    show dv shocked pioneer with dissolve
    dv "Что-что брать?!"
    show mt rage pioneer panama with dspr
    mt "Краску, я сказала! Красного цвета! И ей вы будете красить короб для песка."
    show us calml pioneer with dspr
    us "Какого песка? Сахарного?"
    show mt angry pioneer panama with dspr
    mt "Даже не надейся, Советова! Песок тот для тушения пожаров. Стану я доверять сахар, коли ты и так повадилась в последнее время у поварих {i}свистывать{/i} по сахарнице! Вот как вчера, например…"
    show us dontlike pioneer with dspr
    us "Что? Клевета! Я не делала такого!"
    show mt grin pioneer panama with dspr
    mt "А кому же ещё в этом лагере?.."
    "Ехидно усмехнулась вожатая и обратилась к нам с Мику."
    mt "Вы тоже в стороне не оставайтесь. Потом мне отчитаетесь, что всё прошло без эксцессов и ящик выкрашен целиком."
    show dv normal pioneer
    show us normal pioneer
    with dspr
    dv "А где этот короб-то?"
    mt "Возле столовой, по левую сторону от входа."
    show dv guilty pioneer with dspr
    dv "Но там, же людей много ходит! Нас сразу заметят!"
    mt "Конечно, заметят! И я замечу, если что не так! На то и расчёт. Удачи вам поработать!"
    hide mt with dissolve
    "Ольга Дмитриевна улыбнулась напоследок и ушла. Наказанные девочки злобно глядели ей в спину."
    show dv angry pioneer with dspr
    dv "Совсем Ода разбушевалась! Дышать этой химией и пачкаться даже не подумаю!"
    show us dontlike pioneer with dspr
    us "Ещё и про сахар на меня наговаривает! Кому же ещё, ну-ну!.. Сама небось в чай по пять ложек бросает…"
    show mi upset pioneer with dspr
    mi "Вы хотите сбежать, девочки?"
    "Алиса на минуту задумалась, прикидывая шансы в уме."
    dv "Нет, в этот раз Панамку лучше не бесить. Сделаем всё быстро и свалим от греха подальше."
    us "Да ну…"
    show dv normal pioneer with dspr
    dv "Приказ не обсуждается. В медпункт ШАГОМ МАРШ!"
    show us laugh pioneer with dspr
    us "Есть командир!"
    hide dv
    hide us
    with moveoutright
    "Мелкая вытянулась по стойке смирно и отдала воинское приветствие, но долго так не продержалась и, прыснув от смеха, побежала в медпункт. Алиса двинулась следом."
    show mi normal pioneer with dspr
    stop music fadeout 3
    mi "И нам пора, Лена."
    un "Ага."
    window hide
    scene bg aonl_ext_dining_hall_behind with dissolve
    show mi normal pioneer at left
    with dissolve
    window show
    play music music_list ["i_want_to_play"] fadein 3
    th "Здесь я уже была, вон и тележка стоит на месте." #возможно стоит убрать
    show us normal pioneer at fright
    show dv normal pioneer at cright
    with dissolve
    "Вскоре явились и две наших террористки с банкой краски, двумя стаканчиками и кисточками."
    dv "Ну и где ящик?"
    show mi serious pioneer with dspr
    mi "Видимо, этот."
    "Девушка показала взглядом."
    show dv guilty pioneer with dspr
    dv "А помочь нам никто не собирается?"
    "Алиса смерила Мику напряжённым взглядом."
    show mi upset pioneer with dspr
    mi "Я бы рада Алиса, но мне ещё гитары настраивать, если испачкаю руки краской в этом возникнет проблема."
    show dv angry pioneer with dspr
    dv "Ну ладно, а наша мышь?"

    if aonl_sl_story:
    
        "Рыжая указала на меня взглядом, и я нехотя поплелась к ящику."
        "Ладонь Мику легла на плечо."
        show mi dontlike pioneer close with dspr
        mi "Леночка, конечно, желает тебе помочь." 
        mi "Но ты ведь не хочешь, чтобы вас увидела вожатая и подумала, что ты её заставила?"
        show dv normal pioneer with dspr
        "Алиса не нашлась что ответить, лишь злобно сверкнула глазами и отвернулась."

    if aonl_un_story:
    
        "Мелкая вытянулась по стойке смирно и отдала воинское приветствие, но долго так не продержалась и, прыснув от смеха, побежала в медпункт. Алиса двинулась следом."
        show dv shocked pioneer
        show mi surprise pioneer
        with dspr
        un "Извини Алиса, вожатая дала чёткие распоряжения только наблюдать." 
        un "Я бы рада помочь, но ты, же не хочешь, чтобы она увидела меня за работой и подумала, что ты заставила?"
        "Рыжая буквально челюсть уронила."
        show dv normal pioneer with dspr
        "Впрочем, она быстро взяла себя в руки и отвернулась, недовольно фыркнув."
        "Мику смотрела на меня не менее удивлённым взглядом."
    
    show mi normal pioneer with dspr
    "Две рыжих бестии приступили к работе, стараясь как можно быстрее закончить неинтересное занятие."
    "Надо признать работали они на совесть." 
    "Видимо, когда распоряжение исходит от вожатой, два главных стихийных бедствия относятся к поставленной задаче серьёзно."
    show dv angry pioneer with dspr 
    dv "Вот жешь!"
    "Алиса выронила кисточку, и та завалилась между стеной и ящиком."
    dv "Улька, погоди, надо ящик подвинуть."
    us "Давай."
    "Мелкая отошла в сторону."
    "Алиса поднатужилась, но ящик не сдвинулся."
    dv "Вам особое приглашение нужно?"
    "Переглянувшись, вместе с Мику подошли помочь."
    show dv normal pioneer with dspr
    "Втроём дело пошло веселее, и ящик чуть отодвинулся от стены."
    dv "Сейчас."
    "Рыжая зашарила рукой за стеной."
    show dv surprise pioneer with dspr
    dv "Оу!"
    mi "В чём дело, Алис?"
    "Девушка извлекла кисточку и с ней маленького пластмассового утёнка."
    dv "Ого, смотри, Лена! Нашла чего-то."
    un "Игрушка?"
    show dv smile pioneer with dspr 
    dv "Ну да, а что? У нас с тобой такая же уточка была, помнишь?"
    un "Такая же? И где?"
    show dv grin pioneer with dspr
    dv "Ну не чуди, это же уточка для ванной! Мы ж маленькими в одной ванне купались! Ну?"
    un "А-а-а, ну… Да вспомнила. Когда мы дружили."
    show dv sad pioneer with dspr
    dv "Да."
    mi "А вы дружили?"
    "Алиса, проигнорировала вопрос, опустилась на корточки, повертела утку в руках и печально вздохнула."
    show us dontlike pioneer with dspr
    us "Эй, почему лодырничаем? Чой тут у вас?"
    show dv normal pioneer with dspr
    dv "Не чой, а что! Уль, я тут детство вспоминаю!"
    show us normal pioneer with dspr
    us "Понятно. Но это же просто утка какая-то."
    dv "Не какая-то, а для ванны! Тебе тоже надо объяснять сто раз, для чего она?"
    show us calml pioneer with dspr 
    us "Ну я ж не знаю. Я в ванну без всяких игрушек хожу."
    show dv surprise pioneer with dspr
    dv "Ну а подруга у тебя есть среди ровесниц? Ванну с ней не принимала?"
    us "Спрашиваешь! Есть, но с ней не попринимаешь — она хмурая слишком…"
    show dv smile pioneer with dspr
    dv "Нам это не мешало. Эх, не было у тебя детства совсем!"
    "Махнула рукой Алиса, разочарованная до глубины души. Лицо Ульяны сперва вытянулось от шока…"
    show us dontlike pioneer with dspr
    stop music fadeout 3
    us "Это у меня-то не было детства?"
    show us angry pioneer with dspr
    "…затем скукожилось от злобы. А мы тем временем задвинули ящик на место. Алиса стала молча макала кисть в краску."
    us "Ну знаешь ли…"
    "Мы с Мику не успели, и пикнуть прежде, чем ярко-багровая Уля взмахнула в воздухе кисточкой будто хлыстом."
    show dv shocked pioneer with dspr
    play music music_list ["revenga"] fadein 2
    show mi dontlike pioneer with dissolve
    "Алису всю обрызгало красными каплями. Она в голос ахнула и бросила взор на обидчицу."
    show dv angry pioneer with dspr
    dv "Ах, ты что творишь?!"
    us "За базаром следи, а потом тут гуди!"
    "Мелкая и не думала раскаиваться. Но у Алисы на такой случай был припасён свой туз в рукаве."
    show dv rage pioneer with dspr
    dv "Тебе того же!"
    us "А-А-А-А-А!"
    "Завизжала Ульяна, едва её по щеке полоснула кисточкой Алиса."
    "С рук старшей такое не сошло — младшая выплеснула на неё остатки из стаканчика. И сама еле увернулась от вылившейся краски."
    dv "Ах та-а-ак!"
    "И началась битва, который не знали небеса. Два исполина, держащие целый лагерь в страхе, сошлись не на жизнь, а на смерть. Более страшного и в то же время комичного зрелища сложно себе представить."
    "Сначала они замолотили ладонями перед собой попадая сопернице по рукам."
    show us cry pioneer with dspr
    "Затем Алиса умудрилась вцепиться Ульяне за волосы и сильно дёрнуть, отчего та страшно заревела, но не осталась в долгу, а прыгнула на обидчицу, повалив на землю, и вцепилась в чёлку." 
    "Обе принялись таскать друг друга за причёски и валяться по траве."
    un "Девочки, не надо! Прошу, прекратите!"
    hide mi
    hide us
    hide dv
    # play sound
    with dissolve
    "После недолгого раздумья, зачем-то бросилась разнимать дерущихся, за что меня поблагодарили подножкой. Да так, что я больно рухнула на землю." with vpunch
    mi "Ну-ка живо все успокоились!"
    show mi rage pioneer at center
    show dv scared pioneer at cleft
    show us fear pioneer at cright
    with dissolve
    "Закричала Мику и схватила потасовщиц за уши."
    show us cry pioneer with dspr
    us "Ай-ай, пельмешечка моя!"
    show dv cry pioneer with dspr
    dv "Ой-ой-ой, больно! Отпусти, прошу!"
    mi "Как только обе утихомиритесь!"
    show dv angry pioneer with dspr 
    dv "Да это она во всём виновата!"
    show us dontlike pioneer with dspr 
    us "Чего? Это ты всё первая начала! Детства у меня не было… Тьфу!"
    dv "Да у тебя оно до сих пор в одном месте! Уля-грязнуля…"
    show us angry pioneer with dspr
    us "А ты самая взрослая, поди, Алиса-крыса!"
    mi "Разбираться не буду, кто кого! Ну-у?!"
    show mi angry pioneer
    show dv cry pioneer
    show us cry pioneer
    with dspr
    "Мику нахмурилась, и её хватка усилилась."
    dv "А-а-ай-ай, ладно-ладно! Больше не буду!"
    us "Да-да, аналогично, шеф!"
    mi "Не верю! Извинитесь сперва!"
    dv "Прости, Мику…"
    mi "Не мне, а друг дружке скажите."
    "Потребовала Мику. Алиса скрипнула зубами и, опустив глаза, произнесла."
    dv "Прости, мелк… Кхм, Уляш! Мне не следовало так говорить."
    us "И ты извини… Алиса! Зря я так психанула."
    "У озорницы на щёках выступили слёзы. Либо от боли, либо от чувства вины, во что верилось с трудом."
    show mi grin pioneer
    show dv sad pioneer
    show us sad pioneer
    with dspr
    stop music fadeout 3
    mi "Кавай, ну вот и славненько!"
    "Праведные руки отпустили девочек. Те хором охнули от облегчения, а Мику довольно улыбнулась и потрепала их по макушкам."
    show mi happy pioneer with dspr
    mi "Ах, мои сладенькие анпанчики! Так бы вас и съела!"
    "Я осторожно встала, отряхиваясь и потирая ушибленную точку опоры. От обиды едва не расплакалась, хотя сама же совершила глупость."
    window hide
    show mi smile pioneer
    show dv normal pioneer
    show us normal pioneer
    with fade
    window show
    "Прошло ещё полчаса прежде, чем деревянный короб был целиком выкрашен в солидный морковный цвет." 
    "Краска закончилась аккурат с последним мазком кисти Алисы, и теперь они обе с Ульяной могли заняться более важным для себя в тот момент."
    dv "Ну вот, идти отмываться от этой дряни надо! Как думаешь, не высохла ещё?"
    us "Высохла, не высохла, но форму точно менять придётся! А ключ от склада у вожатой и поможатой, вот достанется…"
    show dv smile pioneer 
    show us smile pioneer
    with dspr
    dv "Да это не беда! Есть у меня идея. Айда за мной!"
    hide dv
    hide us
    with dissolve
    "И приятельницы вместе помчались в сторону кладовой." 
    "Я тем временем подняла уточку, забытую Алисой и спрятала в карман."
    show mi upset pioneer with dspr
    mi "Подумать только: из-за мелкой игрушки такой сырбор можно устроить!"
    un "Зато она помнит что-то хорошее, в отличие от меня…"
    mi "А? Ты чего?"
    un "Неважно, забудь… Пойдём к вожатой? Нужно отчитаться, что всё сделано."
    mi "О, и то верно! А где её искать?"
    un "Хм… Я думала, ты знаешь."
    show mi smile pioneer with dspr
    mi "А, вспомнила! Они же в кружке рукоделия сегодня!"
    un "А у нас и такой есть?"
    mi "Да, Славя им заведует! Думаю, вожатая тоже там. Пойдём?"
    window hide
    scene bg ext_square_day 
    show mi normal pioneer at right
    with dissolve
    show sl normal pioneer at left 
    show mt normal pioneer panama at center behind mi
    with dissolve
    window show 
    "Далеко мы не ушли, вожатая со Славей сами вышли навстречу."
    sl "Ой, привет девчат! Вы уже всё?"
    show mi smile pioneer with dspr
    mi "Да, рыженькие уже закончили красить, мы как раз к вам шли."
    show mt smile pioneer panama with dspr
    mt "Отлично! Теперь вам обеим по ещё одному поручению!" 
    mt "Мику, можешь помочь поварихам перед обедом прибраться в столовой? Девочки сегодня совсем замотались, а им ещё заказ на вечер предстоит большой." 
    mt "А тебе Лена, со Славей сразу после обеда, нужно сплавать на остров собрать земляники."
    un "А зачем?"
    mt "Сюрприз!"
    un "Хорошо."
    play sound sfx_dinner_horn_processed
    mt "Идите обедать. Мику задержись ещё на два слова."
    window hide
    scene bg ext_dining_hall_away_day
    show sl pioneer normal
    with dissolve
    pause 1
    scene bg ext_dining_hall_near_day
    show sl normal pioneer 
    with dissolve
    window show
    sl "Лен, совсем забыла, надо в домик заскочить, займи мне место в столовой."
    un "Хорошо."
    window hide
    play ambience ambience_dining_hall_full fadeout 1 fadein 2
    play sound [sfx_open_dooor_campus_1, sfx_close_door_campus_1]
    pause 1
    scene bg int_dining_hall_people_day with dissolve 
    window show
    th "Опять рыба, да ещё и с картофельным пюре! Сегодня никак рыбный день какой-то?"
    show sl normal pioneer at right with dissolve
    "Когда взяла тарелку, сразу вернулась подруга. Я объяснила пионерам, что она за мной занимала." 
    "Получив свою порцию, Славя показала на свободный столик."
    sl "Вон там место свободное — пойдём."
    show pi normal aonl_pioneer at left with dissolve 
    "Мы направились к столику, который буквально за пару секунд до нас успел занять Семён. Славю это не остановило, а напротив мотивировало."
    show sl smile pioneer with dspr
    sl "Можно?"
    me "А? Да, конечно!"
    "Семён выдвинул стул для первой красавицы лагеря."
    me "Прошу!"
    show sl smile2 pioneer with dspr
    th "Мог бы и мне внимания оказать."
    un "Приятного аппетита…"
    "Я ждала от него какой-либо реакции, но он просто смотрел на меня{w} Не выдержав зрительный контакт, уткнулась в тарелку стараясь, стать незаметной."
    
    if aonl_sl_story:
        th "Пусть поворкуют, я тут лишняя."
    
    if aonl_un_story:
        th "Он не знает этикета?"
    
    show pi smile aonl_pioneer with dspr
    me "И тебе."
    th "Догадался, наконец, лучше поздно чем, никогда."
    sl "Семён, есть какие-то планы на день?"
    me "Нет."
    show sl smile pioneer with dspr
    sl "Тогда ты не хочешь с нами прокатиться на лодке до острова?"
    show pi normal aonl_pioneer with dspr
    "Юноша задумался."
    th "Может, если у него свои дела, то откажется…" 
    me "Зачем?"
    sl "Ольга Дмитриевна просила земляники собрать. Там её много, и она такая вкусная!"
    "Славя блаженно закатила глаза, всем своим видом стараясь передать восторг от земляники."
    th "Какая актриса пропадает."
    me "Земляника… А зачем?"
    show sl shy pioneer with dspr
    sl "Не знаю, но, по-моему, идея отличная."
    show pi smile aonl_pioneer with dspr
    me "Да, пожалуй."
    window hide
    play ambience ambience_boat_station_day fadeout 1 fadein 2
    scene bg ext_boathouse_day
    show sl normal pioneer at left
    show pi normal aonl_pioneer at right
    with fade2
    window show
    play music music_list["everyday_theme"] fadein 3
    "Минут за десять добрались до пристани, корзинки уже лежали в лодке." 
    "Около реки, как всегда, было прохладно и светло." 
    "А когда светит солнце, кажется, что в ней отражаются тысячи солнечных блёсток." 
    "Одинокие вербы, которые стоят вблизи, купают свои длинные жёлто-зеленоватые косы в кристальной воде."
    sl "Вот лодка. Подожди, сейчас за вёслами схожу."
    hide sl with dissolve
    "Мы остались наедине с Сёмой."
    me "А ты землянику любишь?"
    th "Он обратил на меня внимание?"
    "Я улыбнулась."
    un "Не то чтобы очень… Но она вкусная."
    th "Опять запинаюсь, да что же это!"
    me "Ясно…"
    "Юноша снова замолчал, да и я не знала о чём говорить."
    "Какое-то время мы стояли в нерешительности, лишь иногда обмениваясь взглядами."
    show sl normal pioneer with dissolve
    sl "Вот!"
    "Подруга протянула Сёме два весла."
    me "Ага… да…"
    window hide
    scene cg d5_boat with dissolve
    stop ambience fadeout 2
    play sound_loop sfx_rowboat_loop fadein 2
    window show
    "Мы запрыгнули в лодку, и Сёма налёг на вёсла."
    me "А куда именно плыть?"
    sl "Вон туда!"
    "Подруга показала в сторону острова."
    sl "Остров «Ближний»."
    me "Вас понял!"
    "Весь путь мы плыли зигзагами. Видно для Сёмы это впервые. Я старалась смотреть себе в ноги, не встречаясь с парнем взглядом."
    "Примерно на середине пути он бросил вёсла."
    me "А что… Земляника больше нигде не растёт? Я имею ввиду места более доступные."
    sl "Но там она самая вкусная."
    th "Видимо, подруга не поняла в чём проблема."
    un "Тебе, наверное, тяжело одному грести?"
    me "Да нет… Нормально…"
    "Юноша постеснялся попросить помощь, но во взгляде явно читалась благодарность."
    "Чуть отдохнув, Сёма вновь принялся за тяжёлую работу."
    "Мы со Славей обсуждали планы на день."
    window hide
    stop sound_loop
    play sound sfx_boat_impact
    play ambience ambience_lake_shore_day fadein 2
    scene bg ext_island_day
    show sl normal pioneer at left
    show pi normal aonl_pioneer at right
    with dissolve
    window show
    "Наконец прибыли на место, Семён вышел на берег, слегка покачиваясь от усталости."
    sl "Держи!"
    "Славя передала Сёме корзинку."
    sl "Нам бы надо разделиться, так быстрее управимся."
    me "Да, наверное."
    un "Но корзинок-то только две."
    show sl surprise pioneer with dspr
    sl "Да, точно, это я недоглядела!"
    th "Понятное дело, мы же изначально не планировали брать ещё кого-то."
    un "И как мы разделимся?"
    "Сёма задумался, словно выбирая кого-то из нас."
    
    if aonl_sl_story:
    
        "Очень хотелось пойти с ним, если он позовёт, но юноша так и не решился."
        me "Одна корзина у меня, одна - у вас; всё очевидно."
        show sl shy pioneer with dspr
        sl "Нет, давай я с тобой пойду!"
        "Наша активистка, как всегда, взяла быка за рога."
        me "Давай…"
        "Он аж засветился."
        th "Ох дура, на что я надеялась? Когда уже это прекратится?!"
        hide pi
        hide sl
        with dissolve
        stop music fadeout 3
        "С абсолютно безразличным лицом взяла корзинку и, не глядя на них, скрылась в чаще, стараясь уйти как можно дальше, чтобы не слышать их голоса."
        window hide
        play ambience ambience_forest_day fadeout 1 fadein 2
        scene bg ext_polyana_day
        window show
        "Наконец выйдя на лужайку, я остановилась и облегчённо выдохнула."
        th "Пусть воркуют, а я займусь делом."
        "Выкинув лишнее из головы, я принялась за жатву. Земляники оказалось действительно очень много, буквально под каждым кустиком находилась спелая ягода." 
        "Мои руки работали словно автомат, наполняя корзину, а в голове осталась лишь одна цель – как можно быстрее выполнить поставленную задачу, не отвлекаясь ни на что."
        "Вскоре земляника перестала помещаться в корзину."
        th "Фух, наконец, закончила. Надеюсь ребята тоже. Если нет, подожду у лодки."
        window hide
        play ambience ambience_lake_shore_day fadeout 1 fadein 2
        scene bg ext_island_day
        show sl smile pioneer at left
        show pi smile aonl_pioneer at right
        with dissolve
        window show
        un "Ой, а вы уже тут… Вот."
        "Я протянула корзинку."
        show sl smile2 pioneer with dspr
        sl "Отлично! Теперь можно отправляться назад."

    if aonl_un_story:
        me "Давай я пойду с тобой."
        th "Догадался!!!"
        un "Давай…"
        show sl smile pioneer at left with dspr
        sl "Вот и славно!"
        hide sl with dissolve
        "Славя взяла вторую корзинку и пошла в дальнюю часть острова."
        show pi smile aonl_pioneer with dspr
        me "Ну что?"
        un "Что?"
        me "Пойдём?"
        un "Ага."
        "Я улыбнулась."
        me "Только собирай внимательнее! Не пропускай ни одной ягодки!"
        un "И ты тоже."
        stop music fadeout 2
        window hide
        play music music_list["she_is_kind"] fadein 3
        show pi normal aonl_pioneer with fade
        window show
        "Мы приступили к сбору."
        "Земляника оказалась вкусной."
        "Сёма, наверное, съел бы её всю, если бы вовремя не остановился."
        "Я шла рядом, больше любуясь Сёмой, чем собирая ягоды, так как корзинка у нас всего одна."
        "Юноша словно грибник, заглядывал под каждый кустик."
        un "У тебя как-то лучше получается…"
        me "Да?{w} Я их не считаю, если честно."
        un "Нет, правда!"
        "Корзинка была уже наполовину заполнена."
        me "А ты, наверное, любишь природу?"
        stop music fadeout 2
        un "Да."
        window hide
        with flash
        window show
        "Сёма вдруг зажмурился на секунду, опустился на землю и прислонился к дереву."
        me "Красиво всё же здесь!"
        "Я села рядом, коснувшись его локтя своим."
        play music music_list ["lets_be_friends"] fadein 2
        un "Да!"
        scene cg d5_un_sleep with dissolve
        "Мы сидели, прижавшись, друг к другу, наслаждаясь покоем и умиротворением, под щебет птиц и шум листвы."
        "Я почувствовала, как веки наливаются тяжестью, а голова заваливается на бок."
        "Последнее, что помню перед падением в забытье, это безопасность и покой на плече любимого мужчины..."
        "Не знаю, сколько мы так сидели в мире грёз может час, а может 5 минут, но крик Слави вернул к реальности."
        sl "Семён! Лена!"
        stop music fadeout 3
        window hide
        scene bg ext_island_day 
        show pi smile aonl_pioneer at center    
        with dissolve
        # play ambience ambience_forest_day fadein 3
        window show
        "Я открыла глаза и, не совсем придя в себя, недоумённо посмотрела на Сёму."
        me "Что снилось?"
        un "А?"
        th "Ой, я же спала на его плече!"
        "Смущение снова накрыло с головой, покрыв румянцем."
        un "Извини…"
        me "Да ничего."
        "К нам подошла Славя, и я поспешно встала."
        show pi normal aonl_pioneer at cright
        show sl smile pioneer at cleft
        with dissolve
        sl "И как улов?"
        "Сёма грустно протянул корзину."
        show sl normal pioneer at cleft with dspr
        sl "Что-то негусто…"
        "Корзинка Слави оказалась набита доверху."
        sl "Ладно, нам и этого хватит!{w} Пора возвращаться!"
        "Сёма взял корзинку, и мы направились назад к лодке."
        window hide

    scene cg d5_boat with dissolve
    stop ambience fadeout 2
    play sound_loop sfx_rowboat_loop fadein 2
    # play music music_list["everyday_theme"] fadein 3
    window show
    "Обратную дорогу Сёма преодолел значительно быстрее, видимо сказался опыт." 
    "Но пришвартовавшись, юноша обессилено упал на песок."
    stop sound_loop fadeout 1
    play ambience ambience_boat_station_day fadeout 1 fadein 2
    scene bg ext_boathouse_day
    show sl angry pioneer at left
    show pi normal aonl_pioneer at right
    with dissolve
    window show
    sl "Если тебе было так тяжело, то сказал бы!"
    un "Да…"
    me "Да нет ничего… Сейчас полежу и всё пройдёт…"
    th "Храбрица перед нами, порой мальчишки такие дураки."
    show sl normal pioneer at left with dissolve
    sl "Ладно, тогда отнеси корзинки, пожалуйста, Ольге Дмитриевне, а то у нас ещё дела есть."
    me "Да конечно."
    window hide
    scene bg black with Dissolve(1)
    play ambience ambience_camp_center_day fadein 2
    scene bg ext_square_day with Dissolve(1)
    show sl normal pioneer with dissolve
    window show
    sl "Лен, ты уже собрала вещи в поход?"
    un "Какой поход?"
    show sl happy pioneer with dspr
    sl "Ох, ты наша спящая красавица. Я видела, как ты клевала носом. Сегодня поход в лес будет. Нужно подготовиться."
    un "А… Когда?"
    show sl smile2 pioneer with dspr
    sl "После ужина, без ночёвки."
    un "Я… Поняла."
    show sl smile pioneer with dspr
    sl "Тогда расходимся по домикам, встречаемся на ужине."
    un "Хорошо."
    hide sl with dissolve
    th "Никогда не бывала в походах, что туда брать?" 
    th "Может Мику подскажет, если она в домике?"
    window hide
    play ambience ambience_int_cabin_day fadeout 1 fadein 2
    scene bg int_house_of_un_day with fade2
    window show
    play music aonl_wake_up fadein 2
    "Подруги на месте не оказалось. Придётся решать самой."
    "Я достала сумку и перебрала свои нехитрые пожитки."
    "Вещей совсем немного тетрадки, набор карандашей для рисования, альбомы, пара книжек, зеркальце, про которое напрочь забыла. Взять по сути нечего, хотя..."
    "Захватила на всякий случай запасные платочки."
    "В общем-то всё."
    "Начала укладывать вещи обратно, но вдруг моя рука натолкнулась, на что-то твёрдое в альбоме."
    "Открыв на середине, увидела нож, завёрнутый в бумагу."
    th "Совсем забыла. Этот нож взяла в лагерь для резки бумаги, но и в походе он пригодится." 
    th "С самого детства боюсь, леса и населяющих его зверей." 
    th "Конечно, там буду не одна и всё же стоит подстраховаться."
    "Нож почти полностью поместился в кармане юбки, я закрыла его сверху платочком."
    th "Можно идти."
    stop music fadeout 2
    window hide
    play ambience ambience_camp_center_day fadeout 1 fadein 2
    scene bg ext_square_day with fade2 
    window show
    th "Подожду Славю до ужина."
    show pi upset aonl_pioneer with dissolve
    th "Куда это он? У него лицо какое-то напряжённое."
    
    if aonl_sl_story:
    
        th "Впрочем, не моё дело, не стоит лишний раз лезть к чужому парню."
        hide pi with dissolve
        "Я посидела пару минут, стараясь отвлечься мыслями о приближавшемся закате на площади, и как здорово его запечатлеть в красках." 
        "Но вскоре встала с лавочки и отправилась за Сёмой."
        th "Просто посмотрю, что он делает и всё, хуже от этого не станет."
        window hide
    
    if aonl_un_story:
        hide pi with dissolve
        "Я тихонько направилась за ним. Вдруг ему требуется помощь, слишком он напряжён."
        window hide
    
    play sound sfx_hiding_in_bush
    scene bg ext_clubs_day with dissolve
    window show
    "Сёма зашёл в клубы, а я спряталась в кустах, чтобы не привлекать внимание."
    th "Ох, чем же я занимаюсь, прям как шпионка какая-то!"
    play sound [sfx_open_door_2, sfx_close_door_1]
    show pi upset aonl_pioneer with dissolve
    "Вскоре юноша вышел с мешком сахара на плечах и направился в сторону площади, но далеко не ушёл. Спустя каких-то 20 метров опустил ношу на землю, тяжело дыша."
    play music music_list ["she_is_kind"] fadein 2
    th "Да он, же так совсем вымотается!"
    un "Может, тебе помочь?"
    show pi surprise aonl_pioneer with dspr
    "Сёма удивлённо обернулся."
    me "Не думаю, что у тебя получится…"
    un "Я могу за тележкой сходить."
    show pi smile aonl_pioneer with dspr 
    me "Да, это было бы здорово!"
    "Казалось, у него открылось второе дыхание."
    "От его радости, даже я приободрилась."
    un "Подожди тут, я быстро!"
    # Тряска экрана 
    window hide
    scene bg ext_square_day at aonl_running
    show dv normal pioneer2 far  
    with dissolve 
    window show
    "До площади летела, словно на крыльях, хотелось помочь как можно скорее. На краю зрения увидела Алису, но сейчас не до неё."
    window hide
    # Тряска экрана
    scene bg aonl_ext_dining_hall_behind with dissolve
    window show
    "Вот и тележка, хорошо, что она на месте! Было бы неудобно, вернуться к Сёме без неё."
    "Схватив тележку, я побежала обратно."
    window hide
    # Тряска экрана 
    scene bg ext_square_day at aonl_running
    show dv normal pioneer2 far
    with dissolve 
    window show
    "Алиса расположилась на лавочке, наблюдая за мной."
    th "Главное, что не лезет."
    window hide
    # Тряска экрана
    scene bg ext_clubs_day with dissolve
    show pi smile aonl_pioneer with dissolve
    window show
    me "Спасибо."
    "Сёма поставил мешок."
    un "Не за что…"
    
    show red_flash  # Показываем красную вспышку
    with Dissolve(0.5)  # Быстрое появление
    pause 0.5  # Задержка для эффекта
    hide red_flash  # Скрываем вспышку
    with Dissolve(0.5)  # Медленное исчезновение
    
    "От его благодарности, я смутилась, покраснев и опустив взгляд в ноги."
    un "Тогда, я пойду…"
    hide pi with dissolve
    me "Да, удачи! И ещё раз спасибо!"
    stop music fadeout 2
    "Крикнул он вслед, когда я всеми силами спешила на площадь."
    window hide
    scene bg ext_square_day with dissolve
    window show
    "Сев на лавочку, облегчённо выдохнула."
    th "Фух, не думала, что это будет так сложно." 
    th "Стоило ему с благодарностью на меня посмотреть, и я опять потеряла дар речи." 
    th "Словно громкость в горле убавили."
    show dv normal pioneer2 with dissolve
    play music music_list ["you_lost_me"] fadein 2
    dv "Подойди-ка сюда."
    "Рыжая поманила меня пальцем."
    "Делать нечего пришлось подчиниться."
    dv "И куда это мы такие счастливые с тележкой бегаем?"
    un "Да… Я просто…"
    show dv angry pioneer2 with dspr
    dv "Ну!"
    "Подогнала меня Алиса."
    window hide
    show pi smile aonl_pioneer far at fright behind dv with moveinright
    show pi smile aonl_pioneer far at center behind dv with moveinright
    show pi smile aonl_pioneer far at fleft behind dv with moveinright
    hide pi with moveoutleft
    window show
    "Тем временем Семён прокатил тележку по площади."
    show dv smile pioneer2 with dspr
    dv "Серьёзно? Ты до сих пор не поняла, что он неудачник?"
    "От последней фразы меня передёрнуло."
    show dv shocked pioneer with dspr
    un "Не смей его так называть! И вообще тебя это не касается, за собой следи!" with vpunch
    "От моего крика Алиса опешила так, словно на её глазах сломали любимую гитару."
    window hide
    scene bg ext_houses_day with dissolve
    window show
    "Я пошла домой, кипя от гнева."
    th "Да что она себе позволяет!? Как смеет говорить о нём так!? Бесит сука! Бесит!!!"
    th "Видеть её не хочу!"
    stop music fadeout 2
    window hide
    scene bg ext_house_of_un_day with dissolve
    window show
    th "Лучше посижу в домике до ужина, не то не знаю, что с ней сделаю."
    window hide
    play sound [sfx_open_dooor_campus_1, sfx_close_door_campus_1]
    pause 1
    play ambience ambience_int_cabin_day fadeout 1 fadein 2
    scene bg int_house_of_un_day with dissolve
    pause 1
    show blink
    play sound sfx_bed_squeak2
    pause 1
    window show
    "Я легла на кровать, стараясь успокоить нервы."
    window hide
    stop ambience fadeout 2
    pause 3
    $ sunset_time()
    $ persistent.sprite_time = "sunset"
    hide blink
    play sound sfx_dinner_horn_processed
    play ambience ambience_int_cabin_evening fadein 2
    scene bg aonl_int_house_of_un_sunset
    show unblink
    pause 1
    window show
    "Потянулась, разминая мышцы."
    th "Ах, так гораздо лучше."
    window hide
    play ambience ambience_camp_center_evening fadeout 1 fadein 2
    scene bg ext_dining_hall_near_sunset with fade2
    show sl normal pioneer with dissolve
    window show
    sl "Как у тебя дела, всё собрала?"
    un "Да."
    sl "Отлично, знаешь, зачем мы, оказывается, собирали землянику?"
    un "Нет."
    sl "Вожатая распорядилась испечь торт в честь спасения Саши. Так что, сегодня будет сладенькое."
    "Девушка аж слегка запрыгала на месте от предвкушения, что для неё странно."
    th "Впрочем, её можно понять, не каждый день выдают сладенькое в столовой." 
    th "Я тоже с удовольствием поем тортик."
    window hide
    play ambience ambience_dining_hall_full fadeout 1 fadein 2
    play sound [sfx_open_door_2, sfx_close_door_1]
    pause 1
    scene bg aonl_int_dining_hall_people_sunset
    show sl normal pioneer at right
    with dissolve
    window show
    "Сложив два и два, пришла к очевидному выводу."
    un "Видимо, за этим Сёма мешок с сахаром таскал."
    show sl surprise pioneer with dspr
    sl "Мешок с сахаром?"
    un "Да, он сегодня мучился с ним. Я тележку дала, чтобы помочь."
    show sl smile pioneer with dspr
    sl "Вот как? Да, наверное, за этим. Спасибо, что приглядела Лен."
    un "Приглядела?"
    show sl smile2 pioneer with dspr
    sl "Именно. Сёма мальчик хороший, и всё же за ним присмотр нужен, он не всегда проявляет инициативу. Его вести надо."

    if aonl_sl_story:
    
        th "Странно, а мне он показался другим."

    if aonl_un_story:
    
        th "В чём-то она права... И всё же Сёма очень смелый."
    
    sl "Вон он, кстати."
    "Сёма лежал на столе, отодвинув тарелку и опустив голову на руки."
    un "Что с ним!?"
    show sl sad pioneer at right with dspr
    sl "Не знаю, пойдём."
    show pi upset aonl_pioneer at left with dissolve
    sl "Что с тобой?"
    me "Ничего…"
    "Юноша продолжал подпирать стол головой."
    "Мы сели рядом."
    show sl normal pioneer with dspr
    sl "Устал?"
    me "Да, есть немного…"
    show sl sad pioneer with dspr
    sl "Это плохо."
    me "Ещё бы…"
    show sl normal pioneer with dspr
    sl "Ты же помнишь, что после ужина мы все в поход идём? Уже собрался?"
    "Наконец, Сёма поднял на нас взгляд."
    me "Чего? Куда?"
    sl "Поход…"
    "На лице Семёна отразилось все, что он думает о походе, лагере и мировом капитализме вместе взятых."
    sl "Ты не знал?"
    me "Нет…"
    "Он снова опустил голову и закрыл её руками."
    "Мы переглянулись и молча, приступили к трапезе."
    th "Лучше его не трогать. Бедненький, совсем вымотался."
    window hide
    with fade
    show mt smile pioneer far behind pi, sl with dissolve
    window show
    "Когда ужин подходил к концу, вожатая напомнила о себе."
    mt "Ребята! В честь чудесного спасения нашего друга и товарища, Шурика, мы приготовили для вас этот торт!"
    th "Наконец поедим торт!"
    mt "Сейчас… Сейчас…"
    sl "Пойдём! А то без нас съедят!"
    un "Пойдём."
    me "Да, конечно…"
    "Мы двинулись к месту столпотворения пионеров."
    show mt smile pioneer with dissolve
    "Вожатая выставила торт на середину."
    mt "А теперь…"
    play music music_list ["awakening_power"] fadein 2
    window hide
    scene cg d5_cake with dissolve
    window show
    "Закончить Ольга не успела." 
    "Выскочившая Ульяна накинулась на торт, пытаясь впихнуть в себя невпихуемое."
    "Мелкая упиралась и визжала пока её оттаскивали, стараясь съесть как можно больше."
    "Я подцепила остатки торта пальцем."
    th "Земляничный крем такой вкусный..."
    th "Жаль, рыжик всё испортила."
    window hide
    scene bg aonl_int_dining_hall_people_sunset
    show mt rage pioneer at center
    show us surp1 pioneer at right
    with dissolve
    window show
    mt "Ульяна! Это уже переходит все границы!"
    show us cry pioneer at right behind mt with dissolve
    us "Я… Я…"
    show sh normal pioneer at left with dissolve 
    sh "Да ладно вам, Ольга Дмитриевна! Раз уж торт в мою честь, то ничего страшного…"
    mt "Неважно!"
    mt "А ты… Тебя я сегодня накажу по-настоящему, чтобы в следующий раз знала!" 
    show us angry pioneer at right behind mt with dissolve
    us "Ради бога!"
    "Мелкая фыркнула и отвернулась."
    mt "Сегодня с нами в поход не идёшь!"
    us "Да больно надо было!"
    "Поняв, что конфликт исчерпан, а поесть не удастся, пионеры стали расходиться."
    stop music fadeout 2
    window hide
    play ambience ambience_camp_center_evening fadeout 1 fadein 2
    scene bg ext_square_sunset with fade2
    show mi smile pioneer at left with dissolve
    window show
    mi "Леночка, это так здорово! В походе сможем поиграть на гитаре у костра. Я как раз захватила одну. Может, после этого ко мне кто-нибудь запишется. Думаю, если…"
    "Подруга щебетала без умолку и светилась от счастья." 
    "Я же лишь надеялась, как можно скорее вернуться назад и лечь поспать." 
    "Не люблю шумные компании."
    "Да и походы в целом никогда не вызывали у меня какого-либо энтузиазма."
    
    show mt smile pioneer panama behind mi with dissolve
    mt "Вроде все на месте… Отлично!"
    mt "Итак, сегодня мы отправляемся в поход!"
    mt "Важным для любого пионера является умение прийти на выручку товарищу, протянуть руку помощи в трудную минуту, просто спасти его в безвыходной ситуации, наконец!"
    mt "Всему этому нам с вами предстоит научиться!"
    "По толпе пронёсся шёпот, что весь поход закончится в паре километров от лагеря."
    th "Мне так лучше."
    mt "Идти будем парами. Так что, если вы ещё не выбрали себе партнёра, сейчас самое время!"

    if aonl_un_story:
        show mi smile pioneer close with dissolve
        "Хотела встать в пару с Семёном, но Мику уже пристроилась рядом."

    if aonl_sl_story:
        show mi smile pioneer close with dissolve
        "Мику встала рядом со мной."
    

    
    mi "Пойдём вместе подруженька!"
    un "Ага."
    window hide
    play ambience ambience_forest_evening fadeout 1 fadein 2
    scene bg ext_path_sunset
    show mi happy pioneer close at left
    with dissolve
    window show
    "Поначалу было жутковато в лесу, но Мику своим непрерывным щебетом, о том какая вокруг красивая природа, отвлекла от неприятных мыслей." 
    "К тому же если тут есть хищники они уже десять раз сбежали, не выдержав её голоса, который порой переходил до ультразвука."
    window hide
    scene bg ext_polyana_sunset with dissolve2
    window show
    "Примерно через пол часа, мы прибыли на место назначения."
    show mt smile pioneer panama with dissolve
    play music music_list ["dance_of_fireflies"] fadein 2
    mt "Тут сделаем привал."
    hide mt with dissolve
    "Мальчики принесли дров, а Ольга разожгла костёр."
    show sl normal pioneer at cleft
    show pi normal aonl_pioneer at fleft 
    with dissolve
    "Славя о чём-то разговаривала с Сёмой."
    hide sl
    hide pi
    with dissolve
    
    if aonl_un_story:

        th "Нужно действовать, а то пока буду топтаться на месте, могу упустить. Я признаюсь ему сейчас!"
        show dv normal pioneer with dissolve
        "Поправила причёску, отряхнула юбку и гольфы от пыли. Начала осматривать одежду со всех сторон и увидела Алису, подзывавшую жестами к себе."

    if aonl_sl_story:

        show dv normal pioneer with dissolve
        "Я отвернулась и увидела Алису, подзывавшую жестами к себе."
    
    stop music fadeout 2
    th "Как же порой хочется оставить её в лесу и сказать, что она потерялась."
    play music music_list ["blow_with_the_fires"] fadein 3
    un "Что?"
    dv "Ничего! Разговор ещё не закончен."
    un "Какой?"
    show dv angry pioneer with dspr
    dv "Который начали на площади. Думаешь, не вижу, что ты носишься как угорелая, когда Семён рядом. Я тебя как облупленную со школы знаю, на лбу написано «Любовь»." 
    dv "Неужели не смогла никого лучше найти?" 
    dv "Он даже не понимает твоих чувств, для него ты в лучшем случае знакомая!"
    "Я почувствовала, как начинаю понемногу закипать, но сдержалась и ответила вполне сдержанно и спокойно."
    un "Это абсолютно не твоё дело. Ты мне не подруга, чтобы лезть, куда не просят."
    show dv guilty pioneer with dspr
    dv "Мы были подругами, Ленка." 
    dv "И по старой памяти хочу дать совет, держись от него подальше."
    un "Такой же совет, как с Антоном? Это ведь из-за тебя мы расстались. Наговорила гадостей обо мне, а ему про меня!" 
    un "Я слишком поздно поняла, что случилось, если бы не ты!"
    "Сама не заметила, как начала заводиться. Уж слишком свежа в памяти давняя обида."
    show dv sad pioneer with dspr
    dv "Тогда я ошибалась насчёт Антона, но сейчас другой случай."
    un "Ты не ошибалась!"
    "Теперь уже разошлась не на шутку."
    un "Ты поссорила нас из зависти! Я слышала, как потом жаловалась подружкам, что мальчики обращают внимание на меня, а тебя не замечают. Думаешь, я дура?! Не вижу, как ты кипишь от злости, когда ко мне подходят знакомиться?!"
    show dv angry pioneer with dspr
    "От негодования у меня перехватило дыхание, а Алиса озлобилась, сверля взглядом."
    un "Вот теперь слушай…"
    dv "Нет, это ты меня послушай!"
    un "Ничего я слушать не собираюсь!"
    show dv rage pioneer with dspr
    dv "Да? Ну и отлично!"

    if aonl_sl_story:
        show dv grin pioneer with dspr
        dv "Хотя напоследок всё равно скажу, Семён твоим не будет, ты ему не нужна."
        dv "Наша мисс идеал захомутала неудачника, он даже не осознаёт, какие чувства ты к нему испытываешь!"
        "Я словно получила пощёчину. Конечно, понимала, правдивость её слов, но слышать такое, вот так прямо."
        un "Ты… Ты… В-врёшь."
        "На глаза начали наворачиваться слёзы."
        show dv laugh pioneer with dspr
        dv "Вру? Хм, а где они тогда?"
        "Я окинула взглядом поляну. Славю и Семёна не обнаружила."
        th "Неужели они ушли вдвоём!?"
        "Повернувшись к Алисе не нашла, что сказать."
        show dv grin pioneer with dspr
        dv "То-то же."
        stop music fadeout 2
        hide dv with easeoutleft
        "Алиса, надменно развернувшись, ушла к костру, а я так и осталась стоять в стороне."
        "Какое-то время потопталась на месте, смахнула слёзы и попыталась успокоить колотящееся сердце."
        th "Не хочу сидеть у костра, не хочу общаться с другими, не люблю шумные компании." 
        th "Лучше уйду отсюда."
        "Улучив момент, когда вожатая отвернулась, я скрылась в чаще."
        show dv grin pioneer with dissolve
        "Обернувшись назад в последний раз, поймала надменный взгляд Алисы."
        window hide
        $ night_time()
        $ persistent.sprite_time = "night"
        play ambience ambience_camp_center_night fadeout 1 fadein 2
        scene bg ext_aidpost_nigh_after_rain with dissolve
        window show
        "Пока шла назад, места себе не находила. Перед глазами стоял образ надменной Алисы."
        th "Да она права" 
        th "Права, на все 100 процентов." 
        th "Но не хочу слышать это в сотый раз, тем более от неё..." 
        th "Сегодня мне нормально не заснуть."
        th "Выпью снотворного, я знаю, где медсестра прячет ключ."
        play music music_list ["into_the_unknown"] fadein 3
        th "А почему свет горит?"
        "Я осторожно приблизилась к окну и заглянула."
        window hide
        $ persistent.sprite_time = "sunset"
        scene bg int_aidpost_night
        show sl sad pioneer at left
        show pi normal aonl_pioneer at right 
        with dissolve
        window show
        th "Вы издеваетесь!"
        "Славя лежала на кушетке, а Семён стоял рядом. Они о чём-то говорили."
        window hide
        scene cg d5_sl_sleep with dissolve 
        window show
        "Затем юноша снял обувь и лёг с ней, свет погас, но сияния Луны хватало разглядеть силуэты."
        "Я отпрянула от окна."
        window hide
        scene bg ext_aidpost_night
        window show
        th "Неужели они собираются… Нет, не может быть, просто не может быть! Ладно Семён, но Славя, она же помощница вожатой, спортсменка, активистка, отличница, неужели она…?!"
        "Я невольно снова потянулась к окну."
        $ persistent.sprite_time = "night"
        show mt angry pioneer panama with dissolve
        mt "Лена, вот ты где! Почему отделилась от своего отряда?"
        "Сердце ушло в пятки. Если она увидит."
        un "Ну… Мне захотелось побыть одной, Ольга Дмитриевна, только и всего."
        mt "Мне эти ваши юношеские одиночества вот уже где! А нам, старикам, после них валидол глотать и перед родителями отчитываться, где может быть их чадо."
        th "Вот уж нравится людям вообразить себя взрослее, чем они кажутся!" 
        th "Но в чём-то вожатая права — я ушла незаметно ото всех, и ночью в лесу могло случиться всякое."
        un "Извините, что не предупредила…"
        mt "Речь сейчас не о тебе, впрочем, а о твоём сверстнике. Не знаешь ли ты, где Персунов Семён?"
        "Моя руководительница на последних словах добавила ещё и официозных интонаций."
        un "Я… Н-ну… Мне-то, почему это должно быть известно?"
        mt "Не знаю. Я думала, что там, где ты, должен быть и он."
        un "Ну, вы его рядом со мной видите?"
        mt "В самом деле… А предположения есть какие-то?"
        "Я развела руки в стороны и пожала плечами."
        mt "Ладно иди спать. Семёна найду сама."
        th "Вроде пронесло."
        mt "Подожди, а что ты в окне хотела увидеть, Виола разве там?"
        un "Да я…"
        "Не слушая, вожатая подошла к окну."
        th "КАТАСТРОФА!!!!!!!!!!!"
        show mt sad pioneer panama with dissolve
        "На секунду она опешила, но вдруг подобралась и побежала внутрь."
        hide mt
        "Свет в медпункте зажёгся, послышалась ругань."
        "Воспользовавшись моментом я попятилась назад и побежала со всех ног."
        stop music fadeout 3
        window hide
        # тряска
        scene bg aonl_ext_houses_night at aonl_running with dissolve
        window show
        play music music_list ["drown"] fadein 2
        th "Кошмар, что теперь будет с Сёмой, с Славей. Если бы не я…"
        "Я схватилась за голову."
        window hide
        play ambience ambience_int_cabin_night fadeout 1 fadein 2
        scene bg int_house_of_un_night with fade2
        window show
        "Вбежав внутрь рухнула на кровать, не раздеваясь и накрыла голову подушкой."
        th "Блин, блин, что я наделала!"
        "Застучала ножками по кровати от безысходности."
        th "Зачем я туда полезла, ну зачем??!!"
        "Казалось, не найду покоя, но эмоциональная усталость за день дала о себе знать, и я сама не заметила, как отключилась..."
        window hide
        stop music fadeout 2
        show blink
        stop ambience fadeout 2
        pause 4
        jump aonl_day6_alt

    if aonl_un_story:
        "Алиса отвернулась, но вдруг резко сказала."
        dv "Так ты подслушиваешь!"
        me "Что? Я? Нет!"
        show pi normal aonl_pioneer at left behind dv with dissolve
        "Оказывается, к нам незаметно подошёл Семён"
        show dv angry pioneer with dissolve
        "Алиса медленно двинулась на него, а юноша попятился."
        "Ой, что же сейчас будет!?"
        show dv normal pioneer with dspr
        dv "Хотя ладно, слушай!"
        "Алиса остановилась и обернулась ко мне."
        show dv grin pioneer 
        show pi surprise aonl_pioneer
        with dspr
        dv "А знаешь, кстати, что он сегодня за мной подглядывал?"
        un "Что?"
        dv "Подглядывал. И {i}всё{/i} видел!"
        un "Это… Это правда?"
        show pi upset aonl_pioneer with dspr
        "Сёма как-то замялся."
        "Неужели, Сёме нравится Алиса?! {w} Я думала…"
        "Я бросила на него жалобный взгляд и заломила руки."
        "Он меня не любит!"
        "Я опустила глаза, не в силах на него смотреть."
        me "Нет-нет, ничего такого не было!"
        show dv grin pioneer behind pi
        show pi upset aonl_pioneer close at left
        with dissolve
        "Он подошёл ко мне, бросив на Алису гневный взгляд."
        show dv grin pioneer with dspr
        dv "Как же не было? У меня и свидетели есть!"
        me "Да твои свидетели ещё похлеще тебя!"
        th "Слава богу! Опять Алиса врёт. Сука!"
        "Хотелось убить рыжую на месте!"
        "Вместо этого, я с надеждой и лаской посмотрела на моего любимого мужчину."
        "Почему-то, встретившись со мной глазами, Сёма поежился, словно ему стыдно."
        dv "Значит, будешь говорить, что не было?"
        "Любимый на секунду задумался."

        me "Нет, не было…"
        show dv laugh pioneer with dspr
        th  "Ну точно сука!"
        "Я бросила на Алису испепеляющий взгляд, а на моего хорошего посмотрела с нежность, но Сёма стыдливо отвёл глаза"
        th  "Что с ним?"
        me "Нет, не было…"
        "Повторил он уже менее уверенно."
        show dv laugh pioneer far at cleft with dspr

        dv "Ну, как знаешь."
        "Усмехнулась Алиса."
        dv "Тебе решать, кому верить."
        th "Уж точно не тебе и всё же стоит удостовериться."
        hide dv with dissolve
        "Рыжая гордо развернулась и направилась к костру."
        un "Правда?"
        "В этот вопрос я вложила всю надежду и мольбу, ведь Сёма очень дорогой мне человек."
        me "Ну, а если и было?"
        "Я вздрогнула как от пощёчины."
        th "Ну а если и было??????!!!!!!!!!!!!!"
        th "Нет, нет, нет этого не может быть! Сёмочка, за что?! Я же всегда рядом, я всегда за тебя, чтобы ни случилось, а ты, ты…"
        "Мир в голове словно разошёлся по швам. Казалось, я попала в кошмар наяву!"
        th "Какая же я дура! Сама себе придумала любовь, а Семён всего лишь проявлял вежливость. Ему на самом деле нужна Алиса, а я ничего не значу!"
        "Он посмотрел на меня с вызовом, и я почувствовала, как по щеке скользнула слеза."
        me "Нет, то есть я хотел сказать…"
        "Сёма явно смутился."
        th "Глупенький, не нужно передо мной оправдываться, иди к Алисе!" 
        th "Ваша жизнь - это ваша жизнь, а я уж как-нибудь сама…."
        un "Тебе не нужно врать."
        "Я вытерла слёзы и попыталась улыбнуться."
        un "В конце концов, это меня не касается."
        me "Нет, почему же не касается…"
        th "Чего он добивается?"
        show pi normal aonl_pioneer with dspr
        me "Просто я… Ну… Это вышло случайно, понимаешь?{w} Случайность, спровоцированная Ульянкой, не более."
        un "Да, конечно…"
        th "Врунишка, я же вижу, что безразлична."
        me "Правда!"
        un "Я верю."
        "Постаралась произнести эти слова максимально спокойно."
        th "Конечно, я не верю, сволочь!"
        show pi upset aonl_pioneer with dspr
        me "А мне кажется, не веришь."
        th "Он издевается?"
        un "Это что, какая-то игра?"
        "Весь разговор уже начал откровенно раздражать."
        un "Тебе обязательно нужно меня в этом убедить? И даже если я поверю, то что? Всё так и будет на самом деле?"
        me "Я не пытаюсь тебя ни в чём убедить, просто хочу, чтобы ты поняла, что я не виноват… И не хотел…"
        un "Да какое мне вообще дело!"
        "Закричала я во весь голос."
        "К нам повернулся весь лагерь."
        th "Пофиг, пусть думают, что хотят!"
        un "Хочешь подглядывать за кем-то – подглядывай! Что хочешь, то и делай! Но ко мне ты зачем лезешь?"
        me "Я не…"
        stop music fadeout 3
        un "Знаешь что! Мне всё равно!"
        window hide
        play ambience ambience_forest_night fadeout 1 fadein 2
        scene bg ext_path2_night with dissolve
        $ night_time()
        $ persistent.sprite_time = "night"
        window show
        play music music_list ["meet_me_there"] fadein 4
        "Я развернулась и побежала в лес." 
        "Подальше ото всех, подальше от Сёмы."
        "Слёзы застилали глаза, дыхание сбивалось, ветки хлестали по лицу и цеплялись за одежду." 
        "Но я продолжала бежать, сжав кулак у сердца."
        th "Как же больно, как же больно в груди, мне никогда не было так больно! {w} За что??!!"
        window hide
        play ambience ambience_boat_station_night fadeout 1 fadein 2
        scene bg ext_boathouse_night with dissolve
        window show
        "Выбежав из леса, я упала на песок, ободрав колени, и зарыдала во весь голос, свернувшись клубочком."
        scene cg aonl_un_cry_1 with dissolve
        th "За что, за что, за что, за что…???!!!"
        "Стучало в моей голове."
        scene cg aonl_un_cry_2 with dissolve
        th "За что судьба так жестока со мной?!" 
        th "Я же просто хотела любить и быть любимой, чтобы хоть кто-то проявлял ко мне ответную любовь и ласку!"
        th "Разве я так много просила???!!!"
        scene cg aonl_un_cry_1 with dissolve
        "Я зарыдала ещё сильнее выпуская всю боль, скопившуюся внутри."
        th "За что, Сёма?!"
        "Я продолжила лежать, тихо всхлипывая." 
        "Но вдруг, через боль и отчаяние пробилась мысль, заставившая замолчать."
        scene bg ext_boathouse_night with dissolve
        th "Нужно спрятаться!"
        th "Не стоит другим видеть меня такой." 
        th "В конце концов, мои личные страдания никого не волнуют и не касаются."
        "Я осмотрелась. Идея пришла вполне очевидная."
        window hide
        scene cg aonl_un_on_boot_sad with fade2
        window show
        "Взяв вёсла и отвязав лодку, поплыла на остров."
        "Там меня никто не найдёт, и я смогу побыть одна, никого не опасаясь..."
        window hide
        play ambience ambience_lake_shore_night fadeout 1 fadein 2
        scene bg ext_island_night with fade2
        window show
        "Сойдя на берег, я направилась вглубь острова."
        window hide
        # Лена прислонилась к дереву
        play ambience ambience_forest_night fadeout 1 fadein 2
        scene cg aonl_un_alone_day5 with dissolve
        window show
        "Уйдя достаточно далеко, прислонилась к дереву." 
        "Закрыла глаза, попытавшись выбросить всё из головы и успокоиться."
        "Бесполезно."
        window hide
        scene cg d2_sovenok
        show prologue_dream
        with flash
        window show
        "Я прячусь машинально за Сёмой, он такой смелый закрывает от опасности."
        window hide
        scene cg aonl_d6_un_dance
        show prologue_dream
        with flash
        window show
        "Вот мы вместе на пристани, кружимся в танце под Луной. Мне так хорошо в его руках, он надёжный и ласковый."
        window hide
        scene cg d4_catac_un
        show prologue_dream
        with flash
        window show
        "Вместе ищем Шурика в шахтах, Сёма заступается за меня, рискуя жизнью. Настоящий мужчина."
        window hide
        scene cg aonl_cl_d4_un_knees
        show prologue_dream
        with flash
        window show
        "Сёма уставший и уязвимый лежит на моих коленях, я аккуратно глажу волосы любимого, чтобы он отдохнул."
        window hide
        $ persistent.sprite_time = "sunset"
        scene bg ext_polyana_sunset 
        show pi upset aonl_pioneer behind prologue_dream
        show prologue_dream
        with flash
        window show
        "Семён говорит: \"Ну а если и было?\""
        "Нет, Сёма ничего не было, ничего у нас не было." 
        "Я просто себе напридумывала."
        "К горлу снова подкатил ком, а сердце сжалось от боли."
        window hide
        hide prologue_dream
        scene bg ext_island_night with fade2
        $ persistent.sprite_time = "night"
        window show
        stop music fadeout 3
        "Послышались шаги."
        th "Кто-то идёт!"
        "Я замерла, надеясь остаться незамеченной, но нечаянно задела листву у ног."
        "Шаги замерли."
        scene cg d5_un_island with dissolve 
        play music aonl_disce_libens fadein 3
        un "Не подходи…"
        "Прошептала одними губами, но он услышал и замер."
        th "Не хочу, чтобы меня видели такой!"
        me "…"
        un "Не подходи."
        "Сказала я громче."
        me "Откуда ты знаешь, что это я?"
        "Так и думала."
        me "Хорошо…"
        "Он прислонился к дереву с противоположной стороны."
        me "Алиса сказала, что ты можешь быть тут."
        th "Снова Алиса!"
        un "И что?"
        me "Неудобно получилось."
        un "Ты только за этим сюда пришёл?"
        me "Нет… То есть, я не знаю."
        un "Не знаешь, но пришёл?"
        me "Да."
        un "Не стоило."
        me "Почему? Если, конечно, я мешаю…"
        un "Зачем ты за мной бегаешь?"
        me "Я…"
        me "Не подумай ничего такого."
        un "А что я должна думать?"
        me "Не знаю."
        un "Я лишь делаю выводы по твоему поведению."
        me "Я правда не знаю… Наверное, мне лучше уйти."
        un "Зачем? Раз уж пришёл…"
        me "Ладно."
        un "И?"
        me "Что?"
        un "Что Алиса сказала?"
        me "Да ничего особенного."
        un "Вот как."
        me "Да."
        un "Ну ладно тогда."
        me "Угу…"
        "Повисло молчание, настолько осязаемое, что оно буквально давило меня."
        un "Расскажи о чём-нибудь, раз уж пришёл."
        me "Я даже не знаю…"
        un "Например, про то, что было утром."
        me "А что было утром?"
        un "Ты знаешь."
        th "Расставь уже все точки над «i» и покончим с этим! "
        me "Ты же сама не хотела об этом говорить."
        un "Не хотела, а сейчас хочу!"
        "Я почувствовала, как снова подступает ком к горлу."
        "Послышалось шевеление."
        un "Не смотри!"
        me "Ладно-ладно… Но что такого?"
        un "Ничего. Просто не смотри."
        me "Хорошо, как хочешь."
        un "Значит, ничего не было?"
        me "Ну, было… Но всё это лишь глупая случайность!"
        un "Почему ты тогда так переживаешь о том, что я об этом подумаю?"
        me "Не переживаю я!"
        me "Да и вообще, ты-то сама тоже!"
        "Он явно повысил тон."
        un "Что я?"
        me "Почему тебя так волнует эта ситуация?"
        un "А кто тебе сказал, что меня волнует именно эта ситуация?"
        me "Ну а что тогда?"
        un "Ничего…"
        "Произнесла я шёпотом и тихонько заплакала."
        th "Дурак."
        me "Прости, наверное, я виноват. Но я просто не понимаю…"
        th "Да ни в чём ты не виноват, любишь другую, люби. Ко мне не лезь только."
        un "Почему…"
        me "Что?"
        un "Почему ты постоянно извиняешься? {w} За всё. {w} За то, что делал, за то, что собираешься сделать, даже за то, что ещё не сделал."
        me "Но…"
        un "Впрочем, твоё дело!"
        "Я почувствовала, как начала закипать."
        un "Извиняйся, оправдывайся! Какая мне разница…"
        me "Ладно, хорошо, я не виноват! На самом деле, я и не считаю, что в чём-то виноват, но что тогда не так? Что с тобой не так?"
        th "Со мной?! Он что, прикалывается?!"
        un "А почему со мной что-то не так?"
        me "Не знаю… Мне так кажется."
        un "Ах, кажется!"
        "Я рассмеялась, сглотнув слезы."
        un "Ты говоришь так, как будто знаешь меня."
        me "…"
        "Он промолчал."
        un "Уходи…"
        "Юноша остался на месте."
        me "Почему?"
        un "Просто уходи…"
        "Прошептала я."
        me "Не хочу."
        un "Тогда я уйду."
        me "Пойдём уж вместе."
        un "Нет."
        me "И долго ты будешь на меня обижаться? {w} За что? {w} Что вообще с тобой происходит? {w} Все ведут себя нормально, кроме тебя."
        un "Ты правда ничего не понимаешь, Алиса была права."
        me "Права в чём?"
        un "Да так…"
        "Мне всё основательно надоело."
        stop music fadeout 3
        window hide
        play ambience ambience_lake_shore_night fadeout 1 fadein 2
        scene bg ext_island_night with dissolve
        window show
        "Выждав момент я, переступив листву, прокралась к лодке и отчалила. Семён за мной не шёл, видимо не заметил."
        window hide
        stop ambience fadeout 2
        scene bg black with dissolve
        window show
        me "Ну и ладно!"
        "Донесся крик с берега, когда я была примерно на середине реки."
        th "Заметил, наконец. Да и не важно, он мне не важен!"
        window hide
        play ambience ambience_int_cabin_night fadein 2
        scene bg int_house_of_un_night with fade2
        window show
        play music music_list ["meet_me_there"] fadein 3
        "Зайдя домой я упала на кровать, накрывшись одеялом с головой и заплакала, едва сдерживая рыдания." 
        "Казалось, душа выгорела изнутри и сейчас её заполняет лишь боль и обида на себя и всё человечество."
        "Комок страданий словно распухал с каждой секундой, поглощая меня с головой, заполняя каждую клеточку тела и уродуя всё моё естество..." 
        "Порождая что-то ужасное и бесчеловечное."
        "Ненависть, боль и горе заполнили рассудок лишая последних крох разума, осталось лишь желание заставить других страдать, как страдала я!"
        th "Сёма..."
        th "За что... За что ты так со мной..."
        th "Я ведь..."
        th "Любила..."
        th "Тебя..."
        
        #Начало Мику рута!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
        #Фон полёт через облака и звёзды и фон домик лены внутри в конце
    
        $ night_time()
        $ persistent.sprite_time = "night"
    
        play music aonl_over_the_sky fadein 2
        show bg aonl_sky with fade2    
        "Меня словно подняло воздух над кроватью..." 
        "И резко швырнуло через облака..." 
        scene stars with fade2
        "Я пронеслась через звёзды и планеты..." 
        "А потом, словно камень, полетела вниз..."
        stop music fadeout 3
        window hide
        stop ambience fadeout 2
        scene bg black with fade2
        pause 3
        if aonl_classic_miku_route:
            jump aonl_miku
        jump aonl_day6_alt
        
