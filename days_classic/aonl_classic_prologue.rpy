label aonl_prologue_alt:

    call aonl_new_chapter(0, text="О девочке, чьё имя «светлая» гласит,\nРассказ начнём мы с чистого листа.\nСкромна она, грустна, угрюма внешне,\nНо внутренне прекрасна и чиста.")

    $ set_mode_adv()
    $ reload_names()
    $ prolog_time()
    play music music_list["a_promise_from_distant_days_v2"] fadein 3
    $ renpy.pause(3)
    scene anim prolog_1
    with fade3
    window show
    "Снова я вижу сон."
    "{i}Тот{/i} самый сон..."
    "Вот уже много месяцев одно и тоже."
    "Каждый раз стараюсь запомнить, как можно больше деталей, и каждый раз просыпаясь сохраняю лишь смутные образы приоткрытых, словно приглашающих куда-то ворот, рядом с которыми в камне застыли два пионера."
    "Жаль, действительно жаль."
    "Ведь здесь мне по-настоящему рады и ждут."
    "Как минимум девочка, задающая один и тот же вопрос."
    window hide
    
    scene black with fade3
    scene bg ext_camp_entrance_night
    show owl:
        pos (931, 88)
    show prologue_dream
    with fade3
    window show
    dreamgirl "Ты пойдёшь со мной?"
    "Не знаю, куда она зовёт."
    "Может это то самое место за пылающим горизонтом, где находят себя мечтатели с горящими сердцами? Или тихий ночной остров, для путника, желающего обрести долгожданный покой под сенью деревьев?"
    "А может это и не место вовсе, а {b}ПУТЬ{/b}!{w} Путь длинной в бесконечность, где встретит другая, абсолютно иная жизнь, не похожая ни на что в мире!"
    "Знать куда именно отправлюсь мне не дано, но почему-то от голоса девочки по душе разливается родное и ласковое тепло, дающее надежду. А значит, ей можно довериться! Она не обманет!"
    "И я готова! Да я пойду! Я хочу! Пусти меня, прошу!"
    "Но тело остаётся в неподвижности."
    "Но это – всего лишь сон.{w} Тот самый, который я вижу каждую ночь."
    "Разрешено лишь ответить на вопрос, стоять перед воротами, вдыхая прохладный воздух и ощущая легкий ветерок, развевающий локоны."
    "Ветер во сне?"
    "Откуда может быть ветер во сне?!"
    "Хоть кто-нибудь, хоть когда-нибудь чувствовал ветер во сне?! Когда летаешь, падаешь, или просто идёшь по комнатам замка нарисованным воображением?"
    "Если кто-то и чувствовал, то я точно не из их числа."
    "А значит это не сон, не галлюцинация и не фантазия мозга больного шизофренией. Даже у психов есть свои границы."
    "Это реальность! Реальность такая какая есть! С кирпичной кладкой на заборе, с цветами, растущими на клумбе, статуями пионеров, с которых чуть облупилась побелка и совой на воротах, словно тоже ждущей ответа."
    "И даже не просто реальность, а ГРАНИЦА! Граница между двумя мирами на которую довелось ступить по чьему-то желанию, или недосмотру!"
    "Всего один шаг, один шаг за ворота и что-то произойдёт, что-то чего никогда в жизни не случалось."   
    "Но я по-прежнему стою неподвижно, вдыхая прохладный воздух и стараясь запомнить, как можно больше деталей перед пробуждением."
    "Ведь мой ответ не важен."
    "Я соглашалась, просила, рыдала, умоляла, молила всем богам, а порой срывалась в истерике, орала как меня всё достало, как надоело, проклинала и слала к чёрту. В десятый раз, в сотый, а может уже миллионный?"
    "Но независимо от ответа итог всегда один – пробуждение дома. Смутные воспоминания и горькое чувство утраты чего-то важного. Словно сделала, что-то не так, словно повернула не туда на дороге жизни."
    "Где я ошиблась? Почему меня снова и снова водят по заколдованному кругу? Кто-нибудь, скажите! Прошу вас! Молю!"
    "Но в ответ раздаётся лишь вопрос."
    window hide
    
    with fade2
    window show
    "Может быть, спросить у неё что-то?{w} У девочки."
    "Например про книги..."
    "Хотя почему про книги? С чего я вообще взяла, что в этом мире они существуют?"
    "Что если в этом мире уже все давно знают тот самый градус по Фаренгейту, при котором возгорается бумага и в нём уже не осталось ни одной книги?"
    "Тогда мне не нужен такой мир! Нет!"
    "Хотя, такой ужасный мир вряд-ли выглядел бы так...{w} Приветливо."
    "Надо всё же спросить у девочки. Она местная, наверняка знает."
    "А впрочем, не важно..."
    window hide
    
    with fade2
    window show
    "Я подняла взгляд и встретилась глазами с птицей."
    "Совы, страшные, жуткие создания, всегда их боялась с детства."
    "Но мне не страшно. Она не причинит вреда. Ведь здесь я бессмертна. В буквальном смысле бессмертна. "
    "На голову может упасть бетонная плита, тело переехать автобус, лицо разорвать адская сова, но я останусь невредима, потому что здесь существую лишь для одной цели. Дать ответ девочке, ждущей за воротами."
    dreamgirl "Ты пойдёшь со мной?"
    "Снова вопрос. Она по-прежнему ждёт, как и всегда."
    "Можно помолчать собираясь с мыслями, простояв так день, год или тысячелетия. В моём распоряжение время всего мира."
    "Но я не готова на такие эксперименты. Нужно ответить и проснуться, вернувшись к привычной жизни."
    window hide
    $ renpy.pause(2)
    
    menu:
        "Да, я пойду с тобой, милая.":
            $ aonl_classic_miku_route = False
        "Нет, я устала, надоели, оставьте меня в покое!":
            $ aonl_classic_miku_route = True
    $ renpy.pause(1)
    
    window show
    "Ты получила, что хотела, а значит пора."
    "Мир поблек, растворяясь перед глазами."
    "Почему она меня преследует, зачем? Ведь мои слова и действия ничего не значат."
    stop music fadeout 5
    "Или значат?"
    window hide
    
    scene bg black
    with fade3
    $ renpy.pause(2)
    # play sound_loop Шерк-шерк
    #scene bg nlt_and_ressue_steps Здесь фон лестницы в подъезде. Лучше в темных тонах, она сыграет свою роль в эпилоге, когда станет светлее и уютнее. with dissolve
    #Пусть дергается как от медленного шага при каждом Шерк-Шерк, Шерк-Шерк.
    scene bg aonl_int_stairs with dissolve
    play music aonl_fortitude fadein 3
    window show
    "Шерк-Шерк, Шерк-Шерк."
    "Звук шарканья сапог на низком каблуке погружал в апатию."
    "Шерк-Шерк, Шерк-Шерк." 
    "Совсем молодая девушка, а поднимаюсь на второй этаж как дряхлая старуха."  
    "Всего-то второй этаж, а сумочка на плече весом в килограмм давит как тонна железа. Одежда словно сковывает движения, грозя запутать тело и притянув к земле опрокинуть с лестницы. Плечи опущены под тяжким грузом усталости, спина сгорблена."  
    window hide
    
    #scene bg nlt_and_ressue Фон двери. with dissolve 
    scene bg aonl_staircase with dissolve
    window show
    "Я подняла уставший взгляд от пола и посмотрела на дверь."  
    "Шерк-Шерк, Шерк-Шерк." 
    "Ещё чуть-чуть... И я дома. {w} Впрочем, стоит ли оно того?"
    window hide
    
    scene bg aonl_staircase:
        xalign 0.5 yalign 0.5 zoom 1.0
        linear 1.0 zoom 1.25
    pause 1
    # scene bg Фон темной квартиры которая в эпилоге станет чуть светлее.   
    scene bg aonl_int_lena_flat_night with fade2
    window show
    "Бросила сумочку прямо в коридоре. С трудом сняла поношенные сапоги с ноющих ног, повесила пальто на вешалку и направилась в зал." 
    "Надеть тапочки нет ни сил, ни желания." 
    "Наверняка, затяжки на колготках появятся. {w} Ну и Бог с ними." 
    "Не раздеваясь, прямо в свитере и юбке рухнула на диван, устремив взгляд в потолок."
    window hide
    
    #scene bg with dissolve Фон потолка. Временный вариант, взят из оригинала
    scene bg aonl_int_lena_flat_ceiling with dissolve
    window show
    "Как же я устала. Как же устала бороться за жизнь."
    window hide
    
    show blink
    pause 1
    window show
    "Хочется лежать и ни о чём не думать, но нужно встать. Нужно встать и хоть немного поесть."
    window hide
    
    hide blink
    #scene bg nlt_and_ressue_un_room
    show unblink
    pause 2
    scene bg aonl_int_lena_flat_night with dissolve
    window show
    "Я села на кровати, притянула коленки к груди, обхватив их руками и медленно осмотрелась."
    "А ведь моя жизнь не так и плоха. Пусть маленькая, но своя однушка в хрущёвке. Квартирка вполне чистенькая, я постоянно убираюсь и подклеиваю отошедшие обои. Пускай ко мне никто и не заходит, чтобы оценить."
    "Снова подняла взгляд к потолку."
    window hide
    
    #scene bg with dissolve Фон потолка.
    scene bg aonl_int_lena_flat_ceiling with dissolve
    window show
    "Ну почему? Ну почему несмотря на чистоту, я словно не в квартире, а в гробу, который вот-вот раздавит под тяжестью земли?"
    "Повернув голову набок, положила её на колени и по щеке поползла одинокая, скупая слеза."   
    "Может не стоит убираться, ведь это никому не нужно, да и мне не легче?"    
    "Я замотала головой, встав с дивана."
    window hide
        
    #scene nlt_and_ressue_un_room with dissolve
    scene bg aonl_int_lena_flat_night with dissolve
    window show
    "Нет-нет, нельзя зарастать грязью, нельзя. Это будет последний шаг перед… Перед…"   
    "Как следует встряхнула головой отгоняя не прошеные мысли."    
    "Нужно выпить тёплого ромашкового чая, а потом почитать милую и романтичную книгу, лёжа в ванной."
    "Сегодня 31 декабря. Не стоит омрачать праздник. Приободрюсь, а завтра посмотрим, что будет."
    "Поставив чайник на газ, вернулась в зал подтянула ножки к груди, обхватив руками колени и устремила взгляд на мольберт в углу."
    window hide
    
    scene bg aonl_int_un_stud with dissolve
    #scene cg aonl_molbert мольберта, потом на нём в зависимости от рутов нужно будет изобразить или ворота лагеря, или Семёна с Леной в кресле, как в руте Лены в конце. with dissolve 
    window show
    "Пустой чистый холст."
    "Вот уже много месяцев не могу ничего написать. Каждое утро бегу к мольберту, пытаясь запечатлеть свой сон. Иногда мерещатся ворота, иногда летний остров на озере, а иногда жуткие шахты. Но чтобы мне ни снилось не получается сделать даже мельчайшего штриха. " 
    "Словно рукам запрещали рисовать. Картины, образы роятся в голове, но они слишком абстрактны и расплывчаты, взгляду совсем не за что уцепиться."
    "Почему же мне каждую ночь снится одно и тоже, а потом забывается?" 
    "Может это психическое расстройство?"
    window hide
    
    scene bg aonl_int_lena_flat_night with dissolve
    # play sound Телефон 
    #Фон комнаты и спрайт телефона с надписью «начальница». (А нахера создавать отдельный BG? Не проще заказать дешевую рисовку мобильного телефона и использовать его как обычный спрайт. Таким способом хорошо игрались в Дубликате со столами)
    window show
    un "Эхх."
    un "Ало."
    aonl_boss "Тихонова, ты дома?"    
    un "Ну да, всё закончила в офисе."
    aonl_boss "Я забыла отправить документ по почте новому заказчику, он прямо на рабочем столе, не ошибешься, съезди в офис и перешли."
    "На мгновение потеряла дар речи."
    un "Ну ведь… Рабочий день уже закончен и у вас есть машина…"
    aonl_boss "Тихонова, ты всерьёз предлагаешь бросить мои дела и ехать назад?"
    "Интересно, она помнит моё имя?"
    un "Ну я…"
    aonl_boss "У меня забот перед праздником полно, а ты? Или хочешь сказать, что кто-то ждёт дома?"
    "В последней фразе читалась нескрываемая издевка, от чего слегка задрожало левое веко."
    un "Н-нет, просто…"
    aonl_boss "Вот и отлично, документы нужно отправить до завтра, как вышлешь позвони."
    "Она бросила трубку не дожидаясь ответа."
    #hide Убрать спрайт телефона.
    "Минуту я стояла со стеклянным взглядом смотря перед собой."
    "Потом, шмыгнув носом, выключила газ и пошла собираться. Хорошо хоть не разделась."
    window hide
    stop music fadeout 3
    
    #scene bg nlt_and_ressue_steps with dissolve
    scene bg aonl_ext_winter_street_night with dissolve
    pause 1
    play ambience aonl_winter_loop fadein 2
    # play sound_loop sfx_gusty_wind fadein 2
    # звук метели
    window show
    "Город встретил лёгким снегопадом. Но я не обращала внимания на складываемые им узоры, а устремила взгляд в землю и шаркающей походкой поспешила на автобус." 
    "Может, успею к полуночи вернуться назад? {w} А зачем?"
    "Пока шла на остановку, в голове свербела одна мысль."
    window hide
    
    scene cg aonl_lena_winter_prolog with dissolve
    window show
    "Почему я? Почему я? Почему я?"
    "Почему именно я работала сегодня допоздна, в то время как сотрудники праздновали в конференц-зале?" 
    "Почему именно я исправляла чужие ошибки в отчёте, а никто из коллег даже оливье с картошкой не принёс угостить? Да и заметили ли они отсутствие серой офисной мыши на празднике? "
    "И почему именно я иду на работу в личное время, когда другие сидят в тёплых уютных домах и готовятся встретить наступление нового года?"
    "Почему же?! Почему же я не сказала НЕТ?!"
    window hide
    
    scene bg bus_stop with dissolve
    window show
    "Облокотившись о стенку остановки закрыла глаза, вспоминая прошлое."
    "Есть ли в нём светлое пятно?"
    "Нет."
    "Практически нет."
    window hide
    pause 1
    
    $ set_mode_nvl()
    window show
    "Сколько помню, всегда вела себя тихо и робко. Наверное, если бы проводился эксперимент по нахождению истинного серого цвета, моя жизнь вполне подошла бы под эталон. На мне постоянно ездят, постоянно используют. Но не могу сказать, что это как-то давило или мешало, вовсе нет. Я свыклась с обыденностью, и постоянство стало ключом к внутреннему спокойствию. В жизни мало проявляла эмоции, всё держала в себе, разве что написание картин позволяло выражать мысли на холсте." 
    "Из-за отрешённости от общества многие называли меня странной и не слишком уж торопились понимать. Иногда это доставляло неприятности, ведь всегда найдутся те, для кого необычность человека — нечто сродни неполноценности, что даёт повод для издевательств. Особенно в школе. В конечном счёте замкнулась в себе окончательно, стараясь никого не подпускать близко. Со временем одиночество стало нормой, и я уже смирилась с тем, что всю жизнь проведу, прячась в коконе внутренней самозащиты, который создала вокруг себя, дабы никто и никогда не узнал то, о чём думаю, чем живу и о чём мечтаю." 
    "Я попросту разочаровалась в людях и уже не верила, что появится хоть кто-то, кто сможет понять меня и принять. Так и жила — простая, тихая, серая мышка… {w} Лена Тихонова."
    window hide
    
    scene bg bus_stop with dissolve
    play sound sfx_intro_bus_engine_start
    $ renpy.pause(3)
    play sound_loop sfx_intro_bus_engine_loop fadein 3
    $ set_mode_adv()
    scene anim intro_9
    with fade2
    window show
    "Звук автобуса вернул к реальности."
    "Я суетливо поправила одежду и направилась в салон."
    "Какой-то он странный, раньше не видела. Хотя какая разница, по этому маршруту ходит только 410-ый."
    window hide
    
    scene bg bus_stop:
        xzoom -1
    show pi normal aonl_coat far at cright
    with dissolve
    #Фон остановки с Семёном. тут нужно отразить зеркально остановку и на ней пририсовать спрайт семёна в зимней одежде - не понял ничего
    window show
    "Заходя внутрь заметила на противоположной остановке мужчину с печальным взглядом." 
    "Кажется, мы уже виделись... Но где? Может во дворе? А, впрочем, не важно, нужно идти."
    window hide
    $ renpy.pause(2)

    scene cg aonl_lena_winter_prolog_2 with fade
    play sound sfx_intro_bus_door_open

    $ renpy.pause(3, hard=True)
    scene anim intro_11
    with fade
    $ renpy.pause(1, hard=True)

    stop sound_loop fadeout 4
    scene anim intro_13
    with fade2
    $ renpy.pause(3, hard=True)

    scene bg intro_xx with fade
    stop ambience fadeout 2
    play sound_loop sfx_bus_interior_moving fadein 4
    window show
    "Автобус оказался полностью свободен."
    "Оно и понятно, люди разбрелись по домам и магазинам, не то что я."
    "Встряхнула головой отгоняя непрошенные мысли."
    $ volume(0.5, "music")
    play music music_list["lightness_radio_bus"] fadein 7
    "Села на последнее сидение и уперлась лбом в окно, устремив взгляд наружу, чтобы хоть как-то забыть о печали."
    "Не помогло." 
    "За окном периодически мелькали парочки, женщины с колясками, пары с детьми. Говорят, дети — это очень тяжело." 
    "Смешно."
    "Если бы только эти умники знали, как тяжело, когда их нет, когда {b}НИКОГО{/b} нет."
    "В автобусе играла тихая, душевная мелодия, облегчая душу и пробуждая давно забытые воспоминания."
    "Новый год - время чудес."
    "Может и для меня одно найдётся? Может моя жизнь наладится?"
    "На глаза вновь навернулись слёзы."
    window hide
    stop music fadeout 3
    
    show blink
    $ renpy.pause(1.5)
    window show
    "Я опустила веки, смахивая их, и зашептала."
    un "Господи, если ты только есть, помоги."
    window hide
    stop sound_loop fadeout 3
    scene bg black
    with fade3
    $ renpy.pause(3)
    $ volume(1.0, "music")
    jump aonl_opening
    #Опенинг будет новый!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
label aonl_opening:

    $ renpy.movie_cutscene("video/opening.ogv") #!!!!!!!!!!!!!!!!!!!!!!!!!!

    jump aonl_day1_alt

    